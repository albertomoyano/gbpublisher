' Gambas module file

Public RutaProyecto As String = File.Dir(FMain.txtProyecto.Text)
Private hBibToJson As New Collection

' --- Escapa cadenas para JSON ---
Private Function EscapeJSON(value As String) As String

  Dim resultado As String = value

  If IsNull(value) Or value = "" Then Return ""

  ' Escapar caracteres especiales en el orden correcto
  resultado = Replace(resultado, "\\", "\\\\")     ' \ -> \\
  resultado = Replace(resultado, "\"", "\\\"")     ' " -> \"
  resultado = Replace(resultado, Chr(8), "\\b")    ' Backspace -> \b
  resultado = Replace(resultado, Chr(12), "\\f")   ' Form feed -> \f
  resultado = Replace(resultado, Chr(10), "\\n")   ' Line feed -> \n
  resultado = Replace(resultado, Chr(13), "\\r")   ' Carriage return -> \r
  resultado = Replace(resultado, Chr(9), "\\t")    ' Tab -> \t

  Return resultado

End

Public Sub ImportarJSONsiglas()

  Dim sJSON As String
  Dim aJSON As Collection[]
  Dim item As Collection
  Dim inputFile As String
  Dim sql As String
  Dim registrosImportados As Integer = 0

  With Dialog
    .Title = "Seleccionar archivo de siglas JSON"
    .Filter = ["*.json", "Archivos JSON", "*", "Todos los archivos"]
    .Path = User.Home
  End With
  If Dialog.OpenFile() Then Return
  inputFile = Dialog.Path

  sJSON = File.Load(inputFile)
  If Trim(sJSON) = "" Then
    m_Sonido.sonar("Error")
    Message.Error("El archivo JSON está vacío.")
    Return
  Endif

  aJSON = JSON.Decode(sJSON)
  If aJSON.Count = 0 Then
    m_Sonido.sonar("Info")
    Message.Info("No se encontraron registros para importar.")
    Return
  Endif

  m_Sonido.sonar("Question")
  If Message.Question("¿Está seguro de importar " & aJSON.Count & " siglas?" & gb.NewLine &
      "Se asignarán al proyecto actual (ID: " & FMain.id_proyecto.Text & ")") <> 1 Then
    Return
  Endif

  m_ConexionBD.mConn.Begin()

  For Each item In aJSON
    Dim vIdRevista As String = FMain.id_proyecto.Text
    Dim v1, v2, v3, v4, v5, v6, v7, v8, v9 As String

    v1 = ""
    v2 = ""
    v3 = ""
    v4 = ""
    v5 = ""
    v6 = ""
    v7 = ""
    v8 = ""
    v9 = ""

    If item.Exist("clave_glosario") Then v1 = CStr(item["clave_glosario"])
    If item.Exist("tipo_de_entrada_glosario") Then v2 = CStr(item["tipo_de_entrada_glosario"])
    If item.Exist("name_glosario") Then v3 = CStr(item["name_glosario"])
    If item.Exist("descripcion_glosario") Then v4 = CStr(item["descripcion_glosario"])
    If item.Exist("first_glosario") Then v5 = CStr(item["first_glosario"])
    If item.Exist("text_glosario") Then v6 = CStr(item["text_glosario"])
    If item.Exist("first_plural_glosario") Then v7 = CStr(item["first_plural_glosario"])
    If item.Exist("plural_glosario") Then v8 = CStr(item["plural_glosario"])
    If item.Exist("sort_glosario") Then v9 = CStr(item["sort_glosario"])

    v1 = Replace(v1, "'", "''")
    v2 = Replace(v2, "'", "''")
    v3 = Replace(v3, "'", "''")
    v4 = Replace(v4, "'", "''")
    v5 = Replace(v5, "'", "''")
    v6 = Replace(v6, "'", "''")
    v7 = Replace(v7, "'", "''")
    v8 = Replace(v8, "'", "''")
    v9 = Replace(v9, "'", "''")

    sql = "INSERT INTO siglas (id_proyecto, clave_glosario, tipo_de_entrada_glosario, name_glosario, descripcion_glosario, first_glosario, text_glosario, first_plural_glosario, plural_glosario, sort_glosario) VALUES ("
    sql = sql & vIdRevista & ", "
    sql = sql & "'" & v1 & "', "
    sql = sql & "'" & v2 & "', "
    sql = sql & "'" & v3 & "', "
    sql = sql & "'" & v4 & "', "
    sql = sql & "'" & v5 & "', "
    sql = sql & "'" & v6 & "', "
    sql = sql & "'" & v7 & "', "
    sql = sql & "'" & v8 & "', "
    sql = sql & "'" & v9 & "')"

    Try m_ConexionBD.mConn.Exec(sql)
    If Error Then
      ' Print "✗ Error en INSERT: " & Error.Text
      Error.Raise("Error en INSERT: " & Error.Text)
    Else
      registrosImportados += 1

      Dim verificacion As Result
      Try verificacion = m_ConexionBD.mConn.Exec("SELECT * FROM siglas WHERE id_proyecto = " & FMain.id_proyecto.Text & " ORDER BY id DESC LIMIT 1")
      If Not Error And verificacion.Available Then
      Endif
    Endif
  Next

  m_ConexionBD.mConn.Commit()

  m_Sonido.sonar("Info")
  Message.Info("Importación completada correctamente." & gb.NewLine &
    registrosImportados & " siglas importadas desde: " & File.Name(inputFile) & gb.NewLine &
    "Proyecto ID: " & FMain.id_proyecto.Text)

Catch
  m_ConexionBD.mConn.Rollback()
  m_Sonido.sonar("Error")
  Message.Error("Error durante la importación: " & Error.Text)

End

Public Sub ExportarJSONsiglas()

  Dim r As Result
  Dim sJSON As String
  Dim firstRow As Boolean = True

  ' 1) Obtener registros del proyecto actual
  r = m_ConexionBD.mConn.Exec("SELECT * FROM siglas WHERE id_proyecto = &1 ORDER BY id", FMain.id_proyecto.Text)

  If r.Count = 0 Then
    m_Sonido.sonar("Info")
    Message.Info("No se encontraron referencias para exportar.")
    Return
  Endif

  ' 2) Construir JSON con formato legible
  sJSON = "[" & gb.NewLine

  For Each r
    If Not firstRow Then sJSON &= "," & gb.NewLine
    sJSON &= "  {" & gb.NewLine

    ' Exportar solo los campos relevantes con los nuevos nombres
    sJSON &= "    \"clave_glosario\": \"" & EscapeJSON(CStr(r["clave_glosario"])) & "\"," & gb.NewLine
    sJSON &= "    \"tipo_de_entrada_glosario\": \"" & EscapeJSON(CStr(r["tipo_de_entrada_glosario"])) & "\"," & gb.NewLine
    sJSON &= "    \"name_glosario\": \"" & EscapeJSON(CStr(r["name_glosario"])) & "\"," & gb.NewLine
    sJSON &= "    \"descripcion_glosario\": \"" & EscapeJSON(CStr(r["descripcion_glosario"])) & "\"," & gb.NewLine
    sJSON &= "    \"first_glosario\": \"" & EscapeJSON(CStr(r["first_glosario"])) & "\"," & gb.NewLine
    sJSON &= "    \"text_glosario\": \"" & EscapeJSON(CStr(r["text_glosario"])) & "\"," & gb.NewLine
    sJSON &= "    \"first_plural_glosario\": \"" & EscapeJSON(CStr(r["first_plural_glosario"])) & "\"," & gb.NewLine
    sJSON &= "    \"plural_glosario\": \"" & EscapeJSON(CStr(r["plural_glosario"])) & "\"," & gb.NewLine
    sJSON &= "    \"sort_glosario\": \"" & EscapeJSON(CStr(r["sort_glosario"])) & "\"" & gb.NewLine

    sJSON &= "  }"
    firstRow = False
  Next

  sJSON &= gb.NewLine & "]"

  ' GUARDAR ARCHIVO
  Dim outputFile As String = RutaProyecto & "/files/gls-json_" & File.BaseName(FMain.txtProyecto.Text) & ".json"

  ' Crear directorio si no existe
  If Not Exist(File.Dir(outputFile)) Then
    Mkdir File.Dir(outputFile)
  Endif

  File.Save(outputFile, sJSON)
  m_Sonido.sonar("Info")
  Message.Info("Exportación completada correctamente." & gb.NewLine & outputFile)

Catch
  m_Sonido.sonar("Error")
  Message.Error("Error durante la exportación: " & Error.Text)

End

Public Sub ExportarBibTeX2JSON() As Boolean

  Dim f As ResultField
  Dim aFields As New String[]
  Dim i As Integer
  Dim sResultado As Result
  Dim filtrarBib As String
  Dim aEntradas As New Collection[]
  Dim dEntrada As Collection
  Dim sCampo As String
  Dim sJSON As String
  Dim outputFile As String

  ' OBTENER DATOS DEL PROYECTO
  filtrarBib = "select * from bibtex where id_proyecto = " & FMain.id_proyecto.Text
  sResultado = m_ConexionBD.mConn.Exec(filtrarBib)

  ' OBTENER NOMBRES DE CAMPOS
  aFields.Clear
  For Each f In sResultado.Fields
    aFields.Add(f.Name)
  Next

  ' PROCESAR CADA REGISTRO
  While sResultado.Available
    dEntrada = New Collection

    ' PROCESAR TODOS LOS CAMPOS EXCEPTO ID_PROYECTO, ID E ID_ARTICULO
    For i = 0 To aFields.Max
      sCampo = aFields[i]

      ' SALTAR LOS CAMPOS ID, ID_PROYECTO E ID_ARTICULO
      If sCampo <> "id_proyecto" And sCampo <> "id" And sCampo <> "id_articulo" Then
        If Not IsNull(sResultado[sCampo]) Then
          ' PARA CAMPOS NUMÉRICOS VERIFICAR QUE NO SEAN 0 O VACÍOS
          If TypeOf(sResultado[sCampo]) = gb.Integer Or TypeOf(sResultado[sCampo]) = gb.Long Then
            dEntrada[sCampo] = sResultado[sCampo]
            ' PARA CAMPOS DE TEXTO VERIFICAR QUE TENGAN CONTENIDO
          Else If Len(sResultado[sCampo]) > 0 Then
            dEntrada[sCampo] = sResultado[sCampo]
          Endif
        Endif
      Endif
    Next

    aEntradas.Add(dEntrada)
    sResultado.MoveNext
  Wend

  ' CREAR ESTRUCTURA JSON CON METADATOS
  Dim dFinal As New Collection
  Dim fecha As String

  fecha = Format(Now, "dd/mm/yyyy hh:nn:ss")

  dFinal["entries"] = aEntradas
  dFinal["metadata"] = New Collection
  dFinal["metadata"]["generator"] = "gbpublisher"
  dFinal["metadata"]["databaseType"] = "mysql"
  dFinal["metadata"]["creationDate"] = fecha
  dFinal["metadata"]["tableName"] = "bibtex"

  ' CONVERTIR A JSON Y FORMATEAR
  sJSON = JSON.Encode(dFinal)
  sJSON = FormatearJSON(sJSON)

  ' GUARDAR ARCHIVO
  outputFile = File.Dir(FMain.txtProyecto.Text) & "/files/" & File.BaseName(FMain.txtProyecto.Text) & "_db.json"
  File.Save(outputFile, sJSON)

  ' VERIFICAR QUE EL ARCHIVO SE HAYA CREADO
  If Exist(outputFile) Then
    Return True
  Else
    Return False
  Endif

End

Private Function FormatearJSON(jsonCompacto As String) As String

  Dim resultado As String
  Dim nivel As Integer = 0
  Dim i As Integer
  Dim c As String
  Dim enCadena As Boolean = False
  Dim escapado As Boolean = False

  For i = 1 To Len(jsonCompacto)
    c = Mid(jsonCompacto, i, 1)

    ' DETECTAR SI ESTAMOS DENTRO DE UNA CADENA
    If c = "\"" And Not escapado Then
      enCadena = Not enCadena
    Endif

    ' MANEJAR CARACTERES DE ESCAPE
    If c = "\\" And Not escapado Then
      escapado = True
    Else
      escapado = False
    Endif

    ' SOLO FORMATEAR SI NO ESTAMOS DENTRO DE UNA CADENA
    If Not enCadena Then
      Select Case c
        Case "{"
          resultado &= c & "\n" & String(2 * (nivel + 1), " ")
          nivel += 1
        Case "}"
          nivel -= 1
          resultado &= "\n" & String(2 * nivel, " ") & c
        Case "["
          resultado &= c & "\n" & String(2 * (nivel + 1), " ")
          nivel += 1
        Case "]"
          nivel -= 1
          resultado &= "\n" & String(2 * nivel, " ") & c
        Case ","
          resultado &= c & "\n" & String(2 * nivel, " ")
        Case ":"
          resultado &= c & " "
        Default
          resultado &= c
      End Select
    Else
      resultado &= c
    Endif
  Next

  Return resultado

End

Public Sub ImportarJSON() As Boolean

  Dim jsonFile As String
  Dim jsonContent As String
  Dim dDatos As Collection
  Dim aEntradas As Collection[]
  Dim entrada As Collection
  Dim sCampo As String
  Dim sValor As Variant
  Dim sCampos As String
  Dim sValores As String
  Dim sQuery As String
  Dim contador As Integer = 0
  Dim aClavesEntrada As String[]
  Dim i As Integer

  ' ABRIR DIÁLOGO PARA SELECCIONAR ARCHIVO JSON
  Dialog.Title = "Seleccionar archivo JSON para importar"
  Dialog.Filter = ["*.json", "Archivos JSON"]

  If Not Dialog.OpenFile() Then
    jsonFile = Dialog.Path
  Else
    Return False
  Endif

  If Not Exist(jsonFile) Then
    Message.Error("El archivo seleccionado no existe")
    Return False
  Endif

  jsonContent = File.Load(jsonFile)

  dDatos = JSON.Decode(jsonContent)

  If Not dDatos.Exist("entries") Then
    Message.Error("El archivo JSON no tiene la estructura correcta (falta 'entries')")
    Return False
  Endif

  aEntradas = dDatos["entries"]

  For Each entrada In aEntradas
    sCampos = "id_articulo"
    sValores = "0"

    ' OBTENER LAS CLAVES DE LA ENTRADA
    aClavesEntrada = entrada.Keys

    ' ITERAR SOBRE LAS CLAVES
    For i = 0 To aClavesEntrada.Max
      sCampo = aClavesEntrada[i]
      sValor = entrada[sCampo]
      sCampos &= ", " & sCampo

      If TypeOf(sValor) = gb.String Then
        sValor = Replace(sValor, "'", "''")
        sValores &= ", '" & sValor & "'"
      Else
        sValores &= ", " & CStr(sValor)
      Endif
    Next

    sCampos &= ", id_proyecto"
    sValores &= ", " & FMain.id_proyecto.Text

    sQuery = "INSERT INTO bibtex (" & sCampos & ") VALUES (" & sValores & ")"

    m_ConexionBD.mConn.Exec(sQuery)
    contador += 1
  Next

  If contador > 0 Then
    Return True
  Else
    Return False
  Endif

End

' EXPORTAMOS EL ARCHIVO METADATOS EN JSON PARA EL LIBRO PDF DESDE LATEX
Public Sub ExportarMetadatosJSON2PDF() As Boolean

  Dim sResultado As Result
  Dim filtrarMeta As String
  Dim dMetadatos As New Collection
  Dim sJSON As String
  Dim outputFile As String

  ' OBTENER DATOS DEL PROYECTO
  filtrarMeta = "select * from metadatos where id_proyecto = " & FMain.id_proyecto.Text
  sResultado = m_ConexionBD.mConn.Exec(filtrarMeta)

  ' VERIFICAR SI HAY RESULTADOS
  If Not sResultado.Available Then
    Return False
  Endif

  ' AGREGAR CAMPOS ESPECÍFICOS SOLO SI TIENEN VALOR
  If Not IsNull(sResultado["revista_titulo"]) And Len(Trim(sResultado["revista_titulo"])) > 0 Then
    dMetadatos["title"] = sResultado["revista_titulo"]
  Endif

  If Not IsNull(sResultado["libro_subtitulo"]) And Len(Trim(sResultado["libro_subtitulo"])) > 0 Then
    dMetadatos["subtitle"] = sResultado["libro_subtitulo"]
  Endif

  If Not IsNull(sResultado["libro_autoria"]) And Len(Trim(sResultado["libro_autoria"])) > 0 Then
    dMetadatos["authors"] = sResultado["libro_autoria"]
  Endif
  '
  If Not IsNull(sResultado["libro_editor"]) And Len(Trim(sResultado["libro_editor"])) > 0 Then
    dMetadatos["editors"] = sResultado["libro_editor"]
  Endif
  '
  If Not IsNull(sResultado["libro_asunto"]) And Len(Trim(sResultado["libro_asunto"])) > 0 Then
    dMetadatos["subject"] = sResultado["libro_asunto"]
  Endif
  '
  If Not IsNull(sResultado["libro_palabras_clave_es"]) And Len(Trim(sResultado["libro_palabras_clave_es"])) > 0 Then
    dMetadatos["keywords"] = sResultado["libro_palabras_clave_es"]
  Endif
  '
  If Not IsNull(sResultado["libro_resumen_es"]) And Len(Trim(sResultado["libro_resumen_es"])) > 0 Then
    dMetadatos["abstract"] = sResultado["libro_resumen_es"]
  Endif
  '
  If Not IsNull(sResultado["revista_idioma"]) And Len(Trim(sResultado["revista_idioma"])) > 0 Then
    dMetadatos["language"] = sResultado["revista_idioma"]
  Endif
  '
  If Not IsNull(sResultado["revista_editorial"]) And Len(Trim(sResultado["revista_editorial"])) > 0 Then
    dMetadatos["publisher"] = sResultado["revista_editorial"]
  Endif
  '
  If Not IsNull(sResultado["fecha_publicacion"]) Then
    dMetadatos["publication_date"] = Format(sResultado["fecha_publicacion"], "yyyy")
  Endif
  '
  If Not IsNull(sResultado["revista_doi"]) And Len(Trim(sResultado["revista_doi"])) > 0 Then
    dMetadatos["doi"] = sResultado["revista_doi"]
  Endif
  '
  If Not IsNull(sResultado["libro_isbn"]) And Len(Trim(sResultado["libro_isbn"])) > 0 Then
    dMetadatos["isbn"] = sResultado["libro_isbn"]
  Endif
  '
  If Not IsNull(sResultado["licencia"]) And Len(Trim(sResultado["licencia"])) > 0 Then
    dMetadatos["license"] = sResultado["licencia"]
  Endif
  '
  If Not IsNull(sResultado["copyright_holder"]) And Len(Trim(sResultado["copyright_holder"])) > 0 Then
    dMetadatos["copyright_holder"] = sResultado["copyright_holder"]
  Endif
  '
  If Not IsNull(sResultado["copyright_year"]) And Len(Trim(sResultado["copyright_year"])) > 0 Then
    dMetadatos["copyright_year"] = sResultado["copyright_year"]
  Endif
  '
  If Not IsNull(sResultado["revista_volumen"]) And Len(Trim(sResultado["revista_volumen"])) > 0 Then
    dMetadatos["volume"] = sResultado["revista_volumen"]
  Endif
  '
  If Not IsNull(sResultado["libro_paginas"]) And Len(Trim(sResultado["libro_paginas"])) > 0 Then
    dMetadatos["pages"] = sResultado["libro_paginas"]
  Endif
  '
  If Not IsNull(sResultado["libro_serie"]) And Len(Trim(sResultado["libro_serie"])) > 0 Then
    dMetadatos["series"] = sResultado["libro_serie"]
  Endif
  '
  If Not IsNull(sResultado["revista_url"]) And Len(Trim(sResultado["revista_url"])) > 0 Then
    dMetadatos["url"] = sResultado["revista_url"]
  Endif
  '
  If Not IsNull(sResultado["funding_source"]) And Len(Trim(sResultado["funding_source"])) > 0 Then
    dMetadatos["funding"] = sResultado["funding_source"]
  Endif
  '
  dMetadatos["creator"] = "gbpublisher"
  '
  ' ' AGREGAR CLASSIFICATIONS SI EXISTEN
  ' Dim dClassifications As New Collection
  ' Dim tieneClassifications As Boolean = False
  '
  ' If Not IsNull(sResultado["thema"]) And Len(Trim(sResultado["thema"])) > 0 Then
  '   dClassifications["thema"] = sResultado["thema"]
  '   tieneClassifications = True
  ' Endif
  '
  ' If Not IsNull(sResultado["bisac"]) And Len(Trim(sResultado["bisac"])) > 0 Then
  '   dClassifications["bisac"] = sResultado["bisac"]
  '   tieneClassifications = True
  ' Endif
  '
  ' If Not IsNull(sResultado["dewey"]) And Len(Trim(sResultado["dewey"])) > 0 Then
  '   dClassifications["dewey"] = sResultado["dewey"]
  '   tieneClassifications = True
  ' Endif
  '
  ' If tieneClassifications Then
  '   dMetadatos["classifications"] = dClassifications
  ' Endif

  ' CONVERTIR A JSON Y FORMATEAR
  sJSON = JSON.Encode(dMetadatos)
  sJSON = FormatearJSON(sJSON)

  ' GUARDAR ARCHIVO
  outputFile = File.Dir(FMain.txtProyecto.Text) & "/files/metadata.json"
  File.Save(outputFile, sJSON)

  ' VERIFICAR QUE EL ARCHIVO SE HAYA CREADO
  If Exist(outputFile) Then
    Return True
  Else
    Return False
  Endif

End
'
'
'
'

Public Sub ExportarCSLJSON() As Boolean

  Dim f As ResultField
  Dim aFields As New String[]
  Dim i As Integer
  Dim sResultado As Result
  Dim filtrarBib As String
  Dim aEntradas As New Collection[]
  Dim dEntrada As Collection
  Dim sCampo As String
  Dim sJSON As String
  Dim outputFile As String

  ' OBTENER DATOS DEL PROYECTO
  filtrarBib = "select * from bibtex where id_proyecto = " & FMain.id_proyecto.Text
  sResultado = m_ConexionBD.mConn.Exec(filtrarBib)

  ' OBTENER NOMBRES DE CAMPOS
  aFields.Clear
  For Each f In sResultado.Fields
    aFields.Add(f.Name)
  Next

  ' PROCESAR CADA REGISTRO
  While sResultado.Available
    dEntrada = New Collection

    ' MAPEAR CAMPOS BIBTEX A CSL-JSON
    ' ID (usar el campo clave, aFields[4] según tu código)
    If Not IsNull(sResultado[aFields[4]]) Then
      dEntrada["id"] = sResultado[aFields[4]]
    Endif

    ' TYPE - Convertir tipo de entrada BibTeX a CSL
    If Not IsNull(sResultado["tipo_de_entrada"]) Then
      dEntrada["type"] = ConvertirTipoCSL(sResultado["tipo_de_entrada"])
    Endif

    ' PROCESAR CAMPOS SEGÚN MAPEO CSL
    For i = 0 To aFields.Max
      sCampo = aFields[i]

      ' SALTAR CAMPOS ESPECIALES YA PROCESADOS
      If sCampo = "id_proyecto" Or sCampo = "id" Or sCampo = "id_articulo" Or sCampo = "tipo_de_entrada" Or i = 4 Then
        Continue
      Endif

      If Not IsNull(sResultado[sCampo]) And Len(Trim(sResultado[sCampo])) > 0 Then
        MapearCampoCSL(dEntrada, sCampo, sResultado[sCampo])
      Endif
    Next

    aEntradas.Add(dEntrada)
    sResultado.MoveNext
  Wend

  ' CONVERTIR A JSON Y FORMATEAR
  sJSON = JSON.Encode(aEntradas)
  sJSON = FormatearJSON(sJSON)

  ' GUARDAR ARCHIVO
  outputFile = File.Dir(FMain.txtProyecto.Text) & "/files/" & File.BaseName(FMain.txtProyecto.Text) & "_csl.json"
  File.Save(outputFile, sJSON)

  ' VERIFICAR QUE EL ARCHIVO SE HAYA CREADO
  If Exist(outputFile) Then
    Return True
  Else
    Return False
  Endif

End

' FUNCIÓN AUXILIAR: CONVERTIR TIPO DE ENTRADA BIBTEX A CSL
Private Function ConvertirTipoCSL(sTipoBibTeX As String) As String

  Dim mapeo As New Collection

  ' MAPEO DE TIPOS BIBTEX/BIBLATEX A CSL
  mapeo["article"] = "article-journal"
  mapeo["book"] = "book"
  mapeo["booklet"] = "pamphlet"
  mapeo["inbook"] = "chapter"
  mapeo["incollection"] = "chapter"
  mapeo["inproceedings"] = "paper-conference"
  mapeo["conference"] = "paper-conference"
  mapeo["manual"] = "book"
  mapeo["mastersthesis"] = "thesis"
  mapeo["phdthesis"] = "thesis"
  mapeo["proceedings"] = "book"
  mapeo["techreport"] = "report"
  mapeo["unpublished"] = "manuscript"
  mapeo["misc"] = "article"
  mapeo["online"] = "webpage"
  mapeo["patent"] = "patent"
  mapeo["report"] = "report"
  mapeo["thesis"] = "thesis"
  mapeo["collection"] = "book"
  mapeo["dataset"] = "dataset"
  mapeo["software"] = "software"
  mapeo["artwork"] = "graphic"
  mapeo["audio"] = "song"
  mapeo["video"] = "motion_picture"
  mapeo["movie"] = "motion_picture"
  mapeo["legislation"] = "legislation"
  mapeo["jurisdiction"] = "legal_case"

  If mapeo.Exist(Lower(sTipoBibTeX)) Then
    Return mapeo[Lower(sTipoBibTeX)]
  Else
    Return "article"
  Endif

End

' FUNCIÓN AUXILIAR: MAPEAR CAMPO BIBTEX A CSL
Private Sub MapearCampoCSL(dEntrada As Collection, sCampo As String, valor As Variant)

  Dim sCampoLower As String = Lower(sCampo)

  Select Case sCampoLower
      ' CAMPOS DE TEXTO DIRECTO - TÍTULO Y VARIANTES
    Case "title"
      dEntrada["title"] = valor
    Case "subtitle", "sub_title"
      ' Combinar con title si ya existe
      If dEntrada.Exist("title") Then
        dEntrada["title"] = dEntrada["title"] & ": " & valor
      Else
        dEntrada["title"] = valor
      Endif
    Case "shorttitle", "short_title"
      dEntrada["title-short"] = valor
    Case "title_addon"
      ' Añadir al título principal
      If dEntrada.Exist("title") Then
        dEntrada["title"] = dEntrada["title"] & " " & valor
      Endif
    Case "main_title"
      dEntrada["container-title"] = valor
    Case "main_title_addon"
      If dEntrada.Exist("container-title") Then
        dEntrada["container-title"] = dEntrada["container-title"] & " " & valor
      Endif
    Case "index_title"
      dEntrada["title-short"] = valor
    Case "reprint_title"
      dEntrada["original-title"] = valor

      ' IDENTIFICADORES
    Case "doi"
      dEntrada["DOI"] = valor
    Case "isbn"
      dEntrada["ISBN"] = valor
    Case "issn"
      dEntrada["ISSN"] = valor
    Case "ismn"
      dEntrada["ISMN"] = valor
    Case "isrn"
      dEntrada["call-number"] = valor
    Case "url"
      dEntrada["URL"] = valor
    Case "eid"
      dEntrada["number"] = valor

      ' NOTAS Y TEXTOS
    Case "note"
      dEntrada["note"] = valor
    Case "abstract"
      dEntrada["abstract"] = valor
    Case "annotation"
      dEntrada["annote"] = valor
    Case "addendum"
      If dEntrada.Exist("note") Then
        dEntrada["note"] = dEntrada["note"] & " " & valor
      Else
        dEntrada["note"] = valor
      Endif

      ' IDIOMA
    Case "language", "lang_id", "hyphenation"
      dEntrada["language"] = valor
    Case "orig_language"
      dEntrada["original-language"] = valor

      ' CAMPOS NUMÉRICOS
    Case "volume"
      dEntrada["volume"] = valor
    Case "volumes"
      dEntrada["number-of-volumes"] = valor
    Case "number", "issue"
      dEntrada["issue"] = valor
    Case "pages"
      dEntrada["page"] = valor
    Case "page_total"
      dEntrada["number-of-pages"] = valor
    Case "edition"
      dEntrada["edition"] = valor
    Case "chapter"
      dEntrada["chapter-number"] = valor
    Case "part"
      dEntrada["part"] = valor

      ' CAMPOS DE CONTENEDOR
    Case "journal", "journaltitle", "journal_title"
      dEntrada["container-title"] = valor
    Case "journal_title_addon"
      If dEntrada.Exist("container-title") Then
        dEntrada["container-title"] = dEntrada["container-title"] & " " & valor
      Endif
    Case "booktitle", "book_title"
      dEntrada["container-title"] = valor
    Case "book_title_addon"
      If dEntrada.Exist("container-title") Then
        dEntrada["container-title"] = dEntrada["container-title"] & " " & valor
      Endif
    Case "issue_title"
      dEntrada["container-title"] = valor
    Case "issue_title_addon"
      If dEntrada.Exist("container-title") Then
        dEntrada["container-title"] = dEntrada["container-title"] & " " & valor
      Endif
    Case "series"
      dEntrada["collection-title"] = valor

      ' CAMPOS DE PUBLICACIÓN
    Case "publisher"
      dEntrada["publisher"] = valor
    Case "orig_publisher"
      dEntrada["original-publisher"] = valor
    Case "location"
      dEntrada["publisher-place"] = valor
    Case "orig_location"
      dEntrada["original-publisher-place"] = valor
    Case "organization", "institution"
      ' Para tesis e informes técnicos, usar publisher
      dEntrada["publisher"] = valor
    Case "venue"
      dEntrada["event-place"] = valor
    Case "how_published"
      dEntrada["medium"] = valor

      ' AUTOR Y COLABORADORES PRINCIPALES
    Case "author"
      dEntrada["author"] = ParsearNombres(valor)
    Case "editor"
      dEntrada["editor"] = ParsearNombres(valor)
    Case "editor_a"
      dEntrada["editor"] = ParsearNombres(valor)
    Case "editor_b", "editor_c"
      ' Editores adicionales - en CSL no hay campos separados, agregar a note
      If Not dEntrada.Exist("note") Then
        dEntrada["note"] = "Additional editor: " & valor
      Else
        dEntrada["note"] = dEntrada["note"] & "; Additional editor: " & valor
      Endif
    Case "book_author"
      dEntrada["container-author"] = ParsearNombres(valor)
    Case "translator"
      dEntrada["translator"] = ParsearNombres(valor)
    Case "short_author"
      ' Usar como autor si no existe author
      If Not dEntrada.Exist("author") Then
        dEntrada["author"] = ParsearNombres(valor)
      Endif

      ' COLABORADORES SECUNDARIOS
    Case "afterword", "foreword", "introduction", "commentator", "annotator"
      ' CSL no tiene campos específicos, agregar a contributors o note
      If Not dEntrada.Exist("note") Then
        dEntrada["note"] = sCampo & ": " & valor
      Else
        dEntrada["note"] = dEntrada["note"] & "; " & sCampo & ": " & valor
      Endif
    Case "holder"
      dEntrada["authority"] = valor

      ' FECHAS
    Case "year"
      If Not dEntrada.Exist("issued") Then
        dEntrada["issued"] = CrearFechaCSL(valor, Null, Null)
      Endif
    Case "date"
      dEntrada["issued"] = ParsearFechaCompleta(valor)
    Case "url_date"
      dEntrada["accessed"] = ParsearFechaCompleta(valor)
    Case "event_date"
      dEntrada["event-date"] = ParsearFechaCompleta(valor)
    Case "orig_date"
      dEntrada["original-date"] = ParsearFechaCompleta(valor)

      ' EVENTOS
    Case "event_title"
      dEntrada["event"] = valor
    Case "event_title_addon"
      If dEntrada.Exist("event") Then
        dEntrada["event"] = dEntrada["event"] & " " & valor
      Else
        dEntrada["event"] = valor
      Endif

      ' CAMPOS DE ARCHIVO Y REPOSITORIO
    Case "eprint"
      dEntrada["PMID"] = valor
    Case "eprint_type"
      dEntrada["archive"] = valor
    Case "library"
      dEntrada["archive"] = valor
    Case "file"
      dEntrada["source"] = valor

      ' ESTADO Y VERSIÓN
    Case "version"
      dEntrada["version"] = valor
    Case "pub_state"
      dEntrada["status"] = valor

      ' REFERENCIAS CRUZADAS (agregar como note)
    Case "cross_ref", "xref"
      If Not dEntrada.Exist("note") Then
        dEntrada["note"] = "Cross-reference: " & valor
      Else
        dEntrada["note"] = dEntrada["note"] & "; Cross-reference: " & valor
      Endif

      ' TIPO DE TESIS (agregar como genre en CSL)
    Case "tipo_de_tesis"
      dEntrada["genre"] = valor

      ' LABEL/SHORTHAND
    Case "short_hand", "label"
      dEntrada["citation-label"] = valor

  End Select

End

' FUNCIÓN AUXILIAR: CREAR OBJETO FECHA CSL
Private Function CrearFechaCSL(anio As Variant, mes As Variant, dia As Variant) As Collection

  Dim dFecha As New Collection
  Dim aPartes As New Integer[]

  If Not IsNull(anio) Then
    aPartes.Add(CInt(anio))
    If Not IsNull(mes) Then
      aPartes.Add(CInt(mes))
      If Not IsNull(dia) Then
        aPartes.Add(CInt(dia))
      Endif
    Endif
  Endif

  dFecha["date-parts"] = [aPartes]
  Return dFecha

End

' FUNCIÓN AUXILIAR: PARSEAR FECHA COMPLETA (FORMATO ISO O BIBTEX)
Private Function ParsearFechaCompleta(sFecha As String) As Collection

  Dim partes As String[]
  Dim anio, mes, dia As Integer

  ' FORMATO ISO: YYYY-MM-DD
  If InStr(sFecha, "-") > 0 Then
    partes = Split(sFecha, "-")
    anio = CInt(partes[0])
    If partes.Count > 1 Then mes = CInt(partes[1])
    If partes.Count > 2 Then dia = CInt(partes[2])
    Return CrearFechaCSL(anio, mes, dia)
  Else
    ' SOLO ANIO
    Return CrearFechaCSL(CInt(sFecha), Null, Null)
  Endif

End

' FUNCIÓN AUXILIAR: PARSEAR NOMBRES DE AUTOR/EDITOR A FORMATO CSL
' FUNCIÓN AUXILIAR: PARSEAR NOMBRES DE AUTOR/EDITOR A FORMATO CSL
Private Function ParsearNombres(sNombres As String) As Collection[]

  Dim aAutores As New Collection[]
  Dim aNombresDiv As String[]
  Dim nombreCompleto As String
  Dim dAutor As Collection
  Dim posComa As Integer
  Dim partes As String[]
  Dim i As Integer
  Dim given As String

  ' Normalizar espacios en blanco
  sNombres = Replace(sNombres, Chr(9), " ")   ' Tab
  sNombres = Replace(sNombres, Chr(160), " ") ' Non-breaking space
  While InStr(sNombres, "  ")
    sNombres = Replace(sNombres, "  ", " ")
  Wend
  sNombres = Trim(sNombres)

  ' Dividir por " and " usando Replace + Split
  ' Reemplazar " and " por un carácter especial que no esté en uso
  sNombres = Replace(sNombres, " and ", "|||")
  aNombresDiv = Split(sNombres, "|||")

  For i = 0 To aNombresDiv.Max
    nombreCompleto = Trim(aNombresDiv[i])

    If Len(nombreCompleto) = 0 Then Continue

    dAutor = New Collection
    posComa = InStr(nombreCompleto, ",")

    If posComa > 0 Then
      ' FORMATO: Apellido, Nombre
      dAutor["family"] = Trim(Left(nombreCompleto, posComa - 1))
      dAutor["given"] = Trim(Mid(nombreCompleto, posComa + 1))
    Else
      ' FORMATO: Nombre Apellido
      partes = Split(nombreCompleto, " ")

      dAutor["family"] = Trim(partes[partes.Max])

      If partes.Max > 0 Then
        given = ""
        Dim j As Integer
        For j = 0 To partes.Max - 1
          If given <> "" Then given &= " "
          given &= Trim(partes[j])
        Next
        dAutor["given"] = given
      Endif
    Endif

    aAutores.Add(dAutor)
  Next

  Return aAutores

End
