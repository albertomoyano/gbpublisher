' Gambas module file

Public UsuarioEnCurso As String
Public IdUsuario As Integer

Public Sub CerrarTodoMySQL()

  ' CONFIRMACIÓN DE SALIDA
  m_Sonido.sonar("Question")
  If Message.Question("¿Está seguro que desea salir de la aplicación?") = 2 Then ' Usuario eligió 2 = Cancelar
    Return
  Endif

  m_ConexionBD.mConn.Exec("UPDATE usuarios SET en_linea = 0 WHERE usuario = &1", UsuarioEnCurso)
  m_ConexionBD.mConn.Exec("UPDATE proyectos SET usuario_propietario = NULL WHERE id = &1", FMain.id_proyecto.Value)
  m_ConexionBD.mConn.Exec("UPDATE proyectos SET ocupado = 0 WHERE id = &1", FMain.id_proyecto.Value)

  m_ConexionBD.mConn.Commit
  m_Timer.DetenerContador()

  'DETENEMOS EL CONTADOR PARA ARCHIVOS XML
  FMain.DetenerTimerArchivosXML()

  ' DETENEMOS EL CONTADOR PARA ARCHIVOS MD
  FMain.DetenerTimerArchivosProyecto()

  ' LIMPIAR TERMINAL Y PROCESOS
  Try FMain.TerminalViewProyecto.Input("rm -rf " & User.Home & "/.local/share/org.gambas.*" & "\n")
  Try FMain.TerminalViewProyecto.Input("clear" & "\n")
  Wait 0.2

  ' DETIENE EL PROCESO BASH (INTENTOS CORTOS, NO BLOQUEANTES)
  If FMain.$Bash And FMain.$Bash.State = Process.Running Then
    Dim tries As Integer = 0
    While FMain.$Bash.State = Process.Running And tries < 20
      FMain.$Bash.Kill
      Wait 0.05
      tries = tries + 1
    Wend

    If FMain.$Bash.State = Process.Running Then
      FMain.$Bash.Terminate
      Wait 0.2
      If FMain.$Bash.State = Process.Running Then
        m_Sonido.sonar("Warning")
        Message.Warning("El proceso no pudo ser detenido.")
      Endif
    Endif
  Endif

  ' LIMPIAR INTERFAZ
  FMain.txtProyecto.Clear
  FMain.DirViewProyecto.Root = User.Home
  FMain.TextBoxUsuario.Clear
  FMain.TextBoxServidor.Clear

  ' CIERRA LA CONEXIÓN A LA BBDD SI EXISTE Y ESTÁ ABIERTA
  If Not IsNull(m_ConexionBD.mConn) Then
    If m_ConexionBD.mConn.Opened Then
      m_ConexionBD.mConn.Close()
    Endif
    m_ConexionBD.mConn = Null
  Endif

  ' FORZAMOS SALIDA PARA NO QUEDAR EN ESTADO INCONSISTENTE
  Quit

End

Public Sub DirectorioOcultoApp()

  ' LISTA DE RUTAS QUE NECESITAS VERIFICAR/CREAR
  Dim rutasNecesarias As String[] = [
    User.Home & "/.gbpublisher",
    User.Home & "/.gbpublisher/sonido",
    User.Home & "/.gbpublisher/xslt",
    User.Home & "/.gbpublisher/fonts",
    User.Home & "/.gbpublisher/lua",
    User.Home & "/.gbpublisher/csl",
    User.Home & "/.gbpublisher/scripts",
    User.Home & "/.gbpublisher/respaldo",
    User.Home & "/.gbpublisher/media"

  ]

  ' VERIFICAR Y CREAR RUTAS SI NO EXISTEN
  For Each rutaDirSys As String In rutasNecesarias
    If Not Exist(rutaDirSys) Then
      Try Mkdir rutaDirSys  ' INTENTA CREAR EL DIRECTORIO
      If Error Then
        m_Sonido.sonar("Error")
        Message.Error("Error al intentar crear los directorios del sistema.")
      Endif
    End If
  Next

  If Not File.RealPath(User.Home & "/.gbpublisher/.ips.txt") Then
    Copy "ips.txt" To User.Home & "/.gbpublisher/.ips.txt"
  End If

  If Not File.RealPath(User.Home & "/.gbpublisher/buscador.py") Then
    Copy "./buscador.py" To User.Home & "/.gbpublisher/buscador.py"
  End If

  If Not File.RealPath(User.Home & "/.gbpublisher/postprocesar_jats.py") Then
    Copy "./postprocesar_jats.py" To User.Home & "/.gbpublisher/postprocesar_jats.py"
  End If

  ' COPIAR ARCHIVOS .OGG
  Dim srcDir1 As String
  Dim dstDir1 As String
  Dim f1 As String

  srcDir1 = Application.Path & "/sonido"
  dstDir1 = User.Home & "/.gbpublisher/sonido"

  For Each f1 In Dir(srcDir1, "*.ogg")
    If Not Exist(dstDir1 & "/" & f1) Then
      Copy srcDir1 & "/" & f1 To dstDir1 & "/" & f1
    End If
  Next

  ' COPIAR ARCHIVOS .CSL
  Dim srcDir2 As String
  Dim dstDir2 As String
  Dim f2 As String

  srcDir2 = Application.Path & "/csl"
  dstDir2 = User.Home & "/.gbpublisher/csl"

  For Each f2 In Dir(srcDir2, "*.csl")
    If Not Exist(dstDir2 & "/" & f2) Then
      Copy srcDir2 & "/" & f2 To dstDir2 & "/" & f2
    End If
  Next

  ' COPIAR ARCHIVOS .TTF
  Dim srcDir3 As String
  Dim dstDir3 As String
  Dim f3 As String

  srcDir3 = Application.Path & "/fonts"
  dstDir3 = User.Home & "/.gbpublisher/fonts"

  For Each f3 In Dir(srcDir3, "*.ttf")
    If Not Exist(dstDir3 & "/" & f3) Then
      Copy srcDir3 & "/" & f3 To dstDir3 & "/" & f3
    End If
  Next

  ' COPIAR ARCHIVOS .PNG
  Dim srcDir4 As String
  Dim dstDir4 As String
  Dim f4 As String

  srcDir4 = Application.Path & "/media"
  dstDir4 = User.Home & "/.gbpublisher/media"

  For Each f4 In Dir(srcDir4, "*.png")
    If Not Exist(dstDir4 & "/" & f4) Then
      Copy srcDir4 & "/" & f4 To dstDir4 & "/" & f4
    End If
  Next

  ' COPIAR ARCHIVOS DE SCRIPTS
  Dim srcDir5 As String
  Dim dstDir5 As String
  Dim f5 As String

  srcDir5 = Application.Path & "/scripts"
  dstDir5 = User.Home & "/.gbpublisher/scripts"

  For Each f5 In Dir(srcDir5, "*.*")
    If Not Exist(dstDir5 & "/" & f5) Then
      Copy srcDir5 & "/" & f5 To dstDir5 & "/" & f5
    End If
  Next

End

Public Sub CargarUsuarios()

  Dim rUsuarios As Result
  Dim iRow As Integer
  Dim iCol As Integer

  rUsuarios = m_ConexionBD.mConn.Exec("SELECT usuario, en_linea FROM usuarios WHERE activo = 1 ORDER BY en_linea DESC, usuario ASC")

  ' LIMPIAR EL GRIDVIEW
  FMain.TableViewUsuarios.Clear

  ' SI NO HAY DATOS, ESTABLECER EL GRID VACÍO Y SALIR
  If rUsuarios = Null Or rUsuarios.Count = 0 Then
    FMain.TableViewUsuarios.Rows.Count = 0
    FMain.TableViewUsuarios.Columns.Count = 0
    Return
  Endif

  ' CONFIGURAR COLUMNAS Y FILAS
  FMain.TableViewUsuarios.Columns.Count = rUsuarios.Fields.Count
  FMain.TableViewUsuarios.Rows.Count = rUsuarios.Count
  FMain.TableViewUsuarios.MoveTo(0, 0)

  ' POBLAR LOS DATOS DEL RESULTADO EN EL GRIDVIEW
  iRow = 0
  For Each rUsuarios
    For iCol = 0 To rUsuarios.Fields.Count - 1
      If IsNull(rUsuarios[iCol]) Then
        FMain.TableViewUsuarios[iRow, iCol].Text = ""
      Else
        ' SI ES LA COLUMNA "EN_LINEA" (ÍNDICE 1), MOSTRAR TEXTO
        If iCol = 1 Then
          FMain.TableViewUsuarios[iRow, iCol].Text = IIf(CBool(rUsuarios[iCol]), "Conectado", "")
        Else
          FMain.TableViewUsuarios[iRow, iCol].Text = rUsuarios[iCol]
        End If
      Endif
    Next
    iRow += 1
  Next

End

Public Sub DisenarTableViewUsuarios(grid As GridView)

  With grid
    .Header = True
    .Grid = True
    .Columns.Count = 3

    ' CONFIGURACIÓN DE COLUMNAS
    For i As Integer = 0 To 2
      Select Case i
        Case 0
          .Columns[i].Title = "Usuario"
          .Columns[i].Width = 150
        Case 1
          .Columns[i].Title = "Estado"
          .Columns[i].Width = 150
        Case 2
          .Columns[i].Title = "Proyecto"
          .Columns[i].Width = 300
      End Select
    Next
  End With

End

Public Sub CargarScripts()

  Dim rutaScripts As String = User.Home &/ ".gbpublisher/scripts"
  Dim archivos As String[]
  Dim archivo As String
  Dim linea As String
  Dim descripcion As String
  Dim archivoBase As String
  Dim hFile As File
  Dim extension As String
  Dim extensionOriginal As String
  Dim prefijo As String

  ' VERIFICAR QUE EL DIRECTORIO EXISTE
  If Not Exist(rutaScripts) Then
    m_Sonido.sonar("Error")
    Message.Error("El directorio de scripts no existe: " & rutaScripts)
    Return
  Endif

  FMain.ComboBoxScripts.Clear

  ' DEFINIR LAS EXTENSIONES SOPORTADAS
  Dim extensiones As String[] = ["*.py", "*.lua", "*.sh", "*.pl", "*.R"]
  Dim todosArchivos As New String[]

  ' OBTENER ARCHIVOS DE TODAS LAS EXTENSIONES
  For Each extension In extensiones
    archivos = Dir(rutaScripts, extension)
    If Not Error Then
      For Each archivo In archivos
        todosArchivos.Add(archivo)
      Next
    Endif
  Next

  ' VERIFICAR SI HAY ARCHIVOS
  If todosArchivos.Count = 0 Then
    m_Sonido.sonar("Info")
    Message.Info("No se encontraron scripts en el directorio")
    Return
  Endif

  ' PROCESAR CADA ARCHIVO
  For Each archivo In todosArchivos
    descripcion = "Sin descripción"

    ' OBTENER EXTENSIÓN ORIGINAL SIN CONVERTIR
    extensionOriginal = File.Ext(archivo)

    ' CONVERTIR SOLO PARA LA COMPARACIÓN
    extension = LCase(extensionOriginal)

    ' DETERMINAR EL PREFIJO DE COMENTARIO SEGÚN LA EXTENSIÓN
    Select Case extension
      Case "py"
        prefijo = "#"
      Case "lua"
        prefijo = "--"
      Case "sh"
        prefijo = "#"
      Case "pl"
        prefijo = "#"
      Case "r"
        prefijo = "#"
      Default
        prefijo = "#"
    End Select

    ' CONSTRUIR RUTA COMPLETA DEL ARCHIVO
    Dim rutaCompleta As String = rutaScripts &/ archivo

    ' INTENTAR LEER LA PRIMERA LÍNEA DEL ARCHIVO PARA OBTENER DESCRIPCIÓN
    hFile = Open rutaCompleta For Read
    If Not Error Then
      Line Input #hFile, linea
      If Not Error And linea <> "" Then
        linea = Trim(linea)
        ' SALTAR SHEBANG SI EXISTE (#!/...)
        If Left(linea, 2) = "#!" Then
          If Not Eof(hFile) Then
            Line Input #hFile, linea
            linea = Trim(linea)
          Else
            linea = ""
          Endif
        Endif
        ' VERIFICAR SI LA LÍNEA COMIENZA CON EL PREFIJO DE COMENTARIO
        If linea <> "" And Left(linea, Len(prefijo)) = prefijo Then
          ' EXTRAER DESCRIPCIÓN, ELIMINANDO EL PREFIJO Y ESPACIOS
          If Len(linea) > Len(prefijo) Then
            descripcion = Trim(Mid(linea, Len(prefijo) + 1))
          Endif
        Endif
      Endif
      Close #hFile
    Else
      descripcion = "Error al leer archivo"
    Endif

    ' AGREGAR AL COMBOBOX CON LA EXTENSIÓN ORIGINAL
    archivoBase = File.BaseName(archivo) & "." & extensionOriginal
    FMain.ComboBoxScripts.Add(descripcion & " [" & archivoBase & "]")
  Next

End

Public Sub AnalizarMD()

  Dim sInfo As String
  Dim archivos As String[]
  Dim archivo As String
  Dim contenido As String
  Dim caracteres As Integer
  Dim totalCaracteres As Long
  Dim totalArchivos As Integer
  Dim rutaProyecto As String
  Dim rutaArticulos As String
  Dim rutaCompleta As String

  rutaProyecto = File.Dir(FMain.txtProyecto.Text)
  rutaArticulos = rutaProyecto &/ "articulos"

  sInfo = "<b>Estadísticas del proyecto</b>\n"
  sInfo &= "<tt>"
  sInfo &= String(43, "-") & "\n\n"
  sInfo &= FormatearLinea("Archivo", "Caracteres")
  sInfo &= String(43, "-") & "\n"

  ' Archivos en raíz
  If Exist(rutaProyecto) Then
    archivos = Dir(rutaProyecto, "*.md")
    For Each archivo In archivos
      rutaCompleta = rutaProyecto &/ archivo
      contenido = File.Load(rutaCompleta)
      If Error Then
        sInfo &= FormatearLinea(archivo, "Error")
        Error.Clear()
      Else
        caracteres = Len(contenido)
        sInfo &= FormatearLinea(archivo, FormatearNumero(caracteres))
        totalCaracteres += caracteres
        totalArchivos += 1
      Endif
    Next
  Endif

  If archivos.Count > 0 Then sInfo &= "\n"

  ' ARCHIVOS EN /ARTICULOS
  If Exist(rutaArticulos) Then
    archivos = Dir(rutaArticulos, "*.md")
    For Each archivo In archivos
      rutaCompleta = rutaArticulos &/ archivo
      contenido = File.Load(rutaCompleta)
      If Error Then
        sInfo &= FormatearLinea("articulos/" & archivo, "Error")
        Error.Clear()
      Else
        caracteres = Len(contenido)
        sInfo &= FormatearLinea("articulos/" & archivo, FormatearNumero(caracteres))
        totalCaracteres += caracteres
        totalArchivos += 1
      Endif
    Next
  Endif

  sInfo &= String(43, "-") & "\n"
  sInfo &= FormatearLinea("Total (" & totalArchivos & " archivos)", FormatearNumero(totalCaracteres))
  sInfo &= "</tt>"

  Message.Info(sInfo)

End

' FUNCIÓN AUXILIAR PARA FORMATEAR LÍNEAS CON ESPACIOS NO-BREAKING
Private Function FormatearLinea(columna1 As String, columna2 As String) As String

  Dim linea As String
  Dim espacios As String
  Dim i As Integer
  Dim anchoCol1 As Integer = 30
  Dim anchoCol2 As Integer = 10

  ' COLUMNA 1: ALINEADA A LA IZQUIERDA
  espacios = ""
  For i = 1 To anchoCol1 - Len(columna1)
    espacios &= "&nbsp;"
  Next
  linea = columna1 & espacios & " | "

  ' COLUMNA 2: ALINEADA A LA DERECHA
  espacios = ""
  For i = 1 To anchoCol2 - Len(columna2)
    espacios &= "&nbsp;"
  Next
  linea &= espacios & columna2 & "\n"

  Return linea

End

' FUNCIÓN PARA FORMATEAR NÚMEROS CON SEPARADOR DE MILES
Private Function FormatearNumero(numero As Long) As String

  Dim resultado As String
  Dim strNumero As String
  Dim i As Integer
  Dim contador As Integer

  strNumero = Str(numero)
  resultado = ""
  contador = 0

  ' RECORRER EL NÚMERO DE DERECHA A IZQUIERDA
  For i = Len(strNumero) To 1 Step -1
    If contador = 3 Then
      resultado = "." & resultado
      contador = 0
    Endif
    resultado = Mid(strNumero, i, 1) & resultado
    contador += 1
  Next

  Return resultado

End
