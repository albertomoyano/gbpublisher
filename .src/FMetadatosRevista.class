' Gambas class file

' ════════════════════════════════════════════════════════════════════════════
' EVENTO QUE SE EJECUTA AL ABRIR EL FORMULARIO
' CARGA LOS METADATOS DE LA REVISTA DEL PROYECTO ACTUAL
' ════════════════════════════════════════════════════════════════════════════
Public Sub Form_Open()

  ' VERIFICAR QUE HAY UN PROYECTO ABIERTO
  If FMain.id_proyecto.Value <= 0 Then
    m_Sonido.sonar("Warning")
    Message.Warning("Debe abrir un proyecto antes de editar metadatos.")
    Me.Close
    Return
  Endif

  ' SINCRONIZAR EL id_proyecto
  id_proyecto.Value = FMain.id_proyecto.Value

  ' CARGAR LOS METADATOS
  m_Metadatos.CargarMetadatosDeRevista()

  ' CARGAR LOS ESTILOS CSL
  CargarCSLBibTeX()

End

Public Sub btnCerrar_Click()

  Me.Close()

End

' DETECTA CTRL + F1
Public Sub Form_KeyRelease()

  If Key.Control And Key.Code = Key.F1 Then
    m_FuncionesGenericas.MostrarAyuda()
  Endif

End

' ════════════════════════════════════════════════════════════════════════════
' EVENTO CLICK DEL BOTÓN GUARDAR CAMBIOS METADATOS REVISTA
' VALIDA Y GUARDA TODOS LOS METADATOS EN LA BASE DE DATOS
' ════════════════════════════════════════════════════════════════════════════
Public Sub btnGuardarCambiosMetadatosRevista_Click()

  Dim bExito As Boolean

  ' VALIDACIONES BÁSICAS ANTES DE GUARDAR
  If Trim(titulo_revista.Text) = "" Then
    m_Sonido.sonar("Warning")
    Message.Warning("El título de la revista es obligatorio.")
    titulo_revista.SetFocus
    Return
  Endif

  ' CONFIRMACIÓN DEL USUARIO
  If Message.Question("¿Desea guardar los cambios en los metadatos de la revista?",
      "Guardar", "Cancelar") = 2 Then
    m_Sonido.sonar("Question")
    Return
  Endif

  ' GUARDAR LOS METADATOS
  m_Sonido.sonar("Info")
  bExito = m_Metadatos.GuardarMetadatosDeRevista()

  ' SI SE GUARDÓ EXITOSAMENTE, RECARGAR PARA MOSTRAR VALORES ACTUALIZADOS
  If bExito Then
    m_Metadatos.CargarMetadatosDeRevista()
  Endif

End

Public Sub ano_publicacion_KeyPress()
  ' PERMITIR SOLO DÍGITOS Y TECLAS DE CONTROL

  If Not IsDigit(Key.Text) And Key.Code <> Key.BackSpace And Key.Code <> Key.Delete Then
    Stop Event
  Endif

End

Public Sub periodo_embargo_KeyPress()
  ' PERMITIR SOLO DÍGITOS Y TECLAS DE CONTROL

  If Not IsDigit(Key.Text) And Key.Code <> Key.BackSpace And Key.Code <> Key.Delete Then
    Stop Event
  Endif

End

Public Sub ano_inicio_KeyPress()
  ' PERMITIR SOLO DÍGITOS Y TECLAS DE CONTROL

  If Not IsDigit(Key.Text) And Key.Code <> Key.BackSpace And Key.Code <> Key.Delete Then
    Stop Event
  Endif

End

Public Sub ano_fin_KeyPress()
  ' PERMITIR SOLO DÍGITOS Y TECLAS DE CONTROL

  If Not IsDigit(Key.Text) And Key.Code <> Key.BackSpace And Key.Code <> Key.Delete Then
    Stop Event
  Endif

End

Public Sub factor_impacto_KeyPress()

  ' PERMITIR DÍGITOS
  If IsDigit(Key.Text) Then
    Return
  Endif

  ' PERMITIR PUNTO DECIMAL (SOLO UNO)
  If Key.Text = "." Then
    If InStr(factor_impacto.Text, ".") > 0 Then
      ' YA HAY UN PUNTO, NO PERMITIR OTRO
      Stop Event
    Endif
    Return
  Endif

  ' PERMITIR TECLAS DE CONTROL
  If Key.Code = Key.BackSpace Or Key.Code = Key.Delete Then
    Return
  Endif

  ' CUALQUIER OTRA TECLA: BLOQUEAR
  Stop Event

End

' ════════════════════════════════════════════════════════════════════════════
' VALIDAR RANGO RAZONABLE PARA FACTOR DE IMPACTO
' GENERALMENTE ESTÁ ENTRE 0.001 Y 100 (VALORES MUY ALTOS SON RAROS)
' ════════════════════════════════════════════════════════════════════════════
Public Sub factor_impacto_LostFocus()

  Dim valor As Float

  If Trim(factor_impacto.Text) = "" Then
    Return
  Endif

  valor = CFloat(factor_impacto.Text)

  ' VALIDAR RANGO RAZONABLE
  If valor < 0 Then
    m_Sonido.sonar("Warning")
    Message.Warning("El factor de impacto no puede ser negativo.")
    factor_impacto.Text = ""
    factor_impacto.SetFocus
    Return
  Endif

  If valor > 100 Then
    ' ADVERTENCIA PERO NO BLOQUEAR (POR SI ACASO)
    If Message.Question("El factor de impacto ingresado (" & Format(valor, "0.000") & ") es inusualmente alto. ¿Es correcto?", "Sí", "Corregir") = 2 Then
      factor_impacto.SetFocus
      m_Sonido.sonar("Question")
      Return
    Endif
  Endif

  ' FORMATEAR A 3 DECIMALES
  factor_impacto.Text = Format(valor, "0.000")

Catch
  m_Sonido.sonar("Error")
  Message.Error("Valor inválido para factor de impacto.")
  factor_impacto.Text = ""
  factor_impacto.SetFocus

End

Public Sub btnDescripcion_Click()

  FTXTextendido.OriginalTextBox = descripcion_revista
  FTXTextendido.ShowModal

End

Public Sub btnNotasInternas_Click()

  FTXTextendido.OriginalTextBox = notas_internas
  FTXTextendido.ShowModal

End

Public Sub btnVerificarDOI_Click()

  m_FuncionesGenericas.VerificarDOI(doi_prefix)

End

Public Sub btnURLrevista_Click()

  m_FuncionesGenericas.VerificarURLDesdeCampo(url_revista)

End

Public Sub btnVerificarOAI_Click()

  m_FuncionesGenericas.VerificarURLDesdeCampo(url_oai_pmh)

End

Public Sub btnVerificarHandle_Click()

  m_FuncionesGenericas.VerificarURLDesdeCampo(handle_prefix)

End

Public Sub btnVerificarLogo_Click()

  m_FuncionesGenericas.VerificarURLDesdeCampo(url_logo)

End

Public Sub btnVerificarLicencia_Click()

  m_FuncionesGenericas.VerificarURLDesdeCampo(url_licencia)

End

Public Sub btnVerificarURLautores_Click()

  m_FuncionesGenericas.VerificarURLDesdeCampo(instrucciones_autores_url)

End

Public Sub btnVerificarRepositorio_Click()

  m_FuncionesGenericas.VerificarURLDesdeCampo(repositorio_produccion_url)

End

' ════════════════════════════════════════════════════════════════════════════
' VALIDAR ENTRADA EN TIEMPO REAL PARA ISSN
' PERMITE: DÍGITOS, GUION (SOLO UNO EN POSICIÓN 5), X (SOLO AL FINAL)
' ════════════════════════════════════════════════════════════════════════════
Public Sub issn_impreso_KeyPress()

  Dim sTextoActual As String = Last.Text
  Dim iPosicion As Integer = Last.Pos

  ' PERMITIR TECLAS DE CONTROL
  If Key.Code = Key.BackSpace Or Key.Code = Key.Delete Or
      Key.Code = Key.Left Or Key.Code = Key.Right Or
      Key.Code = Key.Home Or Key.Code = Key.End Then
    Return
  Endif

  ' PERMITIR DÍGITOS
  If IsDigit(Key.Text) Then
    Return
  Endif

  ' PERMITIR GUION SOLO EN POSICIÓN 4 (se insertará entre pos 4 y 5)
  If Key.Text = "-" And iPosicion = 4 And InStr(sTextoActual, "-") = 0 Then
    Return
  Endif

  ' PERMITIR X SOLO EN POSICIÓN 8 (último carácter)
  If UCase(Key.Text) = "X" And iPosicion = 8 Then
    Return
  Endif

  ' CUALQUIER OTRA TECLA: BLOQUEAR
  Stop Event

End

' ════════════════════════════════════════════════════════════════════════════
' VALIDACIÓN CENTRALIZADA PARA CAMPOS ISSN
' FORMATO VÁLIDO: ####-#### (4 dígitos, guion, 4 dígitos)
' ════════════════════════════════════════════════════════════════════════════
Public Sub issn_impreso_LostFocus()

  Dim sText As String = Trim(Last.Text)

  ' SI ESTÁ VACÍO, PERMITIR (ES OPCIONAL)
  If sText = "" Then
    Last.Background = Color.White
    Return
  Endif

  ' VERIFICAR LONGITUD EXACTA (9 CARACTERES)
  If Len(sText) <> 9 Then
    Last.Background = Color.Pink
    m_Sonido.sonar("Warning")
    Message.Warning("El ISSN debe tener exactamente 9 caracteres (formato: ####-####).")
    Last.SetFocus()
    Return
  Endif

  ' VERIFICAR QUE EL CARÁCTER 5 SEA UN GUION
  If Mid$(sText, 5, 1) <> "-" Then
    Last.Background = Color.Pink
    m_Sonido.sonar("Warning")
    Message.Warning("El ISSN debe tener un guion en la posición 5 (formato: ####-####).")
    Last.SetFocus()
    Return
  Endif

  ' VERIFICAR QUE LOS PRIMEROS 4 CARACTERES SEAN DÍGITOS
  If Not IsDigit(Mid$(sText, 1, 1)) Or
      Not IsDigit(Mid$(sText, 2, 1)) Or
      Not IsDigit(Mid$(sText, 3, 1)) Or
      Not IsDigit(Mid$(sText, 4, 1)) Then
    Last.Background = Color.Pink
    m_Sonido.sonar("Warning")
    Message.Warning("Los primeros 4 caracteres deben ser dígitos (formato: ####-####).")
    Last.SetFocus()
    Return
  Endif

  ' VERIFICAR QUE LOS ÚLTIMOS 4 CARACTERES SEAN DÍGITOS O 'X' (EL ÚLTIMO PUEDE SER X)
  If Not IsDigit(Mid$(sText, 6, 1)) Or
      Not IsDigit(Mid$(sText, 7, 1)) Or
      Not IsDigit(Mid$(sText, 8, 1)) Or
      (Not IsDigit(Mid$(sText, 9, 1)) And UCase(Mid$(sText, 9, 1)) <> "X") Then
    Last.Background = Color.Pink
    m_Sonido.sonar("Warning")
    Message.Warning("Los últimos 4 caracteres deben ser dígitos (el último puede ser X).")
    Last.SetFocus()
    Return
  Endif

  ' TODO VÁLIDO
  Last.Background = Color.White

  ' NORMALIZAR: CONVERTIR X FINAL A MAYÚSCULA SI EXISTE
  If UCase(Mid$(sText, 9, 1)) = "X" Then
    Last.Text = Left$(sText, 8) & "X"
  Endif

End

Public Sub btnVerificarPreservacion_Click()

  m_FuncionesGenericas.VerificarURLDesdeCampo(repositorio_preservacion_url)

End

Public Sub CargarCSLBibTeX()

  Dim rutaArchivos As String = User.Home &/ ".gbpublisher/csl/"
  Dim archivos As String[]
  Dim archivo As String
  Dim linea As String
  Dim descripcion As String
  Dim hFile As File
  Dim i As Integer
  Dim encontrado As Boolean

  If Not Exist(rutaArchivos) Then
    m_Sonido.sonar("Error")
    Message.Error("El directorio no existe: " & rutaArchivos)
    Return
  Endif

  cmbTipoCSL.Clear

  ' OBTENER LISTA DE ARCHIVOS .CSL
  archivos = Dir(rutaArchivos, "*.csl")
  If Error Then
    archivos = New String[]
  Endif

  If archivos.Count = 0 Then
    m_Sonido.sonar("Info")
    Message.Info("No se encontraron archivos .csl en el directorio")
    Return
  Endif

  ' PROCESAR CADA ARCHIVO
  For Each archivo In archivos
    descripcion = "Sin descripción"
    encontrado = False
    Dim rutaCompleta As String = rutaArchivos &/ archivo

    hFile = Open rutaCompleta For Read
    If Not Error Then
      ' LEER LAS PRIMERAS LÍNEAS BUSCANDO EL COMENTARIO
      For i = 1 To 3  ' REVISAR HASTA 3 LÍNEAS POR SI ACASO
        If Eof(hFile) Then Break

        Line Input #hFile, linea
        If Error Then Break

        linea = Trim(linea)

        ' PARA ARCHIVOS .CSL (COMENTARIOS XML)
        If Right(archivo, 4) = ".csl" Then
          If Left(linea, 4) = "<!--" And Right(linea, 3) = "-->" Then
            descripcion = Trim(Mid(linea, 5, Len(linea) - 7))
            encontrado = True
            Break
          Endif
          ' PARA ARCHIVOS .TEX (COMENTARIOS LATEX)
        Else If Right(archivo, 4) = ".tex" Then
          If Left(linea, 1) = "%" And Len(linea) > 2 Then
            descripcion = Trim(Mid(linea, 2))
            encontrado = True
            Break
          Endif
        Endif
      Next

      Close #hFile
    Else
      descripcion = "Error al leer archivo"
    Endif

    ' AGREGAR AL COMBOBOX
    cmbTipoCSL.Add(descripcion & " [" & archivo & "]")
  Next

End
