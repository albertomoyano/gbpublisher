' Gambas class file

' Formulario que pide el nombre del archivo nuevo
Public RutaDestino As String ' Ruta donde se creará el archivo
Public ArchivoCreado As String ' Nombre final del archivo creado

Public Sub Form_Open()

  txtNombreArchivo.SetFocus()

End

' Validar que solo se puedan escribir letras a-z, números, punto y permitir teclas de control
Public Sub txtNombreArchivo_KeyPress()
  ' Permitir teclas de control (Backspace, Delete, flechas, etc.)

  If Key.Code = Key.BackSpace Or Key.Code = Key.Delete Or Key.Code = Key.Left Or Key.Code = Key.Right Or Key.Code = Key.Home Or Key.Code = Key.End Then
    Return ' Permite estas teclas
  Endif

  ' Validar solo caracteres permitidos para el nombre del archivo
  If Not InStr("-0123456789abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ.", Key.Text) Then
    Stop Event ' Bloquea la tecla
  Endif

End

Public Sub btnCrearArchivo_Click()

  Dim rutaCompleta As String
  Dim partes As String[]
  Dim extension As String
  Dim contenidoInicial As String

  If txtNombreArchivo.Text = "" Then Return

  ' VALIDAR QUE EL NOMBRE TENGA UNA EXTENSIÓN VÁLIDA
  partes = Split(txtNombreArchivo.Text, ".")
  If partes.Count < 2 Then
    m_Sonido.sonar("Warning")
    Message.Warning("El archivo debe tener una extensión válida (ej: .md).")
    Return
  Endif

  ' OBTENER LA EXTENSIÓN (ÚLTIMA PARTE DESPUÉS DEL PUNTO)
  extension = partes[partes.Count - 1]

  ' VALIDAR QUE LA EXTENSIÓN TENGA ENTRE 2 Y 4 CARACTERES
  If Len(extension) < 2 Or Len(extension) > 4 Then
    m_Sonido.sonar("Warning")
    Message.Warning("La extensión debe tener entre 2 y 4 caracteres.")
    Return
  Endif

  rutaCompleta = RutaDestino &/ txtNombreArchivo.Text

  If Exist(rutaCompleta) Then
    m_Sonido.sonar("Warning")
    Message.Warning("Ya existe un archivo con ese nombre.")
    Return
  Endif

  ' CONTENIDO INICIAL DEL ARCHIVO
  contenidoInicial = "# Línea para validar la creación, se debe borrar" & Chr(10)

  ' CREAR ARCHIVO CON CONTENIDO INICIAL
  File.Save(rutaCompleta, contenidoInicial)

  ArchivoCreado = rutaCompleta
  Me.Close()

End
