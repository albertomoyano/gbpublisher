' Gambas module file

Private sProyecto As String = File.Name(FMain.txtProyecto.Text)
Private sRutaProyecto As String = File.Dir(FMain.txtProyecto.Text)
Private sArchivoProyecto As String = File.BaseName(FMain.txtProyecto.Text)

Public Sub GenerarPDFdesdeLATEX()

  Dim sMain As String
  Dim sPreambulo As String
  Dim sContenido As String
  Dim sMetadatos As String

  m_FuncionesGenericas.BorrarArchivosAuxiliares()

  ' Exportamos los metadatos
  m_FuncionesExportar.ExportarMetadatosPDFlibro()

  ' Leer el contenido del archivo de preámbulo
  sPreambulo = File.Load(sRutaProyecto &/ "files/pdf-preambulo.tex")

  ' Leer el contenido de los metadatos
  sMetadatos = File.Load(sRutaProyecto &/ "files/metadatos.tex")

  ' Leer el contenido del archivo del proyecto
  If Exist(sRutaProyecto &/ sProyecto) Then
    sContenido = File.Load(sRutaProyecto &/ sProyecto)
  Else
    Message.Warning("No se encontró el archivo del proyecto: " & sRutaProyecto &/ sProyecto)
    Return
  Endif

  ' Exportamos las referencias bibliográficas
  m_FuncionesExportar.ExportarBibTeX()

  ' Concatenar ambos contenidos
  sMain = sPreambulo & gb.NewLine & sMetadatos & sContenido

  ' Guardar el archivo temporal con el contenido concatenado
  File.Save(sRutaProyecto &/ "temporal.tex", sMain)
  ' Compilar con latexmk usando el proceso bash existente
  Dim compilar As String
  compilar = "latexmk -l -f -outdir=./salidas/pdf --interaction=nonstopmode -pdflatex=lualatex -pdflua temporal.tex ; " &
    "mv salidas/pdf/temporal.pdf salidas/pdf/" & sArchivoProyecto & ".pdf ; " &
    "rm temporal.tex"
  FMain.TerminalViewProyecto.Input(compilar & "\n")
  Wait 1

  m_FuncionesGenericas.BorrarArchivosAuxiliares

End
