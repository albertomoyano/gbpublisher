' Gambas class file

Public Sub Form_Open()

  ' LIMPIAR CAMPOS AL INICIAR
  TextBoxUsuario.Text = ""
  TextBoxClave.Text = ""
  TextBoxUsuario.SetFocus

  ' COMPROBAR SI YA EXISTE UNA CONEXIÓN ACTIVA
  If Object.IsValid(m_ConexionBD) And Object.IsValid(m_ConexionBD.mConn) Then
    If m_ConexionBD.mConn.Opened Then
      ' HAY UNA CONEXIÓN ACTIVA
      m_Sonido.sonar("Error")
      Message.Error("Ya existe una conexión activa al servidor <b>" & FMain.TextBoxServidor.Text & "</b>" & Chr(10) &
        "Debe desconectarse antes de conectarse a un nuevo servidor.")

      ' MOSTRAR BOTÓN DE DESCONEXIÓN
      btnConectarServer.Visible = False
      btnDesconectarServer.Visible = True

      ' DESHABILITAR CAMPOS PARA EVITAR NUEVA CONEXIÓN
      TextBoxUsuario.Enabled = False
      TextBoxClave.Enabled = False
      TextBoxIP.Enabled = False
    Else
      ' NO HAY CONEXIÓN ACTIVA
      btnConectarServer.Visible = True
      btnDesconectarServer.Visible = False
      TextBoxUsuario.Enabled = True
      TextBoxClave.Enabled = True
      TextBoxIP.Enabled = True
    Endif
  Else
    ' No existe aún el módulo o la conexión
    btnConectarServer.Visible = True
    btnDesconectarServer.Visible = False
    TextBoxUsuario.Enabled = True
    TextBoxClave.Enabled = True
    TextBoxIP.Enabled = True
  Endif

  ' CARGAR IPS DISPONIBLES
  m_FuncionesGenericas.CargarComboBoxIP(TextBoxIP)

End

Public Sub btnCancelar_Click()

  Me.Close

End

Public Sub ToggleButtonMostrar_Click()

  If ToggleButtonMostrar.Value Then
    TextBoxClave.Password = False
  Else
    TextBoxClave.Password = True
  Endif
  TextBoxClave.Refresh()
  TextBoxClave.SetFocus()

End

Public Sub btnConectarServer_Click()

  Dim ip As String = TextBoxIP.Text
  Dim usuario As String = TextBoxUsuario.Text
  Dim clave As String = TextBoxClave.Text
  Dim iResultado As Integer

  ' VALIDACIONES BÁSICAS
  If Trim(ip) = "" Then
    m_Sonido.sonar("Warning")
    Message.Warning("Ingrese una IP correcta")
    Return
  End If

  If Trim(usuario) = "" Then
    m_Sonido.sonar("Warning")
    Message.Warning("Ingrese el nombre de usuario")
    TextBoxUsuario.SetFocus()
    Return
  End If

  If Trim(clave) = "" Then
    m_Sonido.sonar("Warning")
    Message.Warning("Ingrese la contraseña")
    TextBoxClave.SetFocus()
    Return
  End If

  ' ENCRIPTAR CLAVE
  Dim sClaveEncriptada As String = Hash.SHA256(clave)

  ' VERIFICAR CONECTIVIDAD
  If Not ValidarConectividad(ip) Then
    m_Sonido.sonar("Error")
    Message.Error("No se puede conectar al servidor " & ip & Chr(10) &
      "Verifique que el servidor esté encendido y la red funcione correctamente.")
    Return
  End If

  ' INTENTAR CONEXIÓN
  iResultado = m_ConexionBD.ConexionAbierta(ip, usuario, sClaveEncriptada)

  Select Case iResultado
    Case 1
      m_Sonido.sonar("Info")
      Message.Info("Conexión exitosa al servidor " & ip)

      m_InicioCierre.UsuarioEnCurso = usuario
      FMain.MenuInicio.Visible = True
      FMain.ButtonCerrarApp.Visible = False
      FMain.ButtonNotitas.Enabled = True
      FMain.btnFaQ.Enabled = True
      FMain.ButtonBlockNotas.Enabled = True
      FMain.ButtonAdminRepositorios.Enabled = True
      FMain.ButtonConsultasSQL.Enabled = True
      FMain.ButtonCorregirBib.Enabled = True
      FMain.btnAdminRevistas.Enabled = True
      FMain.tbMostrarPanelInferior.Enabled = True
      FMain.btnAdminArticulos.Enabled = True
      FMain.btnAdminAutores.Enabled = True
      FMain.btnDOI.Enabled = True
      FMain.TextBoxUsuario.Text = usuario
      FMain.TextBoxServidor.Text = ip
      FMain.TableViewUsuarios.Visible = True
      FMain.tbMostrarPanelInferior.Enabled = False
      m_InicioCierre.CargarUsuarios()

      ' CARGAMOS LOS COMBOBOX
      m_Bibtex.RellenarTipoDeEntrada([FMain.cmbTipoDeEntrada])
      m_Bibtex.RellenarEditorTypes([FMain.cmbEditorType])
      m_Bibtex.RellenarEditorTypes([FMain.cmbEditorTypeA])
      m_Bibtex.RellenarEditorTypes([FMain.cmbEditorTypeB])
      m_Bibtex.RellenarEditorTypes([FMain.cmbEditorTypeC])
      m_Bibtex.RellenarPagination([FMain.cmbPagination])
      m_Bibtex.RellenarPagination([FMain.cmbBookPagination])
      m_Bibtex.RellenarIdiomas([FMain.cmbHyphenation])
      m_Bibtex.RellenarIdiomas([FMain.cmbLanguage])
      m_Bibtex.RellenarIdiomas([FMain.cmbOrigLanguage])
      m_Bibtex.RellenarIdiomas([FMain.cmbLangId])
      m_Bibtex.RellenarEprint([FMain.cmbEprintType])
      m_Bibtex.RellenarPubState([FMain.cmbPubState])
      m_Bibtex.RellenarRelatedType([FMain.cmbRelatedType])
      m_Bibtex.RellenarTipoDeTesis([FMain.cmbTipoDeTesis])
      m_Bibtex.RellenarTipoDeEntrada([FMain.cmbTipoDeEntrada])

      Me.Close()

    Case 2
      m_Sonido.sonar("Error")
      Message.Error("El usuario ya está conectado o está inactivo.")

    Case 3
      m_Sonido.sonar("Error")
      Message.Error("Usuario y/o clave incorrectos.")

    Case Else
      m_Sonido.sonar("Error")
      Message.Error("Error al intentar conectar al servidor.")
  End Select

End

Public Sub btnDesconectarServer_Click()

  ' CONFIRMAR DESCONEXIÓN
  m_Sonido.sonar("Question")
  If Message.Question("¿Desea desconectarse del servidor actual?", "Sí", "No") = 1 Then

    ' LIBERAR AL USUARIO
    Dim sUsuarioEnCurso As String = m_InicioCierre.UsuarioEnCurso
    If sUsuarioEnCurso <> "" Then
      m_ConexionBD.mConn.Exec("UPDATE usuarios SET en_linea = 0 WHERE usuario = &1", sUsuarioEnCurso)
      m_ConexionBD.mConn.Commit
    End If

    Try m_ConexionBD.mConn.Close

    If Not Error Then
      m_Sonido.sonar("Info")
      Message.Info("Se ha desconectado correctamente del servidor.")

      ' ACTUALIZAR BOTONES
      btnConectarServer.Visible = True
      btnDesconectarServer.Visible = False

      ' LIMPIAR DATOS Y HABILITAR CAMPOS
      TextBoxUsuario.Enabled = True
      TextBoxClave.Enabled = True
      TextBoxUsuario.Text = ""
      TextBoxClave.Text = ""

      ' LIMPIAR EN FMAIN
      FMain.TextBoxServidor.Text = ""
      FMain.TextBoxUsuario.Text = ""
      FMain.MenuInicio.Visible = False
      FMain.ButtonCerrarApp.Visible = True
      FMain.TableViewUsuarios.Visible = False

      ' DESHABILITAR BOTONES DEL MENÚ
      FMain.ButtonNotitas.Enabled = False
      FMain.btnFaQ.Enabled = False
      FMain.ButtonBlockNotas.Enabled = False
      FMain.ButtonAdminRepositorios.Enabled = False
      FMain.ButtonConsultasSQL.Enabled = False
      FMain.ButtonCorregirBib.Enabled = False

    Else
      m_Sonido.sonar("Error")
      Message.Error("No se pudo cerrar la conexión correctamente: " & Error.Text)
    End If

  End If

End

Private Function ValidarConectividad(ip As String) As Boolean

  Dim archivo As String = "/tmp/ping_result_" & CStr(Timer)
  Dim comando As String
  Dim contenido As String

  comando = "ping -c 1 -W 3 " & ip & " > " & archivo & " 2>&1; echo $? >> " & archivo

  Try Shell comando Wait
  If Error Then Return False

  If Exist(archivo) Then
    Try contenido = File.Load(archivo)
    If Not Error Then
      Dim lineas As String[] = Split(contenido, "\n")
      Dim codigoSalida As String = ""
      If lineas.Count > 0 Then
        codigoSalida = Trim(lineas[lineas.Count - 1])
        If codigoSalida = "" And lineas.Count > 1 Then
          codigoSalida = Trim(lineas[lineas.Count - 2])
        End If
      End If
      Try Kill archivo
      Return codigoSalida = "0"
    End If
    Try Kill archivo
  End If

  Return False

End

Public Sub TextBoxClave_KeyPress()

  If Key.Code = Key.Return Then
    Stop Event
    btnConectarServer_Click()
  Endif

End

Public Sub btnNuevaIP_Click()

  f_EditarIPs.ShowModal()
  m_FuncionesGenericas.CargarComboBoxIP(TextBoxIP)

End
