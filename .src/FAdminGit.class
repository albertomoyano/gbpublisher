' Gambas class file

Private Contenido As Result
Private ContenidoBusqueda As Result
Private esBusqueda As Boolean = False

Public Sub Form_Open()

  LimpiarCampos()

  With TableViewAdministracion
    .Header = True
    .Grid = True
    .Columns.Count = 2  ' Solo 2 columnas visibles

    .Columns[0].Title = "Proyecto"
    .Columns[0].Width = 250
    .Columns[1].Title = "Título"
    .Columns[1].Width = 400

    .Mode = Select.Single
    .ScrollBar = Scroll.Vertical
  End With

  CargarGridAdmin()

End

Public Sub ButtonCerrarAdmin_Click()

  Me.Close

End

Public Sub LimpiarCampos()

  txtRevistaTitulo.Text = ""
  txtRepositorio.Text = ""

End

Public Sub CargarGridAdmin()

  TableViewAdministracion.Clear
  Contenido = m_ConexionBD.mConn.Exec("SELECT m.*, p.nombre AS nombre_proyecto FROM metadatos m LEFT JOIN proyectos p ON m.id_proyecto = p.id ORDER BY m.id DESC")
  TableViewAdministracion.Rows.Count = Contenido.Count
  TableViewAdministracion.Refresh()
  esBusqueda = False

End

Public Sub CargarResultadoBusqueda(resultado As Result)

  ContenidoBusqueda = resultado
  TableViewAdministracion.Clear
  TableViewAdministracion.Rows.Count = ContenidoBusqueda.Count
  TableViewAdministracion.Refresh()
  esBusqueda = True

End

Public Sub TableViewAdministracion_Data(Row As Integer, Column As Integer)

  Dim contenidoActual As Result

  If esBusqueda Then
    contenidoActual = ContenidoBusqueda
  Else
    contenidoActual = Contenido
  Endif

  If IsNull(contenidoActual) Then Return
  If Row < 0 Then Return

  contenidoActual.MoveTo(Row)

  Select Case Column
    Case 0
      TableViewAdministracion.Data.Text = contenidoActual["nombre_proyecto"]
    Case 1
      TableViewAdministracion.Data.Text = contenidoActual["revista_titulo"]
  End Select

End

Public Sub TableViewAdministracion_Click()

  Dim contenidoActual As Result
  Dim idSel As Integer
  Dim detalle As Result

  If TableViewAdministracion.Row < 0 Then Return

  If esBusqueda Then
    contenidoActual = ContenidoBusqueda
  Else
    contenidoActual = Contenido
  Endif

  contenidoActual.MoveTo(TableViewAdministracion.Row)
  idSel = contenidoActual["id"]

  ' Cargar los campos reales desde la base
  detalle = m_ConexionBD.mConn.Exec("SELECT revista_titulo, repositorio_github FROM metadatos WHERE id = " & idSel)

  If detalle.Available Then
    txtRevistaTitulo.Text = detalle["revista_titulo"]
    txtRepositorio.Text = detalle["repositorio_github"]
  Endif

End

Public Sub ButtonRfrescar_Click()

  LimpiarCampos()
  CargarGridAdmin()

End

Public Sub btnBuscarTitulo_Click()

  Dim resultado As Result
  Dim sCampoBD As String = "revista_titulo"
  Dim sValor As String = Trim(txtRevistaTitulo.Text)
  Dim sConsulta As String
  Dim sFiltro As String

  If sValor = "" Then
    m_Sonido.sonar("Warning")
    Message.Warning("Debe introducir contenido a buscar.")
    txtRevistaTitulo.SetFocus
    Return
  Endif

  sFiltro = Quote("%" & LCase(sValor) & "%")

  sConsulta = "SELECT m.id, m.revista_titulo, m.repositorio_github, p.nombre AS nombre_proyecto " &
    "FROM metadatos m " &
    "LEFT JOIN proyectos p ON m.id_proyecto = p.id " &
    "WHERE LOWER(m." & sCampoBD & ") LIKE " & sFiltro & " ORDER BY m.id DESC;"

  resultado = m_ConexionBD.mConn.Exec(sConsulta)

  If resultado.Count = 0 Then
    m_Sonido.sonar("Warning")
    Message.Warning("No se ha encontrado ningún resultado.")
    Return
  Endif

  CargarResultadoBusqueda(resultado)

End

Public Sub btnBuscarRepositorio_Click()

  Dim sTITULO As String = Trim(txtRepositorio.Text)

  If sTITULO = "" Then
    m_Sonido.sonar("Error")
    Message.Error("La dirección del repositorio es nula.")
    Return
  Endif

  Desktop.Open(sTITULO)

End
