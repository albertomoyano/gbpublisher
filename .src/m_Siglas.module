' Gambas module file

' MÓDULO m_Siglas

Public ContenidoGlo As Result

Public Sub ConfigurarTableViewGlosarioEnCurso(grid As GridView)

  With grid
    .Header = GridView.Both
    .Grid = True
    .Columns.Count = 11
    .Mode = Select.Single  ' IMPORTANTE: HABILITA SELECCIÓN DE FILAS

    ' CONFIGURACIÓN DE COLUMNAS
    For i As Integer = 0 To 10
      Select Case i
        Case 0
          .Columns[i].Title = "Id"
          .Columns[i].Width = 0
        Case 1
          .Columns[i].Title = "Id Proyecto"
          .Columns[i].Width = 0
        Case 2
          .Columns[i].Title = "Clave"
          .Columns[i].Width = 150
        Case 3
          .Columns[i].Title = "Tipo"
          .Columns[i].Width = 100
        Case 4
          .Columns[i].Title = "Nombre"
          .Columns[i].Width = 150
        Case 5
          .Columns[i].Title = "Descripción"
          .Columns[i].Width = 500
        Case 6
          .Columns[i].Title = ""
          .Columns[i].Width = 0
        Case 7
          .Columns[i].Title = ""
          .Columns[i].Width = 0
        Case 8
          .Columns[i].Title = ""
          .Columns[i].Width = 0
        Case 9
          .Columns[i].Title = ""
          .Columns[i].Width = 0
        Case 10
          .Columns[i].Title = ""
          .Columns[i].Width = 0
      End Select
    Next
  End With

End

Public Sub RefrescarTableViewGlosarioEnCurso()

  Dim gloSQL As String

  ' EJECUTAR CONSULTA Y ACTUALIZAR VARIABLE PÚBLICA
  gloSQL = "SELECT * FROM siglas WHERE id_proyecto = &1 ORDER BY id DESC"
  ContenidoGlo = m_ConexionBD.mConn.Exec(gloSQL, FMain.id_proyecto.Value)

  ' LIMPIAR GRIDVIEW SIEMPRE
  FSiglas.gvSiglasEnCurso.Clear

  ' CONFIGURAR NÚMERO DE FILAS SEGÚN RESULTADO
  If ContenidoGlo <> Null And ContenidoGlo.Available Then
    FSiglas.gvSiglasEnCurso.Rows.Count = ContenidoGlo.Count
  Else
    FSiglas.gvSiglasEnCurso.Rows.Count = 0
  Endif

  ' EL EVENTO Data SE ENCARGARÁ DE POBLAR LAS CELDAS
  ' NO HACEMOS NADA MÁS AQUÍ

End

Public Sub MostrarSiglasEnTableViewGlosarioEnCurso(iRow As Integer)

  ' VERIFICAR QUE EL Result TENGA DATOS Y QUE LA FILA SEA VÁLIDA
  If ContenidoGlo = Null Or ContenidoGlo.Count = 0 Then Return
  If iRow < 0 Or iRow >= ContenidoGlo.Count Then Return

  ' MOVER EL PUNTERO DEL Result A LA FILA SELECCIONADA
  ContenidoGlo.MoveTo(iRow)

  ' POBLAR LOS CONTROLES CON LOS DATOS DE LA FILA
  With FSiglas
    .id_sigla.Value = ContenidoGlo!id
    FMain.id_proyecto.Value = ContenidoGlo!id_proyecto
    .txtClaveGlosario.Text = ContenidoGlo!clave_glosario
    .cmbTipoDeEntradaGlosario.Text = ContenidoGlo!tipo_de_entrada_glosario
    .txtNameGlosario.Text = ContenidoGlo!name_glosario
    .txtDescripcionGlosario.Text = ContenidoGlo!descripcion_glosario
    .txtFirstGlosario.Text = ContenidoGlo!first_glosario
    .txtTextGlosario.Text = ContenidoGlo!text_glosario
    .txtFirstPluraIGlosario.Text = ContenidoGlo!first_plural_glosario
    .txtPluralGlosario.Text = ContenidoGlo!plural_glosario
    .txtSortGlosario.Text = ContenidoGlo!sort_glosario
  End With

End

Public Sub LimpiarTextboxSigla()

  FSiglas.txtClaveGlosario.Text = ""
  FSiglas.cmbTipoDeEntradaGlosario.Text = "sigla"
  FSiglas.txtNameGlosario.Text = ""
  FSiglas.txtDescripcionGlosario.Text = ""
  FSiglas.txtFirstGlosario.Text = ""
  FSiglas.txtTextGlosario.Text = ""
  FSiglas.txtFirstPluraIGlosario.Text = ""
  FSiglas.txtPluralGlosario.Text = ""
  FSiglas.txtSortGlosario.Text = ""

End

Public Sub GuardarCamposSigla()

  ContenidoGlo!id = FSiglas.id_sigla.Value
  ContenidoGlo!id_proyecto = FMain.id_proyecto.Value
  ContenidoGlo!clave_glosario = Trim(FSiglas.txtClaveGlosario.Text)
  ContenidoGlo!tipo_de_entrada_glosario = FSiglas.cmbTipoDeEntradaGlosario.Text
  ContenidoGlo!name_glosario = Trim(FSiglas.txtNameGlosario.Text)
  ContenidoGlo!descripcion_glosario = Trim(FSiglas.txtDescripcionGlosario.Text)
  ContenidoGlo!first_glosario = Trim(FSiglas.txtFirstGlosario.Text)
  ContenidoGlo!text_glosario = Trim(FSiglas.txtTextGlosario.Text)
  ContenidoGlo!first_plural_glosario = Trim(FSiglas.txtFirstPluraIGlosario.Text)
  ContenidoGlo!plural_glosario = Trim(FSiglas.txtPluralGlosario.Text)
  ContenidoGlo!sort_glosario = Trim(FSiglas.txtSortGlosario.Text)

End

Public Sub GuardarSiglaPrimeraVez()

  ContenidoGlo = m_ConexionBD.mConn.Create("siglas")
  GuardarCamposSigla()
  ContenidoGlo.Update
  m_ConexionBD.mConn.Commit()

  ' REFRESCAR VISTA
  RefrescarTableViewGlosarioEnCurso()
  m_Sonido.sonar("Info")
  Message.Info("La entrada se guardó con éxito.")

Catch
  m_Sonido.sonar("Error")
  Message.Error("Error al intentar guardar la entrada: " & Error.Text)

End

Public Sub GuardarCambiosSigla()

  ContenidoGlo = m_ConexionBD.mConn.Edit("siglas", "id=" & FSiglas.id_sigla.Value)

  If Not ContenidoGlo.Available Then
    m_Sonido.sonar("Error")
    Message.Error("No se encontró la referencia para editar.")
    Return
  Endif

  GuardarCamposSigla()
  ContenidoGlo.Update
  m_ConexionBD.mConn.Commit()

  ' REFRESCAR VISTA
  RefrescarTableViewGlosarioEnCurso()
  m_Sonido.sonar("Info")
  Message.Info("Cambios guardados correctamente.")

  FSiglas.btnNuevoGlosario.Visible = True
  FSiglas.btnGuardarGlosario.Visible = False
  FSiglas.btnGuardarCambiosGlosario.Visible = False
  FSiglas.btnBorrarGlosario.Visible = False

Catch
  m_Sonido.sonar("Error")
  Message.Error("Error al guardar los cambios: " & Error.Text)

End

Public Sub NuevoGlosario()

  ' VERIFICAR SI HAY UN PROYECTO ACTIVO
  If Trim(FMain.txtProyecto.Text) = "" Then
    m_Sonido.sonar("Info")
    Message.Info("No hay un proyecto en curso.")
    Return
  Endif

  LimpiarTextboxSigla()

  ' CALCULAR NUEVO ID
  Dim Idn As Integer
  Dim MaxId As Variant

  MaxId = m_ConexionBD.mConn.Exec("SELECT MAX(id) FROM siglas LIMIT 1")

  If MaxId["MAX(id)"] = Null Then
    Idn = 0
  Else
    Idn = CInt(MaxId["MAX(id)"]) + 1
  Endif

  FSiglas.id_sigla.Value = Idn

  ' CONFIGURAR INTERFAZ
  FSiglas.tbPanelGlosarios.Index = 1
  FSiglas.txtTMPglosario.Enabled = True
  FSiglas.txtTMPglosario.Font = Font["Monospace"]
  FSiglas.txtTMPglosario.Background = Color.White
  FSiglas.txtTMPglosario.SetFocus

  FSiglas.btnNuevoGlosario.Visible = False
  FSiglas.btnGuardarGlosario.Visible = True
  FSiglas.btnGuardarCambiosGlosario.Visible = False
  FSiglas.btnBorrarGlosario.Visible = False

End

Public Sub GuardarGlosarioPrimeraVez()

  ' VERIFICAR QUE HAY UNA CLAVE
  If Trim(FSiglas.txtClaveGlosario.Text) = "" Then
    m_Sonido.sonar("Info")
    Message.Info("Debe generar una clave.\nNo es posible guardar la entrada.")
    Return
  Endif

  ' VERIFICAR SI YA EXISTE
  ContenidoGlo = m_ConexionBD.mConn.Exec("SELECT * FROM siglas WHERE name_glosario = &1 AND id_proyecto = &2", Trim(FSiglas.txtNameGlosario.Text), FMain.id_proyecto.Value)

  If ContenidoGlo.Count > 0 Then
    m_Sonido.sonar("Error")
    Message.Error("La sigla o glosario que desea agregar ya existe en este libro/revista.")
    Return
  Endif

  ' GUARDAR
  GuardarSiglaPrimeraVez()
  LimpiarTextboxSigla()

  ' LIMPIAR Y CONFIGURAR INTERFAZ
  FSiglas.txtTMPglosario.Enabled = False
  FSiglas.txtTMPglosario.Background = Color.Background
  FSiglas.txtTMPglosario.Clear
  FSiglas.tbPanelGlosarios.Index = 0

  FSiglas.btnNuevoGlosario.Visible = True
  FSiglas.btnGuardarGlosario.Visible = False
  FSiglas.btnGuardarCambiosGlosario.Visible = False
  FSiglas.btnBorrarGlosario.Visible = False

Catch
  m_Sonido.sonar("Error")
  Message.Error("Se obtuvo el siguiente error al intentar guardar: <b>" & Error.Text & "</b>.")

End

Public Sub BorrarGlosario()

  Dim buscarEliminar As Result

  ' VERIFICAR QUE SE HAYA SELECCIONADO UNA ENTRADA
  If FSiglas.id_sigla.Text = "" Then
    m_Sonido.sonar("Info")
    Message.Info("Debe primero seleccionarse la entrada a borrar.")
    Return
  Endif

  m_Sonido.sonar("Question")
  If Message.Question("¿Desea borrar la entrada?", "Si", "No") = 1 Then
    buscarEliminar = m_ConexionBD.mConn.Exec("DELETE FROM siglas WHERE id=" & FSiglas.id_sigla.Value)
  Endif

  LimpiarTextboxSigla()
  RefrescarTableViewGlosarioEnCurso()

  FSiglas.btnNuevoGlosario.Visible = True
  FSiglas.btnGuardarGlosario.Visible = False
  FSiglas.btnGuardarCambiosGlosario.Visible = False
  FSiglas.btnBorrarGlosario.Visible = False

End

Public Sub IniciarSiglas()

  LimpiarTextboxSigla()
  RefrescarTableViewGlosarioEnCurso()

End
