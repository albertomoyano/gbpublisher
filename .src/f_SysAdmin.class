' Gambas class file

Public hConn As Connection
Private sRuta As String = User.Home &/ ".gbpublisher/.superuser.sqlite"

Public Function CreateDataBase() As Boolean

  Dim sDir As String
  Dim sBase As String
  Dim sTablas As String

  sDir = User.Home &/ ".gbpublisher"
  sBase = ".superuser.sqlite"
  sRuta = sDir &/ sBase

  ' Crear directorio si no existe
  If Not Exist(sDir) Then Mkdir sDir

  ' Si la base ya existe, salir devolviendo False
  If Exist(sRuta) Then Return False

  ' Crear conexión y base
  hConn = New Connection
  hConn.Type = "sqlite3"
  hConn.Host = sDir
  hConn.Open()

  sTablas = "CREATE TABLE superuser (" &
    "id INTEGER PRIMARY KEY AUTOINCREMENT, " &
    "usuario TEXT, " &
    "clave TEXT, " &
    "email TEXT);"

  hConn.Databases.Add(sBase)
  Wait 0.2
  hConn.Close()

  hConn.Name = sRuta
  hConn.Open()
  hConn.Exec(sTablas)
  hConn.Close()
  m_Sonido.sonar("Info")
  Message.Info("Base de datos creada con éxito.")
  Return True

Catch
  m_Sonido.sonar("Error")
  Message.Error("Error creando la base de datos:\n" & Error.Text & "\n" & Error.Where)
  Return False

End

Public Sub Form_Open()

  ' Ponemos en True
  txtClaveAdministrador.Password = True
  m_FuncionesGenericas.CargarComboBoxIP(ComboBoxIP)

  ' Si no existe, la crea y devuelve True si fue creada
  Dim creada As Boolean = CreateDataBase()

  If creada Then
    ' Primera ejecución: base recién creada
    HBoxCrearAdmin.Visible = True
    HBoxValidar.Visible = False
    btnActualizarAdmin.Visible = False
    m_Sonido.sonar("Info")
    Message.Info("Debe crear el administrador inicial.")
  Else
    ' Ya existía
    HBoxCrearAdmin.Visible = False
    HBoxValidar.Visible = True
    btnActualizarAdmin.Visible = True
  Endif

  ' Ocultar elementos adicionales siempre
  HBox1a.Visible = False
  HBox2a.Visible = False
  HBox3a.Visible = False
  HBox4a.Visible = False
  HBox5a.Visible = False
  TableViewUsuarios.Visible = False

End

Public Sub btnCerrar_Click()

  Me.Close()

End

Public Sub btnCrearAdmin_Click()

  Dim sUsuario As String = Trim(txtAdministrador.Text)
  Dim sClave As String = Trim(txtClaveAdministrador.Text)
  Dim sEmail As String = Trim(txtEmailAdministrador.Text)
  Dim dbLocal As Connection
  Dim sClaveHash As String
  Dim bTieneMayuscula As Boolean = False
  Dim bTieneNumero As Boolean = False
  Dim i As Integer

  ' Validaciones
  If Not sUsuario Or Not sClave Then
    m_Sonido.sonar("Warning")
    Message.Warning("Usuario y contraseña son obligatorios.")
    Return
  Endif

  ' Validar longitud mínima de contraseña
  If Len(sClave) < 8 Then
    m_Sonido.sonar("Warning")
    Message.Warning("La contraseña debe tener 8 caracteres.")
    Return
  Endif

  ' Validar contraseña: debe tener al menos una mayúscula y un número
  For i = 1 To Len(sClave)
    If Mid(sClave, i, 1) >= "A" And Mid(sClave, i, 1) <= "Z" Then
      bTieneMayuscula = True
    Endif
    If IsDigit(Mid(sClave, i, 1)) Then
      bTieneNumero = True
    Endif
  Next

  If Not bTieneMayuscula Or Not bTieneNumero Then
    m_Sonido.sonar("Warning")
    Message.Warning("La contraseña debe incluir al menos una letra mayúscula y un número.")
    Return
  Endif

  If sEmail And Not IsValidEmail(sEmail) Then
    m_Sonido.sonar("Warning")
    Message.Warning("El formato del email no es válido.")
    Return
  Endif

  ' Conectar a la base
  dbLocal = New Connection
  dbLocal.Type = "sqlite3"
  dbLocal.Name = sRuta
  Try dbLocal.Open()
  If Error Then
    m_Sonido.sonar("Error")
    Message.Error("Error al abrir la base:\n" & Error.Text)
    Return
  Endif

  ' Insertar el primer admin
  sClaveHash = Hash.SHA256(sClave)
  Try dbLocal.Exec("INSERT INTO superuser (usuario, clave, email) VALUES (?, ?, ?);",
    [sUsuario, sClaveHash, sEmail])
  If Error Then
    m_Sonido.sonar("Error")
    Message.Error("Error al crear el administrador:\n" & Error.Text)
    dbLocal.Close()
    Return
  Endif

  dbLocal.Close()
  m_Sonido.sonar("Info")
  Message.Info("Administrador creado correctamente.\n\nAhora puede validar su acceso.")

  ' Cambiar visibilidad de botones
  HBoxCrearAdmin.Visible = False
  HBoxValidar.Visible = True
  btnActualizarAdmin.Visible = True

  ' Limpiar campos
  txtClaveAdministrador.Text = ""

End

Private Function IsValidEmail(sEmail As String) As Boolean

  If Not sEmail Then Return False
  If InStr(sEmail, "@") = 0 Then Return False
  If InStr(sEmail, ".") = 0 Then Return False
  Return True

End

Public Sub ToggleButtonMostrar_Click()

  ' Invertimos la lógica: mostrar caracteres si el botón está activado
  If ToggleButtonMostrar.Value Then
    txtClaveAdministrador.Password = False  ' Mostrar texto
  Else
    txtClaveAdministrador.Password = True   ' Ocultar texto
  Endif

  ' Forzar redibujado y mantener el foco correctamente
  txtClaveAdministrador.Refresh()
  txtClaveAdministrador.SetFocus()

End

Public Sub btnNuevaIP_Click()

  f_EditarIPs.ShowModal()

  ' Refrescar el ComboBox inmediatamente después de cerrar el modal
  m_FuncionesGenericas.CargarComboBoxIP(ComboBoxIP)

End

Public Sub txtAdministrador_KeyPress()

  Dim sChar As String = Key.Text

  ' Permitir teclas de control (Backspace=8, Delete=127, etc.)
  If Key.Code = Key.BackSpace Or Key.Code = Key.Delete Or
      Key.Code = Key.Left Or Key.Code = Key.Right Or
      Key.Code = Key.Home Or Key.Code = Key.End Then
    Return
  Endif

  ' Permitir solo letras minúsculas a-z
  If sChar Then
    If sChar < "a" Or sChar > "z" Then
      Stop Event
    Endif
  Endif

End

Public Sub txtUsuario_KeyPress()

  Dim sChar As String = Key.Text

  ' Permitir teclas de control (Backspace=8, Delete=127, etc.)
  If Key.Code = Key.BackSpace Or Key.Code = Key.Delete Or
      Key.Code = Key.Left Or Key.Code = Key.Right Or
      Key.Code = Key.Home Or Key.Code = Key.End Then
    Return
  Endif

  ' Permitir solo letras minúsculas a-z
  If sChar Then
    If sChar < "a" Or sChar > "z" Then
      Stop Event
    Endif
  Endif

End
