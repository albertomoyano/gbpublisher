' Gambas class file

Private $aCodigosIdioma As String[]
Private $hClient As HttpClient

Public Sub Form_Open()

  ' Configurar idiomas disponibles
  cmbIdiomas.Clear()
  cmbIdiomas.Add("Español")
  cmbIdiomas.Add("Inglés")
  ' cmbIdiomas.Add("Francés")
  ' cmbIdiomas.Add("Alemán")
  ' cmbIdiomas.Add("Italiano")
  ' cmbIdiomas.Add("Portugués")

  ' Array paralelo con códigos ISO
  $aCodigosIdioma = ["es", "en", "fr", "de", "it", "pt-BR"]

  ' Seleccionar español por defecto
  cmbIdiomas.Index = 0

  ' Mensaje inicial si el textbox está vacío
  If Trim(txtPalabra.Text) = "" Then
    HtmlViewDiccionario.Html = ("<p>Ingrese una palabra y presione Consultar.</p>")
  Else
    ' Si ya hay palabra, consultar automáticamente
    ConsultarDiccionario()
  Endif

End

Public Sub btnConsultar_Click()

  ConsultarDiccionario()

End

Private Sub ConsultarDiccionario()

  Dim sPalabra As String
  Dim sIdioma As String
  Dim sUrl As String

  sPalabra = Trim(txtPalabra.Text)
  If sPalabra = "" Then
    Message.Info("Ingrese una palabra para consultar.")
    Return
  Endif

  '--- elegimos motor según idioma seleccionado -----------------
  sIdioma = $aCodigosIdioma[cmbIdiomas.Index]

  Select Case sIdioma
    Case "es"                                                  'ESPAÑOL
      sUrl = "https://rae-api.com/api/words/" & sPalabra
    Case "en"                                                  'INGLÉS
      sUrl = "https://api.dictionaryapi.dev/api/v2/entries/en/" & sPalabra
    Case Else                                                  'OTROS (por ahora igual)
      sUrl = "https://api.dictionaryapi.dev/api/v2/entries/" & sIdioma & "/" & sPalabra
  End Select

  '--- petición HTTP -------------------------------------------
  HtmlViewDiccionario.Html = ("<p><i>Consultando...</i></p>")

  $hClient = New HttpClient As "ClienteDiccionario"
  $hClient.URL = sUrl
  $hClient.Async = True
  $hClient.TimeOut = 10
  $hClient.Get()

End

Public Sub ClienteDiccionario_Finished()

  Dim sRespuesta As String
  Dim vJson As Variant
  Dim sHtml As String

  ' Leer toda la respuesta de golpe
  sRespuesta = Read #Last, Lof(Last)   'lee exactamente los bytes disponibles

  ' Debug
  Print "HTTP Code: "; Last.Code
  Print "Respuesta (primeros 5000 chars): "; Left(sRespuesta, 5000)

  ' Verificar código HTTP
  If Last.Code <> 200 Then
    HtmlViewDiccionario.Html = ("<p style='color:red;'>No se encontró definición para esta palabra.</p>")
    Return
  Endif

  ' Parsear JSON
  Try vJson = JSON.Decode(sRespuesta)
  If Error Then
    HtmlViewDiccionario.Html = ("<p style='color:red;'>Error al procesar respuesta: " & Error.Text & "</p>")
    Return
  Endif

  Print "JSON recibido: "; JSON.Encode(vJson, 0)

  ' Generar HTML
  Try sHtml = GenerarHtmlDefiniciones(vJson, $aCodigosIdioma[cmbIdiomas.Index])
  If Error Then
    HtmlViewDiccionario.Html = ("<p style='color:red;'>Error al generar HTML: " & Error.Text & "</p>")
    Return
  Endif

  HtmlViewDiccionario.Html = sHtml

Catch
  HtmlViewDiccionario.Html = ("<p style='color:red;'>Error: " & Error.Text & "</p>")

End

Public Sub ClienteDiccionario_Error()

  HtmlViewDiccionario.Html = ("<p style='color:red;'>Error de conexión: No se pudo conectar al servidor.</p>")

End

Private Function GenerarHtmlDefiniciones(vJson As Variant, sIdioma As String) As String

  Dim vData As Variant
  Dim sHtml As String
  Dim vMean As Variant
  Dim vDef As Variant
  Dim i, j As Integer
  Dim sPalabra As String

  '--- normalizamos entrada (array u objeto) --------------
  Try vData = vJson[0]          '¿es array?
  If Error Then
    vData = vJson               'no, es objeto simple
  Endif

  '--- palabra ---------------------------------------------------------
  sPalabra = vData["word"]
  sHtml = "<html><body style='font-family:Arial; padding:10px;'>"
  sHtml &= "<h2>" & sPalabra & "</h2>"

  '---------------- RAE (es) -------------------------------------------
  If sIdioma = "es" And vData.Exist("meanings") Then
    sHtml &= "<ol>"
    For Each vDef In vData["meanings"]
      sHtml &= "<li>" & vDef["definition"] & "</li>"
    Next
    sHtml &= "</ol></body></html>"
    Return sHtml
  Endif

  '---------------- dictionaryapi.dev (resto) ---------------------------
  For i = 0 To vData["meanings"].Count - 1
    vMean = vData["meanings"][i]
    sHtml &= "<h3>" & vMean["partOfSpeech"] & "</h3><ol>"
    For j = 0 To vMean["definitions"].Count - 1
      vDef = vMean["definitions"][j]
      sHtml &= "<li>" & vDef["definition"]
      If vDef.Exist("example") And vDef["example"] Then
        sHtml &= "<br><i>Ejemplo: " & vDef["example"] & "</i>"
      Endif
      sHtml &= "</li>"
    Next
    sHtml &= "</ol>"
  Next

  sHtml &= "</body></html>"
  Return sHtml

End

Public Sub btnCerrar_Click()

  Me.Close()

End
