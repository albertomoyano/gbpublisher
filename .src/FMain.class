' Gambas class file

Public rutaApp As String = User.Home &/ ".gbpublisher"
Public $Bash As Process
Public TipoProyectoActual As Integer
Private ContenidoArt As Result
Private ContenidoAut As Result
Private LastTextBox As TextBox
Private sTB_Text As String
Public NombreProyecto As String
Public RutaProyecto As String
Public ProyectoActualCompleto As String
Public sRutaArchivoAbierto As String
Public sContenidoOriginal As String  ' PARA COMPARAR SI HUBO CAMBIOS
Private Const PDFJS_VIEWER As String = "/opt/pdfjs/web/viewer.html"

' Private sRutaSeleccionada As String

Public Sub SplitterPrincipal_Resize()

  SplitterPrincipal.Layout = [1, 1]

End

Public Sub TimerDateNow_Timer()

  Dim Dias As String[] = ["Domingo", "Lunes", "Martes", "Miércoles", "Jueves", "Viernes", "Sábado"]
  Dim nDia As Integer = WeekDay(Now())        ' Devuelve 0 = Domingo, 1 = Lunes, etc.
  Dim sFechaHora As String = Format(Now(), "dd/mm/yyyy hh:nn:ss")

  TextBoxDateNow.Text = Dias[nDia] & ", " & sFechaHora

End

' DIVISOR DE CONSOLA
Public Sub Splitter1_Resize()

  Splitter1.Layout = [2, 1]

End

Public Sub Form_Open()

  ' MOSTRAR DIA Y HORA
  TimerDateNow.Delay = 1000    ' 1 segundo
  TimerDateNow.Enabled = True

  ' HACEMOS UN RESPALDO DE LA BASE DE DATOS
  RespaldarBaseDatos()

  Dim versionRemota As String
  Dim name As String

  ' Configurar relojes
  TimerTemp.Delay = 2000
  TimerTemp.Enabled = True
  TimerNotitas.Delay = 5000
  TimerNotitas.Start

  $Bash = TerminalViewProyecto.Shell("/usr/bin/bash")

  ' CREAMOS EL DIRECTORIO OCULTO DEL PROGRAMA Y COPIAMOS ARCHIVOS DE INICIO
  m_InicioCierre.DirectorioOcultoApp()

  ' COMPROBAMOS DIFERENCIA DE VERSIÓN
  versionRemota = m_FuncionesGenericas.GitVersion("https://raw.githubusercontent.com/albertomoyano/gbpublisher/refs/heads/main/.project")
  If versionRemota <> "" And versionRemota <> Application.Version Then
    m_Sonido.sonar("Info")
    Message.Info("La versión instalada que estás ejecutando (" & Application.Version & ") difiere de la disponible en el repositorio (" & versionRemota & ")")
  Endif

  ' ' VERIFICACIÓN DE INSTANCIAS MÚLTIPLES DE LA APP
  ' Exec ["pgrep", "-fl", Application.Name] Wait To name
  ' If Split(Trim$(name), gb.NewLine, "", True).Count > 1 Then
  '   m_Sonido.sonar("Warning")
  '   Message.Warning(Application.Name & " ya se está ejecutando en este equipo.")
  '   Quit
  ' Endif

  TimerHOY.Delay = 1000
  TimerHOY.Stop' detenemos el reloj
  TextHoraParcial.Text = "00:00:00"

  txtEditorProyecto.Visible = True
  PictureBoxProyecto.Visible = False
  SplitterPrincipal.Layout = [1, 1]
  Splitter1.Layout = [2, 1]' divisor de consola

  PictureBoxProyecto.Picture = Null

  m_Bibtex.ConfigurarTableViewBibtexEnCurso(gvReferenciasEnCurso)
  m_Siglas.ConfigurarTableViewGlosarioEnCurso(gvSiglasEnCurso)
  m_Articulos.ConfigurarTableViewArticulos(gbMetadatosArticulos)
  m_InicioCierre.DisenarTableViewUsuarios(TableViewUsuarios)

  ' LOAD THE HIGHLIGHT FILE
  If Not TextHighlighter.List.Exist("markdown") Then
    TextHighlighter.Register("markdown", "Markdown", Application.Path &/ "Themes/Markdown.highlight")
  End If

  ' EDITOR SETTINGS
  txtEditorProyecto.ShowLineNumber = True
  txtEditorProyecto.ShowCurrent = True
  txtEditorProyecto.ShowModified = True
  txtEditorProyecto.ShowLimit = False
  txtEditorProyecto.ShowSpaces = True
  txtEditorProyecto.Wrap = True
  vbPanelIzquierdo.Visible = False

  ' CONFIGURAR FUENTE
  txtEditorProyecto.Font = Font["Roboto Mono,13"]

  ' ===== ESTILOS COMPLETOS =====

  ' ' Estilos básicos
  ' txtEditorProyecto.Theme["Normal"].Color = Color.Black            ' Texto normal
  ' txtEditorProyecto.Theme["Normal"].Bold = False
  ' txtEditorProyecto.Theme["Highlight"].Color = Color.Yellow        ' Resaltado
  ' txtEditorProyecto.Theme["Highlight"].Bold = False
  ' txtEditorProyecto.Theme["Constant"].Color = &H005CC5&            ' Constantes
  ' txtEditorProyecto.Theme["Constant"].Bold = False
  ' txtEditorProyecto.Theme["String"].Color = Color.Cyan '           ' Strings/Código
  ' txtEditorProyecto.Theme["String"].Bold = False
  ' txtEditorProyecto.Theme["Escape"].Color = &HD73A49&              ' Secuencias escape
  ' txtEditorProyecto.Theme["Escape"].Bold = False
  ' txtEditorProyecto.Theme["Breakpoint"].Color = &HFF0000&          ' Puntos de interrupción
  ' txtEditorProyecto.Theme["Breakpoint"].Bold = False
  ' txtEditorProyecto.Theme["Function"].Color = Color.Cyan
  ' txtEditorProyecto.Theme["Function"].Bold = False
  ' txtEditorProyecto.Theme["Removed"].Color = &HB31D28&             ' Líneas eliminadas
  ' txtEditorProyecto.Theme["Removed"].Bold = False

  ' CONFIGURAR FONDO DEL EDITOR
  Try txtEditorProyecto.Background = Color.RGB(240, 240, 240)

  ' APLICAR HIGHLIGHT CON TEMA GITHUB LIGHT
  If TextHighlighter.List.Exist("markdown") Then
    txtEditorProyecto.Highlight = "markdown"

    ' SHORTCODES (enquote, rdm)
    txtEditorProyecto.Theme["Function"].Color = Color.DarkMagenta
    txtEditorProyecto.Theme["Function"].Bold = True
    ' imagenes
    txtEditorProyecto.Theme["Expansion"].Color = Color.DarkOrange
    txtEditorProyecto.Theme["Expansion"].Bold = False
    ' citas
    txtEditorProyecto.Theme["Documentation"].Color = Color.DarkCyan
    txtEditorProyecto.Theme["Documentation"].Bold = False
    ' numeros
    txtEditorProyecto.Theme["Number"].Color = Color.Magenta
    txtEditorProyecto.Theme["Number"].Bold = False
    txtEditorProyecto.Theme["Operator"].Color = Color.Magenta
    txtEditorProyecto.Theme["Operator"].Bold = False
    ' tablas
    txtEditorProyecto.Theme["Symbol"].Color = Color.DarkCyan
    txtEditorProyecto.Theme["Symbol"].Bold = False
    ' url
    txtEditorProyecto.Theme["Datatype"].Color = Color.Green
    txtEditorProyecto.Theme["Datatype"].Bold = False
    ' cabeceras
    txtEditorProyecto.Theme["Header"].Color = Color.Blue
    txtEditorProyecto.Theme["Header"].Bold = True
    ' texto codigo
    txtEditorProyecto.Theme["String"].Color = &H005CC5&
    txtEditorProyecto.Theme["String"].Bold = False
    ' bold
    txtEditorProyecto.Theme["Keyword"].Color = Color.Black
    txtEditorProyecto.Theme["Keyword"].Bold = True
    ' bastardillas
    txtEditorProyecto.Theme["Comment"].Color = Color.Orange
    txtEditorProyecto.Theme["Comment"].Oblique = True
    ' referencias
    txtEditorProyecto.Theme["Added"].Color = Color.Purple
    txtEditorProyecto.Theme["Added"].Bold = False
    ' footnote
    txtEditorProyecto.Theme["Preprocessor"].Color = Color.DarkGreen
    txtEditorProyecto.Theme["Preprocessor"].Bold = True
    ' error
    txtEditorProyecto.Theme["Error"].Color = Color.Red
    txtEditorProyecto.Theme["Error"].Bold = False

  End If

  txtEditorProyecto.Refresh()

  ' OCULTAMOS EL PANEL INFERIOR IZQUIERDO Y BUSCAR LINEA
  TabPanelIzquierdaInferior.Visible = False
  hbGoTo.Visible = False

  ' CONFIGURAR EL GRID DE COMENTARIOS
  m_GestorComentarios.ConfigurarGridComentarios(gvComentarios)

  ' CARGAR CONFIGURACIÓN DE TIPOGRAFÍA DEL EDITOR
  LoadFontSettings()

End

Public Sub TimerTemp_Timer()

  Dim sOutput As String
  Dim Tctl As String = ""
  Dim Tccd1 As String = ""
  Dim Tccd2 As String = ""
  Dim linea As String
  Dim partes As String[]

  ' EJECUTAR SENSORS Y OBTENER TODA LA SALIDA
  sOutput = ""
  Exec ["sensors"] To sOutput

  ' PROCESAR LÍNEA POR LÍNEA
  For Each linea In Split(sOutput, "\n")
    linea = Trim(linea)

    ' BUSCAR TCTL
    If Left(linea, 5) = "Tctl:" And InStr(linea, "+") > 0 Then
      partes = Split(linea, "+")
      If partes.Count > 1 Then
        partes = Split(partes[1], "°")
        If partes.Count > 0 Then
          Tctl = Trim(partes[0])
        Endif
      Endif

      ' BUSCAR TCCD1
    Else If Left(linea, 6) = "Tccd1:" And InStr(linea, "+") > 0 Then
      partes = Split(linea, "+")
      If partes.Count > 1 Then
        partes = Split(partes[1], "°")
        If partes.Count > 0 Then
          Tccd1 = Trim(partes[0])
        Endif
      Endif

      ' BUSCAR TCCD2
    Else If Left(linea, 6) = "Tccd2:" And InStr(linea, "+") > 0 Then
      partes = Split(linea, "+")
      If partes.Count > 1 Then
        partes = Split(partes[1], "°")
        If partes.Count > 0 Then
          Tccd2 = Trim(partes[0])
        Endif
      Endif
    Endif
  Next

  ' ACTUALIZAR TÍTULO
  If Tctl <> "" And Tccd1 <> "" And Tccd2 <> "" Then
    FMain.Title = Application.Title & " v. " & Application.Version & " | Procesador: " & Tctl & "°C | CCD1: " & Tccd1 & "°C | CCD2: " & Tccd2 & "°C"
  Endif

End

Public Sub DirViewProyecto_Click()' Evento click en DirView actualiza los archivos que se muestran en FileViewProyecto

  Dim bloquear As Boolean = False

  If Not bloquear Then
    FileViewProyecto.Dir = DirViewProyecto.Current
  Endif

End

' EVENTO QUE SE EJECUTA CUANDO SE DETECTA UN CAMBIO EN EL DIRECTORIO
Public Sub Watcher_Change()

  DirViewProyecto.Root = File.Dir(txtProyecto.Text)' REFORZAMOS LA INDICACIÓN DE LA RUTA
  DirViewProyecto.Refresh
  FileViewProyecto.Refresh

End

' EN FMAIN - EVENTO FORM_CLOSE()
Public Sub Form_Close()

  ' MOSTRAR MENSAJE INFORMATIVO
  m_Sonido.sonar("info")
  Message.Info("Para cerrar la aplicación correctamente,\nuse el menú <b>Cerrar el programa</b> o el botón con la <b>X</b> (abajo a la derecha) según corresponda.")

  ' CANCELAR EL CIERRE
  Stop Event

End

Public Sub btnCrearCarpeta_Click()

  If DirViewProyecto.Current = "" Then
    m_Sonido.sonar("Warning")
    Message.Warning("Seleccioná un directorio en el árbol primero.")
    Return
  Endif

  ' GUARDAMOS LA RUTA ACTUAL COMO DESTINO
  f_AgregarCarpeta.RutaDestino = DirViewProyecto.Current
  f_AgregarCarpeta.ShowModal

  ' SI SE CREÓ LA CARPETA, ACTUALIZAMOS EL FILEVIEW
  If f_AgregarCarpeta.CarpetaCreada Then
    FileViewProyecto.Dir = DirViewProyecto.Current
  Endif

  DirViewProyecto.Reload

End

Public Sub btnBorrarCarpeta_Click()

  Dim ruta As String = DirViewProyecto.Current

  ' VERIFICAR QUE LA RUTA EXISTE Y ES UNA CARPETA
  If Not Exist(ruta) Or Not IsDir(ruta) Then
    m_Sonido.sonar("Warning")
    Message.Warning("La ruta seleccionada no es una carpeta válida.")
    Return
  End If

  ' CONFIRMAR ACCIÓN
  m_Sonido.sonar("Question")
  If Message.Question("¿Seguro que deseas borrar la carpeta y TODO su contenido, incluyendo subcarpetas y archivos?", "Sí", "Cancelar") <> 1 Then
    Return
  End If

  ' INTENTAR BORRAR LA CARPETA
  If BorrarCarpeta(ruta) Then
    DirViewProyecto.Reload
    m_Sonido.sonar("Info")
    Message.Info("Carpeta borrada exitosamente.")
  Else
    m_Sonido.sonar("Error")
    Message.Error("No se pudo borrar la carpeta completamente.")
  End If

End

Private Function BorrarCarpeta(RutaCarpeta As String) As Boolean

  Dim archivos As String[]
  Dim archivo As String
  Dim rutaCompleta As String

  ' OBTENER LISTA DE ARCHIVOS Y CARPETAS
  Try archivos = Dir(RutaCarpeta, "*", gb.File + gb.Directory)
  If Error Then
    Return False
  End If

  ' PROCESAR CADA ELEMENTO
  For Each archivo In archivos
    ' SALTAR LAS ENTRADAS "." Y ".."
    If archivo = "." Or archivo = ".." Then Continue

    rutaCompleta = RutaCarpeta &/ archivo

    If IsDir(rutaCompleta) Then
      ' ES UNA SUBCARPETA, LLAMADA RECURSIVA
      If Not BorrarCarpeta(rutaCompleta) Then
        Return False
      End If
    Else
      ' ES UN ARCHIVO, BORRARLO
      Try Kill rutaCompleta
      If Error Then
        Return False
      End If
    End If
  Next

  ' FINALMENTE, BORRAR LA CARPETA VACÍA
  Try Rmdir RutaCarpeta
  If Error Then
    Return False
  End If

  Return True

End Function

' EVENTO PARA AGREGAR ARCHIVO
Public Sub btnAgregarArchivo_Click()

  ' MOSTRAR FORMULARIO DE CREACIÓN
  f_AgregarArchivo.RutaDestino = DirViewProyecto.Current
  f_AgregarArchivo.ShowModal

  ' SI SE CREÓ, ACTUALIZAMOS LA VISTA
  If f_AgregarArchivo.ArchivoCreado Then
    FileViewProyecto.Dir = DirViewProyecto.Current
  Endif

  FileViewProyecto.Reload

End

Public Sub btnBorrarArchivo_Click()

  Dim archivoSeleccionado As String
  Dim rutaCompleta As String

  If FileViewProyecto.Current = "" Then
    m_Sonido.sonar("Warning")
    Message.Warning("No hay ningún archivo seleccionado")
    Return
  Endif

  archivoSeleccionado = FileViewProyecto.Current
  rutaCompleta = FileViewProyecto.Dir &/ archivoSeleccionado

  m_Sonido.sonar("Question")

  If Message.Question("¿Está seguro que desea eliminar el archivo '" & archivoSeleccionado & "'?", "Eliminar", "Cancelar") <> 1 Then
    Return
  Endif

  Try Kill rutaCompleta
  If Error Then
    m_Sonido.sonar("Error")
    Message.Error("Error al eliminar el archivo: " & Error.Text)
  Else
    m_Sonido.sonar("Info")
    Message.Info("Archivo eliminado correctamente")
    FileViewProyecto.Current = ""
    FileViewProyecto.Reload
  Endif

End

Public Sub btnVerIMAGEN_Click()

  Dim sArchivoSeleccionado As String
  Dim sRutaCompleta As String
  Dim sExtension As String
  Dim aExtensionesImagen As String[]

  ' DEFINIR LAS EXTENSIONES DE IMAGEN PERMITIDAS
  aExtensionesImagen = ["png", "jpg", "jpeg", "gif", "bmp", "tiff", "tif", "webp"]

  ' VERIFICAR QUE HAY UN ARCHIVO SELECCIONADO EN EL FILEVIEW
  If FileViewProyecto.Current = "" Then
    m_Sonido.sonar("Warning")
    Message.Warning("Por favor seleccione un archivo de la lista")
    Return
  Endif

  ' OBTENER EL ARCHIVO SELECCIONADO
  sArchivoSeleccionado = FileViewProyecto.Current

  ' CONSTRUIR LA RUTA COMPLETA DEL ARCHIVO
  sRutaCompleta = FileViewProyecto.Dir &/ sArchivoSeleccionado

  ' VERIFICAR QUE EL ARCHIVO EXISTE
  If Not Exist(sRutaCompleta) Then
    m_Sonido.sonar("Error")
    Message.Error("El archivo seleccionado no existe")
    Return
  Endif

  ' OBTENER LA EXTENSIÓN DEL ARCHIVO
  sExtension = LCase(File.Ext(sArchivoSeleccionado))

  ' VERIFICAR QUE ES UN ARCHIVO DE IMAGEN VÁLIDO
  If aExtensionesImagen.Find(sExtension) = -1 Then
    m_Sonido.sonar("Warning")
    Message.Warning("El archivo seleccionado no es una imagen válida.\nExtensiones permitidas: ." & aExtensionesImagen.Join(", ."))
    Return
  Endif

  ' CARGAR Y MOSTRAR LA IMAGEN
  Try PictureBoxProyecto.Picture = Picture.Load(sRutaCompleta)

  If Error Then
    ' SI HAY ERROR AL CARGAR LA IMAGEN
    PictureBoxProyecto.Picture = Null
    PictureBoxProyecto.Visible = False
  Else
    ' SI LA IMAGEN SE CARGÓ CORRECTAMENTE
    PictureBoxProyecto.Visible = True
    TabPanelVisor.Index = 1
  Endif

  If Error Then
    m_Sonido.sonar("Error")
    Message.Error("Error al cargar la imagen:\n" & Error.Text)
    ' LIMPIAR EL IMAGEVIEW EN CASO DE ERROR
    PictureBoxProyecto.Picture = Null
  Endif

End

Public Sub menuPDFcompleto_Click()

  If Not Exist(File.Dir(txtProyecto.Text) & "/salidas/pdf") Then
    Mkdir File.Dir(txtProyecto.Text) & "/salidas/pdf"
    DirViewProyecto.Reload
  End If

End

Public Sub menuHTMLcompleto_Click()

  If Not Exist(File.Dir(txtProyecto.Text) & "/salidas/html") Then
    Mkdir File.Dir(txtProyecto.Text) & "/salidas/html"
    DirViewProyecto.Reload
  End If

End

Public Sub menuXMLpmd_Click()

  If Not Exist(File.Dir(txtProyecto.Text) & "/salidas/xml") Then
    Mkdir File.Dir(txtProyecto.Text) & "/salidas/xml"
    DirViewProyecto.Reload
  End If

End

Public Sub menuSCIELO_Click()

  If Not Exist(File.Dir(txtProyecto.Text) & "/salidas/xml") Then
    Mkdir File.Dir(txtProyecto.Text) & "/salidas/xml"
    DirViewProyecto.Reload
  End If

End

Public Sub btnGuardarCambiosMetadatosRevista_Click()

  m_Metadatos.GuardarMetadatos()
  ' Refrescás Orden de Taller
  m_OrdenTaller.CargarOrdenTaller()

End

Public Sub btnNuevoArticulo_Click()

  m_Articulos.ObtenerIdNuevoArticulo()

  btnNuevoArticulo.Visible = False
  btnGuardarArticulo.Visible = True
  btnGuardarCambiosArticulo.Visible = False
  btnBorrarArticulo.Visible = False

End

Public Sub btnGuardarArticulo_Click()

  m_Articulos.GuardarArticuloPrimeraVez()
  m_Articulos.RefrescarTableViewArticulos()
  m_Articulos.LimpiarTextboxArticulos()
  ' MOSTRAMOS ARTICULOS EN AUTORES Y REFERENCIAS
  m_FuncionesGenericas.LlenarComboBoxArticulos()

  btnNuevoArticulo.Visible = True
  btnGuardarArticulo.Visible = False
  btnGuardarCambiosArticulo.Visible = True
  btnBorrarArticulo.Visible = False

End

Public Sub btnGuardarCambiosArticulo_Click()

  m_Articulos.GuardarCambiosArticulo()
  m_Articulos.RefrescarTableViewArticulos()
  m_Articulos.LimpiarTextboxArticulos()
  ' MOSTRAMOS ARTICULOS EN AUTORES Y REFERENCIAS
  m_FuncionesGenericas.LlenarComboBoxArticulos()

  btnNuevoArticulo.Visible = True
  btnGuardarArticulo.Visible = False
  btnGuardarCambiosArticulo.Visible = False
  btnBorrarArticulo.Visible = False

End

Public Sub gbMetadatosArticulos_Data(Row As Integer, Column As Integer)

  If (ContenidoArt <> Null) Then
    If Row >= 0 And Row < ContenidoArt.Count Then
      ContenidoArt.MoveTo(Row)
      Try gbMetadatosArticulos.Data.Text = Str(ContenidoArt[Column])
    Endif
  Endif

End

Public Sub gbMetadatosArticulos_Click()

  ' CONFIGURAR COMO SE MUESTRAN LOS BOTONES
  btnNuevoArticulo.Visible = True
  btnGuardarArticulo.Visible = False
  btnGuardarCambiosArticulo.Visible = True
  btnBorrarArticulo.Visible = True
  ' EN EL EVENTO CLICK DE LA CELDA PASAR COMO PARÁMETRO LA FILA
  m_Articulos.MostrarArticulosEnTableViewArticulos(gbMetadatosArticulos.row)

End

Public Sub ComboBoxArticuloAsociado_Click()

  m_FuncionesGenericas.ComboBoxArticuloAsociado()

End

Public Sub BtnNuevoBib_Click()

  ' VERIFICAR SI HAY UN PROYECTO ACTIVO
  If Trim(txtProyecto.Text) = "" Then
    m_Sonido.sonar("Info")
    Message.Info("No hay un proyecto en curso. Por favor, abra o cree un proyecto antes de continuar.")
    Return
  Else
    ' LIMPIAMOS LOS TEXTBOX AL INICIAR UNA NUEVA CARGA
    m_Bibtex.LimpiarTextboxBibtex()

    ' SOLO SI HAY PROYECTO SE EJECUTA LO SIGUIENTE
    Dim Idn As Integer
    Dim MaxId As Variant

    MaxId = m_ConexionBD.mConn.Exec("SELECT MAX(id) FROM bibtex LIMIT 1")

    If MaxId["MAX(id)"] = Null Then
      Idn = 0
    Else
      Idn = CInt(MaxId["MAX(id)"]) + 1
    Endif

    id_bibtex.Value = Idn
    '
    TabPanel5.Index = 1
    txtTMPbiblio.Enabled = True
    txtTMPbiblio.Font = Font["Monospace"]
    txtTMPbiblio.Background = Color.White
    txtTMPbiblio.Clear
    txtTMPbiblio.SetFocus

    BtnNuevoBib.Visible = False
    btnGuardarBib.Visible = True
    btnGuardarCambiosBib.Visible = False
    BtnEliminarBib.Visible = False
    TabPanelBibliografia.Index = 0

  Endif

End

Public Sub btnGuardarBib_Click()

  m_Bibtex.GuardarBibtex()

End

Public Sub btnGenerarClaveBib_Click()

  m_Bibtex.AgregarClaveBib()

End

Public Sub btnVerificarURL_Click()

  m_FuncionesGenericas.VerificarURLDesdeCampo(txtUrl)

End

Public Sub btnVerificarDOI_Click()

  m_FuncionesGenericas.VerificarDOI(txtDoi)

End

Public Sub btnChequearISBN_Click()

  Dim ISBN As String = txtISBN.Text

  If m_FuncionesGenericas.EsISBNValido(ISBN) Then
    m_Sonido.sonar("Info")
    Message.Info("El ISBN es válido.")
  Else
    m_Sonido.sonar("Warning")
    Message.Warning("El ISBN no es válido.")
  End If

End

Public Sub btnChequearISSN_Click()

  Dim sISSN As String
  Dim sURL As String

  ' OBTENER EL TEXTO DEL TEXTBOX
  sISSN = txtIssn.Text

  ' VERIFICAR QUE NO ESTÉ VACÍO
  If Trim(sISSN) = "" Then
    m_Sonido.sonar("Error")
    Message.Error("Por favor, introduce un ISSN para buscar")
    Return
  Endif

  ' VALIDAR FORMATO ISSN Y DÍGITO DE CONTROL
  If Not m_FuncionesGenericas.IsValidISSN(sISSN) Then
    m_Sonido.sonar("Error")
    Message.Error("El ISSN no es válido. Debe tener el formato XXXX-XXXX o XXXXXXXX y pasar la validación del dígito de control.")
    Return
  Endif

  ' CONSTRUIR LA URL DE BÚSQUEDA PARA EL PORTAL ISSN
  sURL = "https://portal.issn.org/resource/ISSN/" & Replace(sISSN, "-", "")

  ' ABRIR LA URL EN EL NAVEGADOR PREDETERMINADO
  Desktop.Open(sURL)

End

Public Sub btnBuscarGoogle_Click()

  Dim sURL As String = Trim(txtUrl.Text)

  ' VERIFICAR QUE NO ESTÉ VACÍO
  If Trim(sURL) = "" Then
    m_Sonido.sonar("Error")
    Message.Error("Por favor, introduce un URL.")
    Return
  Endif

  ' ABRIR LA URL EN EL NAVEGADOR PREDETERMINADO
  Desktop.Open(sURL)

End

' INICIALIZAR EL MAPEO DE CAMPOS
Public Sub _new()

  m_Bibtex.InicializarMapaCampos()

End

Public Sub BtnBuscarAuthor_Click()

  m_Bibtex.BuscarPorCampo(txtAuthor, gvReferenciasEnCurso, "author")

End

Public Sub BtnBuscarBookAuthor_Click()

  m_Bibtex.BuscarPorCampo(txtBookAuthor, gvReferenciasEnCurso, "book_author")

End

Public Sub BtnBuscarEditor_Click()

  m_Bibtex.BuscarPorCampo(txtEditor, gvReferenciasEnCurso, "editor")

End

Public Sub BtnBuscarTituloReferencia_Click()

  m_Bibtex.BuscarPorCampo(txtTitle, gvReferenciasEnCurso, "title")

End

Public Sub BtnBuscarBookTitle_Click()

  m_Bibtex.BuscarPorCampo(txtBookTitle, gvReferenciasEnCurso, "book_title")

End

Public Sub BtnBuscarMainTitle_Click()

  m_Bibtex.BuscarPorCampo(txtMainTitle, gvReferenciasEnCurso, "main_title")

End

' OCULTAR EL COMBOBOX CUANDO SE HACE CLIC EN EL FORMULARIO
Public Sub Form_Click()

  ComboBoxPublisher.Visible = False
  ComboBoxInstitution.Visible = False
  ComboBoxOrganization.Visible = False
  ComboBoxJournal.Visible = False

End

Public Sub BtnBuscarClaveBib_Click()

  m_Bibtex.BuscarPorCampo(txtClaveBibtex, gvReferenciasEnCurso, "clave_bibtex")

End

Public Sub btnEmpujarGitLocal_Click()

  Dim fecha As String
  Dim rutaGit As String

  ' OBTENER LA FECHA ACTUAL
  fecha = Format(Date(), "dd-mm-yyyy")
  rutaGit = File.Dir(txtProyecto.Text) & "/.git"

  ' VERIFICAR SI EL DIRECTORIO EXISTE
  If Not Exist(rutaGit) Then
    TerminalViewProyecto.Input("git init --initial-branch=main" & "\n")
    TerminalViewProyecto.Input("git add ." & "\n")
    TerminalViewProyecto.Input("git commit -m " & fecha & "\n")
    TerminalViewProyecto.SetFocus()
  Else
    TerminalViewProyecto.Input("git add ." & "\n")
    TerminalViewProyecto.Input("git commit -m " & fecha & "\n")
    TerminalViewProyecto.SetFocus()
  Endif

End

Public Sub btnEmpujarGitHubRemoto_Click()

  Dim fecha As String
  Dim usuario As String = Trim(txtUsuarioGit.Text)
  Dim IdGitHub As String
  Dim resultado As String
  Dim repoLocal As Boolean = False
  Dim repoConfigurado As Boolean = False
  Dim ramaMain As Boolean = False

  ' MOSTRAMOS LA CONSOLA
  FMain.TabPanelIzquierdaInferior.Index = 0

  ' OBTENER LA FECHA ACTUAL
  fecha = Format(Date(), "dd-mm-yyyy")

  ' VERIFICAR SI EL NOMBRE DE USUARIO ESTÁ VACÍO
  If usuario = "" Then
    m_Sonido.sonar("Error")
    Message.Error("Debe indicar su nombre de usuario en GitHub", "Ok")
    Return
  End If

  IdGitHub = "git@github.com:" & usuario & "/" & NombreProyecto & ".git"

  ' VERIFICAR SI EL DIRECTORIO ACTUAL ES UN REPOSITORIO GIT
  resultado = Shell$(NombreProyecto & "/git rev-parse --is-inside-work-tree 2>/dev/null")
  If Trim(resultado) = "true" Then
    repoLocal = True
  Endif

  ' SI NO ES UN REPOSITORIO, INICIALIZARLO
  If Not repoLocal Then
    TerminalViewProyecto.Input("git init" & "\n")
  Endif

  ' VERIFICAR SI EL REPOSITORIO YA TIENE UN ORIGIN CONFIGURADO
  resultado = Shell$("git remote -v")
  If InStr(resultado, IdGitHub) > 0 Then
    repoConfigurado = True
  Endif

  ' VERIFICAR SI LA RAMA ACTUAL ES MAIN
  resultado = Shell$("git branch --show-current")
  If Trim(resultado) = "main" Then
    ramaMain = True
  Endif

  ' SI EL REPO NO ESTÁ CONFIGURADO, AGREGAR ORIGIN
  If Not repoConfigurado Then
    TerminalViewProyecto.Input("git remote add origin " & IdGitHub & "\n")
  Endif

  ' SI LA RAMA NO ES MAIN, CAMBIAR A MAIN
  If Not ramaMain Then
    TerminalViewProyecto.Input("git branch -M main" & "\n")
  Endif

  ' EJECUTAR LOS COMANDOS PRINCIPALES
  TerminalViewProyecto.Input("git add ." & "\n")
  TerminalViewProyecto.Input("git commit -m '" & fecha & "'" & "\n")
  TerminalViewProyecto.Input("git push --set-upstream origin main" & "\n")
  TerminalViewProyecto.SetFocus()

End

Public Sub refrecarViewFile()

  ' NOS REASEGURAMOS DE LA RUTA EN LA TERMINAL
  TerminalViewProyecto.Input("cd " & RutaProyecto & "\n")
  TerminalViewProyecto.Input("clear" & "\n")

  DirViewProyecto.Root = RutaProyecto
  DirViewProyecto.Reload
  FileViewProyecto.Reload
  gvReferenciasEnCurso.Refresh()
  gvSiglasEnCurso.Refresh()
  gbMetadatosArticulos.Refresh()
  ' gbMetadatosAutores.Refresh()

End

Public Sub btnRefrescarGrid_Click()

  refrecarViewFile()

End

' OCULTAR EL COMBOBOX CUANDO EL TEXTBOX PIERDE EL FOCO
Public Sub txtPublisher_LostFocus()

  ' USAR UN TIMER PARA DAR TIEMPO A QUE SE PROCESE EL CLICK DEL COMBOBOX
  TimerPublisher.Delay = 100
  TimerPublisher.Start()

End

' OCULTAR EL COMBOBOX CUANDO EL TEXTBOX PIERDE EL FOCO
Public Sub txtInstitution_LostFocus()

  ' USAR UN TIMER PARA DAR TIEMPO A QUE SE PROCESE EL CLICK DEL COMBOBOX
  TimerInstitution.Delay = 100
  TimerInstitution.Start()

End

Public Sub txtOrganization_LostFocus()

  TimerOrganization.Delay = 100
  TimerOrganization.Start()

End

' MÉTODO ADICIONAL PARA MANEJAR CUANDO EL USUARIO HACE CLIC FUERA DEL COMBO
Public Sub txtPublisher_GotFocus()

  ' OPCIONAL: MOSTRAR TODOS LOS RESULTADOS CUANDO EL TEXTBOX OBTIENE EL FOCO
  If Trim(txtPublisher.Text) <> "" Then
    m_FuncionesGenericas.BuscarEditorialDesdeTexto()
  Endif

End

Public Sub txtInstitution_GotFocus()

  If Trim(txtInstitution.Text) <> "" Then
    m_FuncionesGenericas.BuscarInstitucionDesdeTexto()
  Endif

End

Public Sub txtOrganization_GotFocus()

  If Trim(txtOrganization.Text) <> "" Then
    m_FuncionesGenericas.BuscarInstitucionDesdeTexto()
  Endif

End

Public Sub txtJournalTitle_GotFocus()

  If Trim(txtJournalTitle.Text) <> "" Then
    m_FuncionesGenericas.BuscarJournalDesdeTexto()
  Endif

End

Public Sub btnBuscarTituloEnGoogle_Click()

  Dim sTITULO As String = Trim(txtTitle.Text)

  ' VERIFICAR QUE NO ESTÉ VACÍO
  If sTITULO = "" Then
    m_Sonido.sonar("Error")
    Message.Error("Por favor, introduce un título a buscar.")
    Return
  Endif

  ' ABRIR LA BÚSQUEDA EN EL NAVEGADOR PREDETERMINADO
  Desktop.Open("https://www.google.com/search?q=" & sTITULO)

End

Public Sub btnChequearISSNRevista_Click()

  Dim sISSN As String
  Dim sURL As String

  ' OBTENER EL TEXTO DEL TEXTBOX
  sISSN = txtRevistaISSN.Text

  ' VERIFICAR QUE NO ESTÉ VACÍO
  If Trim(sISSN) = "" Then
    m_Sonido.sonar("Error")
    Message.Error("Por favor, introduce un ISSN para buscar")
    Return
  Endif

  ' VALIDAR FORMATO ISSN Y DÍGITO DE CONTROL
  If Not m_FuncionesGenericas.IsValidISSN(sISSN) Then
    m_Sonido.sonar("Error")
    Message.Error("El ISSN no es válido. Debe tener el formato XXXX-XXXX o XXXXXXXX y pasar la validación del dígito de control.")
    Return
  Endif

  ' CONSTRUIR LA URL DE BÚSQUEDA PARA EL PORTAL ISSN
  sURL = "https://portal.issn.org/resource/ISSN/" & Replace(sISSN, "-", "")

  ' ABRIR LA URL EN EL NAVEGADOR PREDETERMINADO
  Desktop.Open(sURL)

End

Public Sub btnVerificarURLetica_Click()

  m_FuncionesGenericas.VerificarURLDesdeCampo(txtRevistaPoliticaEticaURL)

End

Public Sub btnVerificarURLrevisores_Click()

  m_FuncionesGenericas.VerificarURLDesdeCampo(txtPoliticaRevisoresURL)

End

Public Sub btnVerificarURLopen_Click()

  m_FuncionesGenericas.VerificarURLDesdeCampo(txtPoliticaOpenAccessURL)

End

Public Sub btnVerificarDOIarticulo_Click()

  m_FuncionesGenericas.VerificarDOI(txtArticuloDoi)

End

Public Sub btnVerificarURLrevista_Click()

  m_FuncionesGenericas.VerificarURLDesdeCampo(txtRevistaURL)

End

Public Sub btnChequearAbreviatura_Click()

  Dim sNLM As String
  Dim sURL As String

  ' OBTENER EL TEXTO DEL TEXTBOX
  sNLM = Trim(txtRevistaTitulo.Text)

  ' VERIFICAR QUE NO ESTÉ VACÍO
  If sNLM = "" Then
    m_Sonido.sonar("Error")
    Message.Error("Por favor, introduce el nombre de la revista para buscar.")
    Return
  Endif

  ' REEMPLAZAR ESPACIOS POR "+" PARA LA URL
  sNLM = Replace(sNLM, " ", "+")

  ' CONSTRUIR LA URL CON LA BÚSQUEDA INCLUIDA
  sURL = "https://www.ncbi.nlm.nih.gov/nlmcatalog?term=" & sNLM

  Desktop.Open(sURL)

End

Public Sub ComboBoxArticuloAsociadoEnbibtex_Click()

  m_FuncionesGenericas.ComboBoxArticuloAsociadoEnbibtex()

End

' Public Sub btnGuardarAutor_Click()
'
'   If id_articulo_autor.Text = "" Then
'     m_Sonido.sonar("Info")
'     Message.Info("Debe asociar un artículo.")
'     Return
'   Endif
'
'   m_Autores.GuardarAutoresPrimeraVez()
'   m_Autores.RefrescarTableViewAutores()
'   m_Autores.LimpiarTextboxAutores
'
'   ' configurar como se muestran los botones
'   btnAgregarAutorListado.Visible = True
'   btnGuardarAutor.Visible = False
'   btnGuardarCambiosAutor.Visible = False
'   btnBorrarAutor.Visible = False
'
' End

' Public Sub btnGuardarCambiosAutor_Click()
'
'   m_Autores.GuardarCambiosAutores()
'   m_Autores.ObtenerIdNuevoAutor()
'   m_Autores.RefrescarTableViewAutores()
'   m_Autores.LimpiarTextboxAutores
'
'   ' configurar como se muestran los botones
'   btnAgregarAutorListado.Visible = True
'   btnGuardarAutor.Visible = False
'   btnGuardarCambiosAutor.Visible = False
'   btnBorrarAutor.Visible = False
'
' End

' Public Sub gbMetadatosAutores_Click()
'
'   ' configurar como se muestran los botones
'   btnAgregarAutorListado.Visible = True
'   btnGuardarAutor.Visible = False
'   btnGuardarCambiosAutor.Visible = True
'   btnBorrarAutor.Visible = True
'
'   ' Mostrar los datos en los TextBox
'   m_Autores.MostrarMetadatosEnTableViewAutores(gbMetadatosAutores.Row)
'
'   ' Sincronizar el ComboBox con el ID del artículo
'   m_FuncionesGenericas.ActualizarComboBoxArticuloAsociadoDesdeId()
'
' End

Public Sub btnGuardarCambiosEditor_Click()

  If sRutaArchivoAbierto = "" Or Not Exist(sRutaArchivoAbierto) Then
    m_Sonido.sonar("Warning")
    Message.Warning("No hay un archivo válido cargado para guardar.")
    Return
  Endif

  ' INTENTAR GUARDAR
  Try File.Save(sRutaArchivoAbierto, txtEditorProyecto.Text)

  If Error Then
    m_Sonido.sonar("Error")
    Message.Error("Error al guardar el archivo: " & Error.Text)
    Error.Clear
    Return
  Endif

  ' ACTUALIZAR EL CONTENIDO ORIGINAL DESPUÉS DE GUARDAR
  sContenidoOriginal = txtEditorProyecto.Text

  ' ACTUALIZAR EL LISTADO DE MARCAS
  m_GestorComentarios.ActualizarGridComentarios(gvComentarios, txtEditorProyecto.Text)

  m_Sonido.sonar("Info")
  Message.Info("Archivo guardado correctamente.")

  ' ACTUALIZAMOS EL CONTEO DE LINEAS
  ActualizarContadorLineas()

End

Public Sub menuSalir_Click()

  m_InicioCierre.CerrarTodoMySQL()

End

' Private Function URLEncode(sTexto As String) As String
'
'   Dim sResultado As String = ""
'   Dim i As Integer
'   Dim c As String
'   Dim code As Integer
'
'   For i = 1 To Len(sTexto)
'     c = Mid(sTexto, i, 1)
'     code = Asc(c)
'
'     Select Case c
'       Case "A" To "Z", "a" To "z", "0" To "9", "-", "_", ".", "~"
'         sResultado &= c
'       Case " "
'         sResultado &= "+"  ' o usar %20 si querés más precisión
'       Case Else
'         sResultado &= "%" & Hex(code, 2)
'     End Select
'   Next
'
'   Return sResultado
'
' End Function
'
' Public Sub ButtonBuscarOrcidAutor_Click()
'
'   Dim sNombre As String
'   Dim sURL As String
'
'   sNombre = txtAutorOrcid.Text
'
'   If Trim(sNombre) = "" Then
'     m_Sonido.sonar("Error")
'     Message.Error("Por favor, introduce un nombre para buscar el ORCID")
'     Return
'   Endif
'
'   sURL = "https://orcid.org/orcid-search/search?searchQuery=" & URLEncode(sNombre)
'
'   Desktop.Open(sURL)
'
' End

Public Sub menuAbrirRevista_Click()

  TipoProyectoActual = m_Constantes.TIPO_REVISTA_MD
  AbrirProyecto(m_Constantes.TIPO_REVISTA_MD)

End

Public Sub menuAbrirLibroMD_Click()

  TipoProyectoActual = m_Constantes.TIPO_LIBRO_MD
  AbrirProyecto(m_Constantes.TIPO_LIBRO_MD)

End

Public Sub menuAbrirLibroLTX_Click()

  TipoProyectoActual = m_Constantes.TIPO_LIBRO_LATEX
  AbrirProyecto(m_Constantes.TIPO_LIBRO_LATEX)

End

' ' COMPILAR EPUB LIBRO
' Public Sub menuEPUBlibro_Click()
'
'   ' CONFIRMAMOS ESTAR EN EL DIRECTORIO DE TRABAJO DEL PROYECTO
'   TerminalViewProyecto.Input("cd " & RutaProyecto & "\n")
'   TerminalViewProyecto.Input("clear" & "\n")
'
'   ' VERIFICAR QUE TXTPROYECTO.TEXT NO ESTÉ VACÍO
'   If Trim(txtProyecto.Text) = "" Then
'     m_Sonido.sonar("Warning")
'     Message.Warning("No se ha especificado un archivo de proyecto")
'     Return
'   Else
'     Dim sExtension As String
'     ' OBTENER LA EXTENSIÓN DEL ARCHIVO
'     sExtension = LCase(File.Ext(txtProyecto.Text))
'     ' EJECUTAR LA FUNCIÓN CORRESPONDIENTE SEGÚN LA EXTENSIÓN
'     Select Case sExtension
'       Case "tex"
'         m_FuncionesExportar.GenerarEPUBlibroTEX()
'       Case "md"
'         m_GenerarSalidas.GenerarEPUBlibroMD()
'       Default
'         m_Sonido.sonar("Error")
'         Message.Error("Extensión no soportada: " & sExtension & Chr(10) & "Solo se admiten archivos .tex y .md")
'     End Select
'
'   Endif
'
' End

' Public Sub menuPDFlibro_Click()
'
'   ' CONFIRMAMOS ESTAR EN EL DIRECTORIO DE TRABAJO DEL PROYECTO
'   TerminalViewProyecto.Input("cd " & RutaProyecto & "\n")
'   TerminalViewProyecto.Input("clear" & "\n")
'
'   ' VERIFICAR QUE TXTPROYECTO.TEXT NO ESTÉ VACÍO
'   If Trim(txtProyecto.Text) = "" Then
'     m_Sonido.sonar("Warning")
'     Message.Warning("No se ha especificado un archivo de proyecto")
'     Return
'   Else
'     Dim sExtension As String
'     ' OBTENER LA EXTENSIÓN DEL ARCHIVO
'     sExtension = LCase(File.Ext(txtProyecto.Text))
'     ' EJECUTAR LA FUNCIÓN CORRESPONDIENTE SEGÚN LA EXTENSIÓN
'     Select Case sExtension
'       Case "tex"
'         m_FuncionesExportar.GenerarPDFlibroTEX()
'       Case "md"
'         m_GenerarSalidas.GenerarPDFlibroMD()
'       Default
'         m_Sonido.sonar("Error")
'         Message.Error("Extensión no soportada: " & sExtension & Chr(10) & "Solo se admiten archivos .tex y .md")
'     End Select
'
'   Endif
'
' End

Public Sub AbrirProyecto(nTipoProyecto As Integer)

  Select Case nTipoProyecto
    Case m_Constantes.TIPO_REVISTA_MD
      Dialog.Title = "Abrir proyecto en Markdown"
      Dialog.Filter = ["r-*.md", "Markdown"]
    Case m_Constantes.TIPO_LIBRO_MD
      Dialog.Title = "Abrir libro (Markdown)"
      Dialog.Filter = ["l-*.md", "Libro en Markdown"]
    Case m_Constantes.TIPO_LIBRO_LATEX
      Dialog.Title = "Abrir proyecto en LaTeX"
      Dialog.Filter = ["l-*.tex", "LaTeX"]
  End Select

  m_Sonido.sonar("OpenApp")

  ' IMPORTANTE: NO CAMBIAR EXTENSIONES AUTOMÁTICAMENTE
  Dialog.AutoExt = False
  Dialog.Path = User.Home

  If Dialog.OpenFile() Then Return

  ' GUARDAMOS EL TIPO DE PROYECTO PARA USAR EN OTRAS CONDICIONES
  TipoProyectoActual = nTipoProyecto

  ' LLAMAMOS A LA FUNCIÓN CORRESPONDIENTE SEGÚN EL TIPO
  Select Case nTipoProyecto
    Case m_Constantes.TIPO_REVISTA_MD
      AbrirRevistaMD()
    Case m_Constantes.TIPO_LIBRO_MD
      AbrirLibroMD()
    Case m_Constantes.TIPO_LIBRO_LATEX
      AbrirLibroTEX()
  End Select

End

Private Function ObtenerDescripcionTipo(iTipo As Integer) As String

  Select Case iTipo
    Case m_Constantes.TIPO_REVISTA_MD
      Return "Revista MD"
    Case m_Constantes.TIPO_LIBRO_MD
      Return "Libro MD"
    Case m_Constantes.TIPO_LIBRO_LATEX
      Return "Libro LaTeX"
    Default
      Return "Tipo desconocido (" & iTipo & ")"
  End Select

End

Private Sub InicializarTablasProyecto(idProyecto As Integer)

  Dim resultado As Result
  Dim nombreProyecto As String

  ' OBTENER EL NOMBRE DEL PROYECTO
  resultado = m_ConexionBD.mConn.Exec("SELECT nombre FROM proyectos WHERE id = &1", idProyecto)
  If resultado.Available Then
    nombreProyecto = resultado["nombre"]
  Endif

  ' METADATOS
  resultado = m_ConexionBD.mConn.Exec("SELECT id FROM metadatos WHERE id_proyecto = &1", idProyecto)
  If Not resultado.Available Then
    ' GUARDAR DIRECTAMENTE EL NOMBRE DEL PROYECTO
    m_ConexionBD.mConn.Exec("INSERT INTO metadatos (id_proyecto) VALUES (&1)", idProyecto)
  Endif

  ' ORDEN_TALLER
  resultado = m_ConexionBD.mConn.Exec("SELECT id FROM orden_taller WHERE id_proyecto = &1", idProyecto)
  If Not resultado.Available Then
    ' GUARDAR DIRECTAMENTE EL NOMBRE DEL PROYECTO
    m_ConexionBD.mConn.Exec("INSERT INTO orden_taller (id_proyecto, origen_ot) VALUES (&1, &2)", idProyecto, nombreProyecto)
  Endif

  m_ConexionBD.mConn.Commit

End

Public Sub AbrirRevistaMD()

  Dim sRutaSeleccionada As String = Dialog.Path

  If sRutaSeleccionada = "" Then
    Return
  Endif

  ' ASIGNAMOS EL NOMBRE Y RUTA DEL PROYECTO A LAS VARIABLES GLOBALES
  NombreProyecto = File.Name(sRutaSeleccionada)
  RutaProyecto = File.Dir(sRutaSeleccionada)

  Dim sExtension As String = LCase(File.Ext(sRutaSeleccionada))
  Dim sNombreArchivo As String = LCase(File.Name(sRutaSeleccionada))

  ' DETERMINAR TIPO SEGÚN NORMAS: r- (revista .md), l- (libro .md/.tex)
  Dim iTipoSeleccionado As Integer = -1

  Select Case True
    Case Left(sNombreArchivo, 2) = "r-" And sExtension = "md"
      iTipoSeleccionado = m_Constantes.TIPO_REVISTA_MD

    Case Left(sNombreArchivo, 2) = "l-" And sExtension = "md"
      iTipoSeleccionado = m_Constantes.TIPO_LIBRO_MD

    Case Left(sNombreArchivo, 2) = "l-" And sExtension = "tex"
      iTipoSeleccionado = m_Constantes.TIPO_LIBRO_TEX

    Case Else
      m_Sonido.sonar("Error")
      Message.Error("El archivo seleccionado no cumple las normas de nomenclatura." & gb.NewLine &
        "Formatos válidos:" & gb.NewLine &
        "• Revista Markdown: r-nombre.md" & gb.NewLine &
        "• Libro Markdown: l-nombre.md" & gb.NewLine &
        "• Libro LaTeX: l-nombre.tex" & gb.NewLine &
        "Archivo recibido: " & sNombreArchivo)
      Return
  End Select

  ' BUSCAR EN LA BBDD SI EL PROYECTO QUE VAMOS A ABRIR EXISTE
  Dim sSQL As String = "SELECT * FROM proyectos WHERE nombre = &1"
  Dim Contenido As Result = m_ConexionBD.mConn.Exec(sSQL, NombreProyecto)
  Dim nuevoID As Integer

  ' LIBERAR PROYECTO QUE ESTA ABIERTO
  If NombreProyecto <> "" Then
    Dim sProyectoEnCurso As String = File.Name(txtProyecto.Text)
    m_ConexionBD.mConn.Exec("UPDATE proyectos SET ocupado = 0 WHERE nombre = &1", sProyectoEnCurso)
    m_ConexionBD.mConn.Exec("UPDATE proyectos SET usuario_propietario = NULL WHERE nombre = &1", sProyectoEnCurso)
    m_ConexionBD.mConn.Commit
    m_Timer.DetenerContador()
  Endif

  If Contenido.Available Then
    ' VALIDACIONES SOBRE EL REGISTRO EXISTENTE
    Dim iTipoBD As Integer = Contenido["tipo_producto"]

    ' 1) COINCIDENCIA DE TIPO
    If iTipoBD <> iTipoSeleccionado Then
      m_Sonido.sonar("Error")
      Message.Error("El tipo de archivo no coincide con el tipo registrado en la base de datos." & gb.NewLine &
        "Se esperaba: " & ObtenerDescripcionTipo(iTipoSeleccionado) & gb.NewLine &
        "Se encontró: " & ObtenerDescripcionTipo(iTipoBD))
      Return
    Endif

    ' 2) ACTIVO? (0 ES FALSE Y 1 ES TRUE)
    If Not Contenido["activo"] Then
      m_Sonido.sonar("Warning")
      Message.Warning("El proyecto '" & NombreProyecto & "' está archivado." & gb.NewLine &
        "Debe solicitar al editor que lo reactive para poder abrirlo.")
      Return
    Endif

    ' 3) NO OCUPADO?
    If Contenido["ocupado"] Then
      m_Sonido.sonar("Warning")
      Message.Warning("El proyecto '" & NombreProyecto & "' está ocupado en otra máquina." & gb.NewLine &
        "Lo puede abrir en modo parcial o espere a que se libere.")
      Return
    Endif

    nuevoID = Contenido["id"]
    m_Sonido.sonar("Info")
    Message.Info("Proyecto encontrado en la base de datos y listo para abrir.")
  Else
    ' NO EXISTE: CREAR REGISTRO NUEVO (MySQL)
    m_ConexionBD.mConn.Exec("INSERT INTO proyectos (nombre, tipo_producto, usuario_propietario, activo, ocupado) VALUES (&1, &2, &3 1, 0)",
      NombreProyecto, iTipoSeleccionado, m_InicioCierre.UsuarioEnCurso)

    ' OBTENER LAST_INSERT_ID() EN MYSQL
    Dim rLast As Result = m_ConexionBD.mConn.Exec("SELECT LAST_INSERT_ID()")
    If rLast.Available Then
      nuevoID = rLast[0]
    Else
      ' FALLBACK: SI NO PODEMOS RECUPERAR EL ID, AVISAR Y ABORTAR
      m_ConexionBD.mConn.Rollback
      m_Sonido.sonar("Error")
      Message.Error("No se pudo obtener el ID del proyecto insertado. Abortando apertura.")
      Return
    Endif

    m_ConexionBD.mConn.Commit
    m_Sonido.sonar("Info")
    Message.Info("Proyecto nuevo registrado en la base de datos.")
  Endif

  ' INICIALIZAR TODAS LAS TABLAS RELACIONADAS CON EL PROYECTO
  InicializarTablasProyecto(nuevoID)

  ' MARCAR NUEVO PROYECTO COMO OCUPADO
  m_ConexionBD.mConn.Exec("UPDATE proyectos SET ocupado = 1 WHERE id = &1", nuevoID)
  m_ConexionBD.mConn.Commit

  ' MARCAR PROYECTO CON PROPIETARIO
  m_ConexionBD.mConn.Exec("UPDATE proyectos SET usuario_propietario = &1 WHERE id = &2", m_InicioCierre.UsuarioEnCurso, nuevoID)
  m_ConexionBD.mConn.Commit

  ' ACTUALIZAR RUTA DEL PROYECTO
  txtProyecto.Text = sRutaSeleccionada

  ' MOSTRAR / OCULTAR BARRA DE MENUES
  HBoxGuardarTextArea.Visible = True

  ' LIMPIAMOS LOS FORMULARIOS
  m_Metadatos.LimpiarCamposMetadatos()
  m_Articulos.LimpiarTextboxArticulos()
  m_Autores.LimpiarTextboxAutores()
  m_Bibtex.LimpiarTextboxBibtex()
  m_Siglas.LimpiarTextboxSigla()

  ' LIMPIAR CACHÉ O ENTORNO PREVIO
  Shell Quote$("rm -rf " & User.Home & "/.local/share/org.gambas.*") & "\n" Wait

  ' CONTROLOMANOS EXISTAN LOS ARCHIVOS BASE PARA UN PROYECTO
  m_FuncionesGenericas.CopiarArchivosBase_MD()

  ' CONFIGURAR INTERFAZ PARA REVISTA MD
  m_FuncionesGenericas.ConfigurarFormulariosRevistaMD()

  ' MOSTRAR EL ID DEL PROYECTO
  id_proyecto.Value = nuevoID

  ' CARGAMOS LOS COMBOXBOX CON LOS ESTILOS CSL PARA BIBTEX
  CargarCSLBibTeX()

  ' CARGAMOS LOS SCRIPTS PARA MANIPULAR CON LOS *.md
  m_InicioCierre.CargarScripts()

  ' CARGAMOS LOS METADATOS DEL PROYECTO
  m_Metadatos.CargarMetadatosDelProyecto()

  ' CARGAMOS LAS REFERENCIAS BIBTEX
  m_Bibtex.IniciarBibtex()

  ' CARGAMOS LAS SIGLAS
  m_Siglas.IniciarSiglas()

  ' ARRANCAMOS EL CONTADOR
  m_Timer.IniciarContadorReloj(id_proyecto.Value)

  ' MARCAR QUE SE ABRIÓ EN MODO completo
  FMain.ValueBoxParcial.Value = 0

  ' CARGAR LISTA DE ARTÍCULOS EN EL COMBOBOX
  CargarListaArticulosMD()

  ' CARGAMOS EL ARCHIVO DEL PROYECTO EN EL EDITOR
  txtEditorProyecto.Text = File.Load(sRutaSeleccionada)
  txtEditorProyecto.ReadOnly = False
  txtEditorProyecto.Enabled = True

  ' GUARDAR LA RUTA DEL ARCHIVO ABIERTO Y EL CONTENIDO ORIGINAL
  sRutaArchivoAbierto = sRutaSeleccionada
  sContenidoOriginal = txtEditorProyecto.Text
  vbPanelIzquierdo.Visible = True

  ' LLENAR EL GRID CON LAS MARCAS EXISTENTES
  m_GestorComentarios.ActualizarGridComentarios(gvComentarios, txtEditorProyecto.Text)

  ' OCULTAMOS EL PANEL INFERIOR IZQUIERDO
  TabPanelIzquierdaInferior.Visible = False
  HBoxBuscar.Visible = False

  ' ACTUALIZAMOS EL CONTEO DE LINEAS
  ActualizarContadorLineas()

End

Public Sub CargarListaArticulosMD()

  Dim sRutaArticulos As String
  Dim aArchivos As String[]
  Dim sArchivo As String

  ' Limpiar el ComboBox
  cmbArticulosRevista.Clear()

  ' Agregar primero el archivo principal del proyecto
  cmbArticulosRevista.Add(NombreProyecto)

  ' Construir ruta a la carpeta artículos
  sRutaArticulos = RutaProyecto &/ "articulos"

  ' Listar archivos .md en la carpeta artículos
  aArchivos = Dir(sRutaArticulos, "*.md")

  ' Ordenar alfabéticamente
  aArchivos.Sort()

  ' Agregar cada archivo al ComboBox
  For Each sArchivo In aArchivos
    cmbArticulosRevista.Add(sArchivo)
  Next

  ' Seleccionar el archivo principal por defecto
  cmbArticulosRevista.Index = 0

End

Public Sub cmbArticulosRevista_Click()

  Dim sArchivoSeleccionado As String
  Dim sRutaCompleta As String
  Dim sContenido As String
  Dim iRespuesta As Integer

  ' Verificar que hay algo seleccionado
  If cmbArticulosRevista.Index = -1 Then Return

  ' Obtener el archivo seleccionado
  sArchivoSeleccionado = cmbArticulosRevista.Text

  If sArchivoSeleccionado = "" Then Return

  ' VERIFICAR SI HAY CAMBIOS SIN GUARDAR
  If sRutaArchivoAbierto <> "" Then
    If txtEditorProyecto.Text <> sContenidoOriginal Then
      m_Sonido.sonar("Warning")
      iRespuesta = Message.Question("Hay cambios sin guardar en el archivo actual." & gb.NewLine &
        "¿Desea guardar los cambios antes de continuar?",
        "Sí", "No", "Cancelar")

      Select Case iRespuesta
        Case 1  ' Sí - Guardar
          btnGuardarCambiosEditor_Click()

        Case 2  ' No - Continuar sin guardar
          ' No hacer nada, continuar

        Case 3, 0  ' Cancelar o cerrar diálogo
          Return
      End Select
    Endif
  Endif

  ' Determinar la ruta completa según la selección
  If cmbArticulosRevista.Index = 0 Then
    ' Es el archivo principal del proyecto
    sRutaCompleta = txtProyecto.Text
  Else
    ' Es un artículo de la carpeta articulos
    sRutaCompleta = RutaProyecto &/ "articulos" &/ sArchivoSeleccionado
  Endif

  ' Verificar que el archivo existe
  If Not Exist(sRutaCompleta) Then
    m_Sonido.sonar("Error")
    Message.Error("El archivo no existe:" & gb.NewLine & sRutaCompleta)
    Return
  Endif

  ' Cargar el contenido en el editor
  sContenido = File.Load(sRutaCompleta)

  If sContenido Then
    txtEditorProyecto.Text = sContenido
    ' ACTUALIZAR LA RUTA DEL ARCHIVO ABIERTO Y EL CONTENIDO ORIGINAL
    sRutaArchivoAbierto = sRutaCompleta
    sContenidoOriginal = sContenido
  Else
    m_Sonido.sonar("Error")
    Message.Error("No se pudo cargar el contenido del archivo")
  Endif

  ' LLENAR EL GRID CON LAS MARCAS EXISTENTES
  m_GestorComentarios.ActualizarGridComentarios(gvComentarios, txtEditorProyecto.Text)

  ' ACTUALIZAMOS EL CONTEO DE LINEAS
  ActualizarContadorLineas()

End

Public Sub AbrirLibroMD()

End

Public Sub AbrirLibroTEX()

  Dim sRutaSeleccionada As String = Dialog.Path

  If sRutaSeleccionada = "" Then
    Return
  Endif

  ' ASIGNAMOS EL NOMBRE Y RUTA DEL PROYECTO A LAS VARIABLES GLOBALES
  NombreProyecto = File.Name(sRutaSeleccionada)
  RutaProyecto = File.Dir(sRutaSeleccionada)

  Dim sExtension As String = LCase(File.Ext(sRutaSeleccionada))
  Dim sNombreArchivo As String = LCase(File.Name(sRutaSeleccionada))

  ' DETERMINAR TIPO SEGÚN NORMAS: r- (revista .md), l- (libro .md/.tex)
  Dim iTipoSeleccionado As Integer = -1

  Select Case True
    Case Left(sNombreArchivo, 2) = "r-" And sExtension = "md"
      iTipoSeleccionado = m_Constantes.TIPO_REVISTA_MD

    Case Left(sNombreArchivo, 2) = "l-" And sExtension = "md"
      iTipoSeleccionado = m_Constantes.TIPO_LIBRO_MD

    Case Left(sNombreArchivo, 2) = "l-" And sExtension = "tex"
      iTipoSeleccionado = m_Constantes.TIPO_LIBRO_LATEX

    Case Else
      m_Sonido.sonar("Error")
      Message.Error("El archivo seleccionado no cumple las normas de nomenclatura." & gb.NewLine &
        "Formatos válidos:" & gb.NewLine &
        "• Revista Markdown: r-nombre.md" & gb.NewLine &
        "• Libro Markdown: l-nombre.md" & gb.NewLine &
        "• Libro LaTeX: l-nombre.tex" & gb.NewLine &
        "Archivo recibido: " & sNombreArchivo)
      Return
  End Select

  ' BUSCAR EN LA BBDD SI EL PROYECTO QUE VAMOS A ABRIR EXISTE
  Dim sSQL As String = "SELECT * FROM proyectos WHERE nombre = &1"
  Dim Contenido As Result = m_ConexionBD.mConn.Exec(sSQL, NombreProyecto)
  Dim nuevoID As Integer

  ' LIBERAR PROYECTO QUE ESTA ABIERTO
  If NombreProyecto <> "" Then
    Dim sProyectoEnCurso As String = File.Name(txtProyecto.Text)
    m_ConexionBD.mConn.Exec("UPDATE proyectos SET ocupado = 0 WHERE nombre = &1", sProyectoEnCurso)
    m_ConexionBD.mConn.Exec("UPDATE proyectos SET usuario_propietario = NULL WHERE nombre = &1", sProyectoEnCurso)
    m_ConexionBD.mConn.Commit
    m_Timer.DetenerContador()
  Endif

  If Contenido.Available Then
    ' VALIDACIONES SOBRE EL REGISTRO EXISTENTE
    Dim iTipoBD As Integer = Contenido["tipo_producto"]

    ' 1) COINCIDENCIA DE TIPO
    If iTipoBD <> iTipoSeleccionado Then
      m_Sonido.sonar("Error")
      Message.Error("El tipo de archivo no coincide con el tipo registrado en la base de datos." & gb.NewLine &
        "Se esperaba: " & ObtenerDescripcionTipo(iTipoSeleccionado) & gb.NewLine &
        "Se encontró: " & ObtenerDescripcionTipo(iTipoBD))
      Return
    Endif

    ' 2) ACTIVO? (0 ES FALSE Y 1 ES TRUE)
    If Not Contenido["activo"] Then
      m_Sonido.sonar("Warning")
      Message.Warning("El proyecto '" & NombreProyecto & "' está archivado." & gb.NewLine &
        "Debe solicitar al editor que lo reactive para poder abrirlo.")
      Return
    Endif

    ' 3) NO OCUPADO?
    If Contenido["ocupado"] Then
      m_Sonido.sonar("Warning")
      Message.Warning("El proyecto '" & NombreProyecto & "' está ocupado en otra máquina." & gb.NewLine &
        "Lo puede abrir en modo parcial o espere a que se libere.")
      Return
    Endif

    nuevoID = Contenido["id"]
    m_Sonido.sonar("Info")
    Message.Info("Proyecto encontrado en la base de datos y listo para abrir.")
  Else
    ' NO EXISTE: CREAR REGISTRO NUEVO (MySQL)
    m_ConexionBD.mConn.Exec("INSERT INTO proyectos (nombre, tipo_producto, activo, ocupado) VALUES (&1, &2, 1, 0)",
      NombreProyecto, iTipoSeleccionado)

    ' OBTENER LAST_INSERT_ID() EN MYSQL
    Dim rLast As Result = m_ConexionBD.mConn.Exec("SELECT LAST_INSERT_ID()")
    If rLast.Available Then
      nuevoID = rLast[0]
    Else
      ' FALLBACK: SI NO PODEMOS RECUPERAR EL ID, AVISAR Y ABORTAR
      m_ConexionBD.mConn.Rollback
      m_Sonido.sonar("Error")
      Message.Error("No se pudo obtener el ID del proyecto insertado. Abortando apertura.")
      Return
    Endif

    m_ConexionBD.mConn.Commit
    m_Sonido.sonar("Info")
    Message.Info("Proyecto nuevo registrado en la base de datos.")
  Endif

  ' INICIALIZAR TODAS LAS TABLAS RELACIONADAS CON EL PROYECTO
  InicializarTablasProyecto(nuevoID)

  ' MARCAR PROYECTO COMO OCUPADO
  m_ConexionBD.mConn.Exec("UPDATE proyectos SET ocupado = 1 WHERE id = &1", nuevoID)
  m_ConexionBD.mConn.Commit

  ' ACTUALIZAR RUTA DEL PROYECTO
  txtProyecto.Text = sRutaSeleccionada

  ' MOSTRAR / OCULTAR BARRA DE BOTONES
  HBoxGuardarTextArea.Visible = False

  ' LIMPIAMOS LOS FORMULARIOS
  m_Metadatos.LimpiarCamposMetadatos()
  m_Articulos.LimpiarTextboxArticulos()
  m_Autores.LimpiarTextboxAutores()
  m_Bibtex.LimpiarTextboxBibtex()
  m_Siglas.LimpiarTextboxSigla()

  ' LIMPIAR CACHÉ O ENTORNO PREVIO
  Shell Quote$("rm -rf " & User.Home & "/.local/share/org.gambas.*") & "\n" Wait

  ' CONTROLOMANOS EXISTAN LOS ARCHIVOS BASE PARA UN PROYECTO
  m_FuncionesGenericas.CopiarArchivosBase()

  ' CONFIGURAR INTERFAZ PARA LIBRO LATEX
  m_FuncionesGenericas.ConfigurarFormulariosLibroLaTeX()

  ' MOSTRAR EL ID DEL PROYECTO
  id_proyecto.Value = nuevoID

  ' CARGAMOS LOS SCRIPTS PARA MANIPULAR CON LOS *.tex
  m_InicioCierre.CargarScripts()

  ' CARGAMOS LOS METADATOS DEL PROYECTO
  m_Metadatos.CargarMetadatosDelProyecto()

  ' CARGAMOS LOS DATOS DE LA ORDEN DE TALLER
  m_OrdenTaller.CargarOrdenTaller()

  ' CARGAMOS LAS REFERENCIAS BIBTEX
  m_Bibtex.IniciarBibtex()

  ' CARGAMOS LAS SIGLAS
  m_Siglas.IniciarSiglas()

  ' ARRANCAMOS EL CONTADOR
  m_Timer.IniciarContadorReloj(id_proyecto.Value)

  ' MARCAR PROYECTO COMO OCUPADO CON PROPIETARIO
  Dim usuarioPropietario As String = Trim(FMain.TextBoxUsuario.Text)
  Dim idProyecto As Integer = FMain.id_proyecto.Value

  m_ConexionBD.mConn.Exec("UPDATE proyectos SET ocupado = 1, usuario_propietario = &1 WHERE id = &2", usuarioPropietario, idProyecto)

  If Error Then
    Message.Error("Error al ejecutar UPDATE: " & Error.Text)
    Return
  Endif

  m_ConexionBD.mConn.Commit

  ' LIMPIAMOS EL EDITOR Y LO PONEMOS READONLY=FALSE
  txtEditorProyecto.Text = ""
  txtEditorProyecto.ReadOnly = True
  txtEditorProyecto.Enabled = False

  ' OCULTAMOS EL PANEL INFERIOR IZQUIERDO
  TabPanelIzquierdaInferior.Visible = False
  HBoxBuscar.Visible = False

End

Public Sub btnVerEPUB_Click()

  Dim sRutaCompleta As String
  Dim sArchivoSeleccionado As String

  ' OBTENER EL ARCHIVO SELECCIONADO
  sArchivoSeleccionado = FileViewProyecto.Current

  sRutaCompleta = FileViewProyecto.Dir &/ sArchivoSeleccionado

  ' VERIFICAR QUE EL ARCHIVO EXISTE
  If Not Exist(sRutaCompleta) Then
    m_Sonido.sonar("Error")
    Message.Error("El archivo seleccionado no existe")
    Return
  Else
    Desktop.Open(sRutaCompleta)
  Endif

End

Public Sub btnVerPDF_Click()

  Dim sRutaCompleta As String
  Dim sArchivoSeleccionado As String

  ' OBTENER EL ARCHIVO SELECCIONADO
  sArchivoSeleccionado = FileViewProyecto.Current

  sRutaCompleta = FileViewProyecto.Dir &/ sArchivoSeleccionado

  ' VERIFICAR QUE EL ARCHIVO EXISTE
  If Not Exist(sRutaCompleta) Then
    m_Sonido.sonar("Error")
    Message.Error("El archivo seleccionado no existe")
    Return
  Else
    Desktop.Open(sRutaCompleta)
  Endif

End

Public Sub btnVerHTML_Click()

  Dim sRutaCompleta As String
  Dim sArchivoSeleccionado As String

  ' OBTENER EL ARCHIVO SELECCIONADO
  sArchivoSeleccionado = FileViewProyecto.Current

  sRutaCompleta = FileViewProyecto.Dir &/ sArchivoSeleccionado

  ' VERIFICAR QUE EL ARCHIVO EXISTE
  If Not Exist(sRutaCompleta) Then
    m_Sonido.sonar("Error")
    Message.Error("El archivo seleccionado no existe")
    Return
  Else
    Desktop.Open(sRutaCompleta)
  Endif

End

Public Sub ComboBoxScripts_Click()

  m_FuncionesGenericas.EjecutarScript(ComboBoxScripts)

End

Public Sub BtnBuscarEnArchivos_Click()

  Dim palabraAbuscar As String

  palabraAbuscar = TextBoxBuscarEnConsola.Text

  TerminalViewProyecto.Clear
  TerminalViewProyecto.Input("python3 " & rutaApp & "/buscador.py " & palabraAbuscar & "\n")
  TerminalViewProyecto.SetFocus

End

Public Sub txtLibroPaginas_KeyPress()

  ' SOLO PERMITIR NÚMEROS 0-9, TECLAS DE CONTROL Y BACKSPACE
  If Key.Code >= Key.F1 Then Return ' PERMITIR TECLAS DE FUNCIÓN
  If Key.Code = Key.BackSpace Or Key.Code = Key.Delete Or Key.Code = Key.Left Or Key.Code = Key.Right Then Return

  ' SI NO ES UN DÍGITO, CANCELAR LA TECLA
  If Key.Text < "0" Or Key.Text > "9" Then
    Stop Event ' CANCELA LA ENTRADA DE LA TECLA
  Endif

End

Public Sub txtRevistaVolumen_KeyPress()

  If Key.Code >= Key.F1 Then Return
  If Key.Code = Key.BackSpace Or Key.Code = Key.Delete Or Key.Code = Key.Left Or Key.Code = Key.Right Then Return

  If Key.Text < "0" Or Key.Text > "9" Then
    Stop Event
  Endif

End

Private Function ValidarEmail(email As String) As Boolean

  email = Trim(email)
  If email = "" Then Return True

  Dim partes As String[] = Split(email, "@")
  If partes.Count <> 2 Then Return False

  Dim usuario As String = partes[0]
  Dim dominio As String = partes[1]

  If Len(usuario) = 0 Or Len(dominio) = 0 Then Return False
  If InStr(dominio, ".") = 0 Then Return False

  Return True

End

Public Sub txtEditorJefeEmail_LostFocus()

  If Not ValidarEmail(txtEditorJefeEmail.Text) Then
    m_Sonido.sonar("Warning")
    Message.Warning("Email no válido")
    txtEditorJefeEmail.SetFocus()
  Endif

End

Public Sub txtRevistaEmail_LostFocus()

  If Not ValidarEmail(txtRevistaEmail.Text) Then
    m_Sonido.sonar("Warning")
    Message.Warning("Email no válido")
    txtRevistaEmail.SetFocus()
  Endif

End

Public Sub txtEditorAsociadoEmail_LostFocus()

  If Not ValidarEmail(txtEditorAsociadoEmail.Text) Then
    m_Sonido.sonar("Warning")
    Message.Warning("Email no válido")
    txtEditorAsociadoEmail.SetFocus()
  Endif

End

Public Sub txtCoordinadorEditorialEmail_LostFocus()

  If Not ValidarEmail(txtCoordinadorEditorialEmail.Text) Then
    m_Sonido.sonar("Warning")
    Message.Warning("Email no válido")
    txtCoordinadorEditorialEmail.SetFocus()
  Endif

End

Public Sub CargarCSLBibTeX()

  Dim rutaArchivos As String = User.Home &/ ".gbpublisher/csl/"
  Dim archivos As String[]
  Dim archivo As String
  Dim linea As String
  Dim descripcion As String
  Dim hFile As File
  Dim i As Integer
  Dim encontrado As Boolean

  If Not Exist(rutaArchivos) Then
    m_Sonido.sonar("Error")
    Message.Error("El directorio no existe: " & rutaArchivos)
    Return
  Endif

  cmbTipoCSL.Clear

  ' OBTENER LISTA DE ARCHIVOS .CSL
  archivos = Dir(rutaArchivos, "*.csl")
  If Error Then
    archivos = New String[]
  Endif

  If archivos.Count = 0 Then
    m_Sonido.sonar("Info")
    Message.Info("No se encontraron archivos .csl en el directorio")
    Return
  Endif

  ' PROCESAR CADA ARCHIVO
  For Each archivo In archivos
    descripcion = "Sin descripción"
    encontrado = False
    Dim rutaCompleta As String = rutaArchivos &/ archivo

    hFile = Open rutaCompleta For Read
    If Not Error Then
      ' LEER LAS PRIMERAS LÍNEAS BUSCANDO EL COMENTARIO
      For i = 1 To 3  ' REVISAR HASTA 3 LÍNEAS POR SI ACASO
        If Eof(hFile) Then Break

        Line Input #hFile, linea
        If Error Then Break

        linea = Trim(linea)

        ' PARA ARCHIVOS .CSL (COMENTARIOS XML)
        If Right(archivo, 4) = ".csl" Then
          If Left(linea, 4) = "<!--" And Right(linea, 3) = "-->" Then
            descripcion = Trim(Mid(linea, 5, Len(linea) - 7))
            encontrado = True
            Break
          Endif
          ' PARA ARCHIVOS .TEX (COMENTARIOS LATEX)
        Else If Right(archivo, 4) = ".tex" Then
          If Left(linea, 1) = "%" And Len(linea) > 2 Then
            descripcion = Trim(Mid(linea, 2))
            encontrado = True
            Break
          Endif
        Endif
      Next

      Close #hFile
    Else
      descripcion = "Error al leer archivo"
    Endif

    ' AGREGAR AL COMBOBOX
    cmbTipoCSL.Add(descripcion & " [" & archivo & "]")
  Next

End

Public Sub txtRevistaIdioma_KeyPress()

  Dim texto As String = txtRevistaIdioma.Text
  Dim posicion As Integer = txtRevistaIdioma.Pos
  Dim tecla As String = Key.Text

  ' PERMITIR TECLAS DE CONTROL
  If Key.Code = Key.BackSpace Or Key.Code = Key.Delete Or Key.Code = Key.Left Or Key.Code = Key.Right Then Return
  If Key.Code >= Key.F1 Then Return  ' Teclas de función

  ' LIMITAR LONGITUD MÁXIMA (AA-AA = 5 CARACTERES)
  If Len(texto) >= 5 And Key.Code <> Key.BackSpace Then
    Stop Event
    Return
  Endif

  ' VALIDAR SEGÚN POSICIÓN
  Select Case posicion
    Case 0, 1  ' PRIMERAS DOS POSICIONES: SOLO LETRAS MINÚSCULAS
      If tecla < "a" Or tecla > "z" Then Stop Event
    Case 2  ' TERCERA POSICIÓN: SOLO GUIÓN
      If tecla <> "-" Then Stop Event
    Case 3, 4  ' CUARTA Y QUINTA POSICIÓN: SOLO LETRAS MAYÚSCULAS
      If tecla < "A" Or tecla > "Z" Then Stop Event
    Default
      Stop Event  ' NO PERMITIR MÁS CARACTERES
  End Select

End

Public Sub txtRevistaIdioma_LostFocus()

  Dim idioma As String = Trim(txtRevistaIdioma.Text)

  ' SI ESTÁ VACÍO, ES VÁLIDO - RESTAURAR COLOR Y SALIR
  If idioma = "" Then
    txtRevistaIdioma.Background = Color.White
    Return
  Endif

  ' SI NO ESTÁ VACÍO, DEBE TENER EXACTAMENTE 5 CARACTERES
  If Len(idioma) <> 5 Then
    txtRevistaIdioma.Background = Color.Pink
    m_Sonido.sonar("Warning")
    Message.Warning("El formato del idioma debe ser: aa-AA (por ejemplo: es-ES)")
    txtRevistaIdioma.SetFocus()
    Return
  Endif

  ' Validar formato completo
  Dim char1 As String = Mid(idioma, 1, 1)
  Dim char2 As String = Mid(idioma, 2, 1)
  Dim char3 As String = Mid(idioma, 3, 1)
  Dim char4 As String = Mid(idioma, 4, 1)
  Dim char5 As String = Mid(idioma, 5, 1)

  If (char1 < "a" Or char1 > "z") Or
      (char2 < "a" Or char2 > "z") Or
      (char3 <> "-") Or
      (char4 < "A" Or char4 > "Z") Or
      (char5 < "A" Or char5 > "Z") Then
    txtRevistaIdioma.Background = Color.Pink
    m_Sonido.sonar("Warning")
    Message.Warning("El formato del idioma debe ser: aa-AA (por ejemplo: es-ES)")
    txtRevistaIdioma.SetFocus()
  Else
    txtRevistaIdioma.Background = Color.White
  Endif

End

Public Sub ButtonSeleccionarLogoLicencia_Click()

  Dialog.Title = "Seleccionar archivo"
  Dialog.Filter = ["*.png", ("Archivos png"), ("Todos los archivos")]
  Dialog.AutoExt = True
  Dialog.Path = RutaProyecto & "/media/"
  If Dialog.OpenFile() Then
    Return
  Else
    txtImagenTapita.Text = File.Name(Dialog.Path)
  Endif
Catch
  m_Sonido.sonar("Error")
  Message.Error("No se pudo abrir el archivo")

End

Public Sub ButtonVerResumenEs_Click()

  f_TXTextendido.OriginalTextBox = txtLibroResumenEs
  f_TXTextendido.ShowModal

End

Public Sub ButtonVerResumenEn_Click()

  f_TXTextendido.OriginalTextBox = txtLibroResumenEn
  f_TXTextendido.ShowModal

End

Public Sub ButtonMostrarResumenEs_Click()

  f_TXTextendido.OriginalTextBox = txtResumenEs
  f_TXTextendido.ShowModal

End

Public Sub ButtonMostrarResumenEn_Click()

  f_TXTextendido.OriginalTextBox = txtResumenEn
  f_TXTextendido.ShowModal

End

Public Sub ButtonMostrarAbstract_Click()

  f_TXTextendido.OriginalTextBox = txtAbstract
  f_TXTextendido.ShowModal

End

Public Sub ButtonMostrarNote_Click()

  f_TXTextendido.OriginalTextBox = txtNote
  f_TXTextendido.ShowModal

End

Public Sub ButtonMostrarLibrary_Click()

  f_TXTextendido.OriginalTextBox = txtLibrary
  f_TXTextendido.ShowModal

End

Public Sub ButtonMostrarAnnotation_Click()

  f_TXTextendido.OriginalTextBox = txtAnnotation
  f_TXTextendido.ShowModal

End

Public Sub ButtonMostrarFile_Click()

  f_TXTextendido.OriginalTextBox = txtFile
  f_TXTextendido.ShowModal

End

Public Sub btnMostrarGlosarioDescripcionExtendido_Click()

  f_TXTextendido.OriginalTextBox = txtDescripcionGlosario
  f_TXTextendido.ShowModal

End

Public Sub menuSalidaTeX_Click()

  ' VERIFICAR QUE TXTPROYECTO.TEXT NO ESTÉ VACÍO
  If Trim(txtProyecto.Text) = "" Then
    m_Sonido.sonar("Warning")
    Message.Warning("No se ha especificado un archivo de proyecto")
    Return
  Else
    Dim sExtension As String
    ' OBTENER LA EXTENSIÓN DEL ARCHIVO
    sExtension = LCase(File.Ext(txtProyecto.Text))
    ' EJECUTAR LA FUNCIÓN CORRESPONDIENTE SEGÚN LA EXTENSIÓN
    Select Case sExtension
      Case "md"
        m_FuncionesExportar.GenerarTEXlibroMD()
      Default
        m_Sonido.sonar("Error")
        Message.Error("Extensión no soportada: " & sExtension & Chr(10) & "Solo se admiten archivos .md")
    End Select

  Endif

End

Public Sub menuTeXconCambios_Click()

  Dim ProyectoTeX As String = File.Dir(txtProyecto.Text) &/ "salidas/tex/" & File.BaseName(FMain.txtProyecto.Text) & ".tex"
  Dim compilar As String = "latexmk -l -f -outdir=./salidas/pdf  --interaction=nonstopmode -pdflatex=lualatex -pdflua " & ProyectoTeX

  If Exist(ProyectoTeX) Then
    TerminalViewProyecto.Input(compilar & "\n")
    Wait 2
  Else
    m_Sonido.sonar("Info")
    Message.Info("No se encuentra el archivo " & File.BaseName(FMain.txtProyecto.Text) & ".tex para compilar.")
  Endif

  m_FuncionesGenericas.BorrarArchivosAuxiliares()

End

Public Sub menuXMLlibroIndesign_Click()

  TerminalViewProyecto.Input("cd " & RutaProyecto & "\n")
  TerminalViewProyecto.Input("clear" & "\n")

  m_FuncionesExportar.ObtenerXMLlibroIndesign()

End

Public Sub btnLimpiarTapita_Click()

  txtImagenTapita.Clear

End

Public Sub btnLimpiarLicencia_Click()

  txtLogoLicencia.Clear

End

Public Sub TimerHOY_Timer()

  m_Timer.ActualizarContador()

End

Public Sub menuPDFsoloTeX_Click()

  FMain.TabPanelIzquierdaInferior.Index = 0
  m_GenerarSalidas.GenerarPDFdesdeLATEX()

End

Public Sub btnChequearISBNLibro_Click()

  Dim ISBN As String = txtLibroISBN.Text

  If m_FuncionesGenericas.EsISBNValido(ISBN) Then
    m_Sonido.sonar("Info")
    Message.Info("El ISBN es válido.")
  Else
    m_Sonido.sonar("Warning")
    Message.Warning("El ISBN no es válido.")
  End If

End

Public Sub ComboBoxLicencias_Click()

  m_Sonido.sonar("Info")
  ' VERIFICAR QUE HAY UN ELEMENTO SELECCIONADO
  If ComboBoxLicencias.Index >= 0 Then
    ' COPIAR EL TEXTO DEL ELEMENTO SELECCIONADO AL TEXTBOX
    txtLicencia.Text = ComboBoxLicencias.Text
  End If

End Sub

Public Sub btnInfo_Click()

  m_Sonido.sonar("Info")
  Message.Warning("<b>Atención:</b> gbpublisher solo admite rutas de directorios con letras simples (a-z, A-Z) y sin espacios.\n\nEl uso de acentos, ñ u otros caracteres especiales puede generar errores en las distintas compilaciones.", "Entendido")

End

Public Sub txtPublisher_KeyPress()

  If Key.Code = Key.Enter Or Key.Code = Key.Return Then
    m_FuncionesGenericas.BuscarEditorialDesdeTexto()
  Endif

End

Public Sub txtInstitution_KeyPress()

  If Key.Code = Key.Enter Or Key.Code = Key.Return Then
    m_FuncionesGenericas.BuscarInstitucionDesdeTexto()
  Endif

End

Public Sub txtOrganization_KeyPress()

  If Key.Code = Key.Enter Or Key.Code = Key.Return Then
    m_FuncionesGenericas.BuscarOrganizationDesdeTexto()
  Endif

End

Public Sub txtLocation_KeyPress()

  If Key.Code = Key.Enter Or Key.Code = Key.Return Then
    m_FuncionesGenericas.BuscarLocationDesdeTexto()
  Endif

End

Public Sub txtJournalTitle_KeyPress()

  If Key.Code = Key.Enter Or Key.Code = Key.Return Then
    m_FuncionesGenericas.BuscarJournalDesdeTexto()
  Endif

End

Public Sub txtLocation_GotFocus()

  If Trim(txtLocation.Text) <> "" Then
    m_FuncionesGenericas.BuscarLocationDesdeTexto()
  Endif

End

Public Sub txtLocation_LostFocus()

  TimerLocation.Delay = 100
  TimerLocation.Start()

End

Public Sub TimerPublisher_Timer()

  TimerPublisher.Stop()
  ComboBoxPublisher.Visible = False

End

Public Sub TimerInstitution_Timer()

  TimerInstitution.Stop()
  ComboBoxInstitution.Visible = False

End

Public Sub TimerOrganization_Timer()

  TimerOrganization.Stop()
  ComboBoxOrganization.Visible = False

End

Public Sub TimerLocation_Timer()

  TimerLocation.Stop()
  ComboBoxLocation.Visible = False

End

Public Sub TimerJournal_Timer()

  TimerJournal.Stop()
  ComboBoxJournal.Visible = False

End

Public Sub ComboBoxPublisher_Click()

  If ComboBoxPublisher.Index >= 0 Then
    txtPublisher.Text = ComboBoxPublisher.Text
    ComboBoxPublisher.Visible = False
  Endif

End

Public Sub ComboBoxInstitution_Click()

  If ComboBoxInstitution.Index >= 0 Then
    txtInstitution.Text = ComboBoxInstitution.Text
    ComboBoxInstitution.Visible = False
  Endif

End

Public Sub ComboBoxOrganization_Click()

  If ComboBoxOrganization.Index >= 0 Then
    txtOrganization.Text = ComboBoxOrganization.Text
    ComboBoxOrganization.Visible = False
  Endif

End

Public Sub ComboBoxLocation_Click()

  If ComboBoxLocation.Index >= 0 Then
    txtLocation.Text = ComboBoxLocation.Text
    ComboBoxLocation.Visible = False
  Endif

End

Public Sub txtPages_Change()

  Dim curpos As Integer

  curpos = txtPages.pos

  Dim texto As String

  texto = Trim(txtPages.Text)

  texto = Replace(texto, "  ", " ")
  texto = Replace(texto, "–", "-")
  texto = Replace(texto, "--", "-")
  texto = Replace(texto, " -", "-")
  texto = Replace(texto, "- ", "-")
  texto = Replace(texto, " a ", "-")
  texto = Replace(texto, ".", "")
  texto = Replace(texto, " al ", "-")

  txtPages.Text = Trim(texto)

  ' Restaura la posición del cursor
  txtPages.pos = curpos

End

Public Sub AllTextBoxes_MouseDown()

  LastTextBox = Last
  sTB_Text = Last.Text

End

Public Sub menuNormalizarGlifos_Click()

  If sTB_Text <> "" Then
    LastTextBox.Text = Replace(LastTextBox.Text, "’", "'")
    LastTextBox.Text = Replace(LastTextBox.Text, "´", "'")
    LastTextBox.Text = Replace(LastTextBox.Text, "`", "'")
  End If

End

Public Sub menuNormalizarBIBTEX_Click()

  If sTB_Text <> "" Then LastTextBox.Text = String.LCase(LastTextBox.Text)
  AplicarMayusculasConExcepcion(LastTextBox)

End

Public Sub AplicarMayusculasConExcepcion(textBox As TextBox)'usada para normalizar autores

  Dim Resultado As String
  Dim palabras As String[] = Split(textBox.Text, " ")
  Dim palabraActual As String

  For i As Integer = 0 To palabras.Max
    palabraActual = palabras[i]

    ' VERIFICAR SI LA PALABRA ES "Y" O "&"
    If palabraActual = "y" Or palabraActual = "&" Or palabraActual = ", y" Then
      Resultado &= "and"
    Else If palabraActual = "and" Then
      Resultado &= "and"
    Else
      ' CONVERTIR LA PRIMERA LETRA A MAYÚSCULA Y EL RESTO A MINÚSCULA
      Resultado &= String.UCase(String.Left(palabraActual, 1)) & String.LCase(Mid(palabraActual, 2))
      ' REEMPLAZAR LA SECUENCIA ", Y" POR " AND " (CON ESPACIOS ANTES Y DESPUÉS)
      Resultado = Replace(Resultado, ", and ", " and ")
      Resultado = Replace(Resultado, "; and ", " and ")
      Resultado = Replace(Resultado, ". and ", " and ")
    End If

    ' Agregar un espacio entre palabras excepto al final
    If i < palabras.Max Then
      Resultado &= " "
    End If
  Next

  ' ESTABLECER EL NUEVO TEXTO EN EL TEXTBOX
  textBox.Text = Resultado

End

Public Sub menuConvertirMinusculas_Click()

  If sTB_Text <> "" Then
    LastTextBox.Text = Trim(LastTextBox.Text)
    LastTextBox.Text = String.LCase(LastTextBox.Text)
  End If

End

Public Sub menuConvertirMayusculas_Click()

  If sTB_Text <> "" Then
    LastTextBox.Text = Trim(LastTextBox.Text)
    LastTextBox.Text = String.UCase(LastTextBox.Text)
  End If

End

Public Sub menuTodoMayuscula_Click()

  If sTB_Text <> "" Then LastTextBox.Text = String.LCase(LastTextBox.Text)
  AplicarMayusculasTotal(LastTextBox)

End

Public Sub menuMayusculaInicial_Click()

  If sTB_Text <> "" Then
    LastTextBox.Text = String.LCase(LastTextBox.Text)
    AplicarMayusculasALetras(LastTextBox)
  End If

End

Public Sub AplicarMayusculasALetras(textBox As TextBox)

  Dim Resultado As String
  Dim convertir As Boolean = True ' CAPITALIZAR LA PRIMERA LETRA AL INICIO

  ' OBTENER EL TEXTO ACTUAL DEL TEXTBOX
  sTB_Text = TextBox.Text

  ' Recorrer cada carácter en la cadena
  For i As Integer = 1 To String.Len(sTB_Text)
    Dim caracter As String = String.Mid(sTB_Text, i, 1)

    ' VERIFICAR SI SE DEBE CONVERTIR A MAYÚSCULA
    If convertir And (caracter >= "a" And caracter <= "z") Then
      Resultado &= String.UCase(caracter)
      convertir = False ' DESPUÉS DE CONVERTIR, NO SE DEBE CONVERTIR EL SIGUIENTE CARÁCTER
    Else
      Resultado &= String.LCase(caracter)
    End If

    ' SI EL CARÁCTER ACTUAL ES UN PUNTO
    If caracter = "." Then
      ' ASEGURARSE DE QUE HAYA UN ESPACIO DESPUÉS DEL PUNTO
      If i < String.Len(sTB_Text) And String.Mid(sTB_Text, i + 1, 1) = " " Then
        convertir = True
      End If
    End If

    ' SI EL CARÁCTER ACTUAL ES UN ESPACIO, EL PRÓXIMO CARÁCTER DEBE CONVERTIRSE A MAYÚSCULA SI SE SIGUIÓ UN PUNTO
    If caracter = " " Then
      ' SOLO REACTIVAR CONVERTIR SI EL CARÁCTER ANTERIOR ERA UN PUNTO
      If i > 1 And String.Mid(sTB_Text, i - 1, 1) = "." Then
        convertir = True
      End If
    End If
  Next

  ' ESTABLECER EL NUEVO TEXTO EN EL TEXTBOX
  textBox.Text = Resultado

End

Public Sub AplicarMayusculasTotal(textBox As TextBox)

  Dim Resultado As String

  ' OBTENER EL TEXTO ACTUAL DEL TEXTBOX
  sTB_Text = textBox.Text

  ' VARIABLE PARA DETERMINAR SI EL PRÓXIMO CARÁCTER DEBE CONVERTIRSE A MAYÚSCULA
  Dim convertir As Boolean = True

  ' RECORRER CADA CARÁCTER EN LA CADENA
  For i As Integer = 1 To String.Len(sTB_Text)
    Dim caracter As String = String.Mid(sTB_Text, i, 1)

    ' VERIFICAR SI SE DEBE CONVERTIR A MAYÚSCULA
    If convertir Then
      ' VERIFICAR LA EXCEPCIÓN PARA LA PALABRA "AND"
      Dim palabraAnd As String = String.LCase(String.Mid(sTB_Text, i, 3))

      If palabraAnd = "and" Then
        Resultado &= "and" ' AGREGAR "AND" SIN CAMBIOS
        i = i + 2 ' SALTAR LAS LETRAS DE LA PALABRA "AND"
      Else
        Resultado &= String.UCase(caracter) ' CONVERTIR A MAYÚSCULA
      End If

      convertir = False
    Else
      Resultado &= String.UCase(caracter) ' CONVERTIR A MAYÚSCULA
    End If

    ' SI EL CARÁCTER ACTUAL ES UN ESPACIO, EL PRÓXIMO CARÁCTER DEBE CONVERTIRSE
    If caracter = " " Then
      convertir = True
    End If
  Next

  ' ESTABLECER EL NUEVO TEXTO EN EL TEXTBOX
  textBox.Text = Resultado

End

Public Sub menuMayusculaParaIngles_Click()

  If sTB_Text <> "" Then LastTextBox.Text = String.LCase(LastTextBox.Text)
  AplicarTitleCase(LastTextBox)

End

Public Sub AplicarTitleCase(textBox As TextBox)

  Dim Resultado As String
  Dim palabras As String[]

  ' DIVIDIR EL TEXTO EN PALABRAS SEPARADAS POR ESPACIOS
  palabras = Split(sTB_Text, " ")

  ' LISTA DE PALABRAS QUE DEBEN IR EN MINÚSCULAS (EXCEPTO SI SON LA PRIMERA, ÚLTIMA PALABRA, O DESPUÉS DE UN PUNTO)
  Dim minusc As New Collection

  minusc.Add("and", "and")
  minusc.Add("but", "but")
  minusc.Add("or", "or")
  minusc.Add("the", "the")
  minusc.Add("a", "a")
  minusc.Add("an", "an")
  minusc.Add("in", "in")
  minusc.Add("on", "on")
  minusc.Add("at", "at")
  minusc.Add("by", "by")
  minusc.Add("for", "for")
  minusc.Add("with", "with")
  minusc.Add("about", "about")
  minusc.Add("from", "from")
  minusc.Add("of", "of")
  minusc.Add("to", "to")

  ' PARA MANEJAR DESPUÉS DE UN PUNTO Y LA PRIMERA PALABRA
  Dim convertirProximaPalabra As Boolean = True

  For i As Integer = 0 To palabras.Max
    Dim palabra As String = String.LCase(palabras[i])

    ' MANEJO DE PALABRAS CON GUIONES
    Dim subPalabras As String[] = Split(palabra, "-")
    ' INICIALIZAR PALABRAFINAL EN CADA ITERACIÓN
    Dim palabraFinal As String = ""

    For Each subPalabra As String In subPalabras
      ' SI ES LA PRIMERA, ÚLTIMA PALABRA, DESPUÉS DE UN PUNTO, O LA SUBPALABRA NO ESTÁ EN LA LISTA DE MINÚSCULAS
      If convertirProximaPalabra Or i = 0 Or i = palabras.Max Or Not minusc.Exist(subPalabra) Then
        palabraFinal &= String.UCase(String.Left(subPalabra, 1)) & String.LCase(String.Mid(subPalabra, 2))
      Else
        palabraFinal &= subPalabra
      End If

      ' AÑADIR GUION ENTRE LAS SUBPALABRAS
      If subPalabra <> subPalabras.Last Then
        palabraFinal &= "-"
      End If
    Next

    Resultado &= palabraFinal

    ' VERIFICAR SI LA PALABRA ACTUAL TERMINA CON UN PUNTO
    If String.Right(palabras[i], 1) = "." Then
      convertirProximaPalabra = True
    Else
      convertirProximaPalabra = False
    End If

    ' AÑADIR UN ESPACIO DESPUÉS DE CADA PALABRA, EXCEPTO LA ÚLTIMA
    If i < palabras.Max Then
      Resultado &= " "
    End If
  Next

  ' ESTABLECER EL NUEVO TEXTO EN EL TEXTBOX
  textBox.Text = Resultado

End

Public Sub Menu37_Click()

  ' REEMPLAZA LOS ESPACIOS EN BLANCO CON EL SIGNO DE "+" PARA FORMATEAR LA CONSULTA DE BÚSQUEDA
  sTB_Text = Replace(Replace(sTB_Text, " ", "+"), ",+", "+")

  If sTB_Text <> "" Then Desktop.Open("https://cse.google.com/cse?cx=005053095451413799011:alg8dd3pluq&q=" & sTB_Text)

End

Public Sub Menu36_Click()

  If sTB_Text <> "" Then Desktop.Open("https://dle.rae.es/" & sTB_Text)

End

Public Sub Menu35_Click()

  If sTB_Text <> "" Then Desktop.Open("https://www.wordreference.com/definicion/" & sTB_Text)

End

Public Sub Menu34_Click()

  If sTB_Text <> "" Then Desktop.Open("https://www.bing.com/search?q=" & sTB_Text)

End

Public Sub Menu33_Click()

  If sTB_Text <> "" Then Desktop.Open("https://html.duckduckgo.com/html?q=" & sTB_Text)'

End

Public Sub Menu32_Click()

  If sTB_Text <> "" Then Desktop.Open("https://www.google.com/search?q=" & sTB_Text)

End

' OBTENER INFORMACIÓN DEL CPU
Private Function GetCPUInfo() As String

  Dim sResult As String = "No disponible"
  Dim hFile As File
  Dim sLine As String
  Dim asParts As String[]

  Try hFile = Open "/proc/cpuinfo" For Input
  If Error Then Return "Error al leer información del CPU"

  While Not Eof(hFile)
    Line Input #hFile, sLine
    If sLine Begins "model name" Then
      asParts = Split(sLine, ":")
      sResult = Trim(asParts[1])
      Break
    Endif
  Wend
  Close #hFile

  Return sResult

End

' OBTENER INFORMACIÓN DE MEMORIA
Private Function GetMemoryInfo() As String

  Dim sResult As String = "No disponible"
  Dim hFile As File
  Dim sLine As String
  Dim iMemTotal As Long
  Dim asParts As String[]
  Dim sMemValue As String

  Try hFile = Open "/proc/meminfo" For Input
  If Error Then Return "Error al leer información de memoria"

  While Not Eof(hFile)
    Line Input #hFile, sLine
    If sLine Begins "MemTotal:" Then
      asParts = Split(sLine, ":")
      If asParts.Count >= 2 Then
        sMemValue = Trim(asParts[1])
        ' REMOVER "KB" SI ESTÁ PRESENTE
        sMemValue = Replace(sMemValue, " kB", "")
        sMemValue = Replace(sMemValue, "kB", "")
        iMemTotal = Val(sMemValue) \ 1024
        sResult = CStr(iMemTotal) & " MB"
      Endif
      Break
    Endif
  Wend
  Close #hFile

  Return sResult

End

Public Sub btnGuardarIGUAL_Click()

  ' VERIFICAR QUE HAY UNA TEXTO EN LA CLAVEBIB
  If Trim(txtClaveBibtex.Text) = "" Then
    m_Sonido.sonar("Info")
    Message.Info("Debe generar una clave. No es posible guardar la entrada.")
    Return
  Endif

  If m_Bibtex.VerificarCaracteresInvalidos() Then Return

  If m_Bibtex.VerificacionBase() Then Return

  m_Bibtex.GuardarBibtexPrimeraVez()
  m_Bibtex.RefrescarTableViewBibtexEnCurso()
  m_Bibtex.LimpiarTextboxBibtex()
  gvReferenciasEnCurso.Refresh

  txtTMPbiblio.Enabled = False
  txtTMPbiblio.Background = Color.Background
  txtTMPbiblio.Clear
  TabPanel5.Index = 0

  BtnNuevoBib.Visible = True
  btnGuardarBib.Visible = False
  btnGuardarCambiosBib.Visible = False
  BtnEliminarBib.Visible = False
  btnGuardarIGUAL.Visible = False

Catch
  m_Sonido.sonar("Error")
  Message.Error("Se obtuvo el siguiente error al intentar guardar: <b>" & Error.Text & "</b>.")

End

Public Sub BtnEliminarBib_Click()

  m_Bibtex.EliminarBibtex()

End

Public Sub btnAbrirDirectorio_Click()

  Dim sText As String = RutaProyecto

  If Trim(sText) = "" Then
    m_Sonido.sonar("Warning")
    Message.Warning("No tiene ningún proyecto abierto.")
    Return
  Endif

  If Exist(sText) Then
    m_Sonido.sonar("OpenApp")
    Desktop.Open(sText)
  Endif

  TerminalViewProyecto.SetFocus

End

Public Sub btnNuevoGlosario_Click()

  m_siglas.NuevoGlosario()

End

Public Sub btnBorrarGlosario_Click()

  m_siglas.BorrarGlosario()

End

Public Sub btnAgregarClaveGlosario_Click()

  ' LIMPIAMOS EL TEXTBOX DE LA CLAVE
  txtClaveGlosario.Text = ""

  Dim textoOrigen As String

  textoOrigen = txtNameGlosario.Text

  ' Reemplaza caracteres problemáticos
  textoOrigen = Replace(textoOrigen, "´", "")
  textoOrigen = Replace(textoOrigen, "`", "")
  textoOrigen = Replace(textoOrigen, " ", "")
  textoOrigen = Replace(textoOrigen, "á", "a")
  textoOrigen = Replace(textoOrigen, "à", "a")
  textoOrigen = Replace(textoOrigen, "ä", "a")
  textoOrigen = Replace(textoOrigen, "é", "e")
  textoOrigen = Replace(textoOrigen, "è", "e")
  textoOrigen = Replace(textoOrigen, "ë", "e")
  textoOrigen = Replace(textoOrigen, "í", "i")
  textoOrigen = Replace(textoOrigen, "ì", "i")
  textoOrigen = Replace(textoOrigen, "ï", "i")
  textoOrigen = Replace(textoOrigen, "ó", "o")
  textoOrigen = Replace(textoOrigen, "ò", "o")
  textoOrigen = Replace(textoOrigen, "ö", "o")
  textoOrigen = Replace(textoOrigen, "ú", "u")
  textoOrigen = Replace(textoOrigen, "ù", "u")
  textoOrigen = Replace(textoOrigen, "ü", "u")
  textoOrigen = Replace(textoOrigen, "ñ", "n")
  textoOrigen = Replace(textoOrigen, "&", "")
  textoOrigen = Replace(textoOrigen, "ç", "c")
  textoOrigen = Replace(textoOrigen, "{", "")
  textoOrigen = Replace(textoOrigen, "}", "")
  textoOrigen = Replace(textoOrigen, "[", "")
  textoOrigen = Replace(textoOrigen, "]", "")
  textoOrigen = Replace(textoOrigen, "¿", "")
  textoOrigen = Replace(textoOrigen, "?", "")
  textoOrigen = Replace(textoOrigen, "¡", "")
  textoOrigen = Replace(textoOrigen, "!", "")
  textoOrigen = Replace(textoOrigen, "(", "")
  textoOrigen = Replace(textoOrigen, ")", "")
  textoOrigen = Replace(textoOrigen, "-", "")
  textoOrigen = Replace(textoOrigen, "_", "")
  textoOrigen = Replace(textoOrigen, "'", "")
  textoOrigen = Replace(textoOrigen, "#", "")
  textoOrigen = Replace(textoOrigen, "@", "")
  textoOrigen = Replace(textoOrigen, "/", "")
  textoOrigen = Replace(textoOrigen, "°", "")
  textoOrigen = Replace(textoOrigen, "~", "")

  ' CONVIERTE EL TEXTO A MINÚSCULAS Y ELIMINA ESPACIOS AL PRINCIPIO Y AL FINAL
  textoOrigen = Trim(LCase(textoOrigen))
  '
  ' AGREGA EL TEXTO AL FINAL DEL TEXTBOX DE DESTINO SIN BORRAR SU CONTENIDO ACTUAL
  txtClaveGlosario.Text = "glo" & id_sigla.Text & txtClaveGlosario.Text & textoOrigen

End

Public Sub btnGuardarGlosario_Click()

  m_siglas.GuardarGlosarioPrimeraVez()

End

Public Sub gvReferenciasEnCurso_Data(Row As Integer, Column As Integer)

  If m_Bibtex.ContenidoBib <> Null And Row >= 0 And Row < m_Bibtex.ContenidoBib.Count Then
    m_Bibtex.ContenidoBib.MoveTo(Row)
    Try gvReferenciasEnCurso.Data.Text = Str(m_Bibtex.ContenidoBib[Column])
  Endif

End

Public Sub gvSiglasEnCurso_Data(Row As Integer, Column As Integer)

  If m_siglas.ContenidoGlo <> Null And Row >= 0 And Row < m_siglas.ContenidoGlo.Count Then
    m_siglas.ContenidoGlo.MoveTo(Row)
    Try gvSiglasEnCurso.Data.Text = Str(m_siglas.ContenidoGlo[Column])
  Endif

End

Public Sub gvReferenciasEnCurso_Click()

  ' CONFIGURAR COMO SE MUESTRAN LOS BOTONES
  BtnNuevoBib.Visible = True
  btnGuardarBib.Visible = False
  btnGuardarCambiosBib.Visible = True
  BtnEliminarBib.Visible = True
  btnGuardarIGUAL.Visible = False

  ' MOSTRAR LOS DATOS EN LOS TEXTBOX
  m_Bibtex.MostrarRefereciasEnTableViewBibtexEnCurso(gvReferenciasEnCurso.Row)

  ' ' Sincronizar el ComboBox con el ID del artículo (solo para revistas)
  ' If TipoProyectoActual = m_Constantes.TIPO_REVISTA_MD Then
  '   m_FuncionesGenericas.ActualizarComboBoxArticuloEnbibtexDesdeId()
  ' Endif

End

Public Sub gvSiglasEnCurso_Click()

  btnNuevoGlosario.Visible = True
  btnGuardarGlosario.Visible = False
  btnGuardarCambiosGlosario.Visible = True
  btnBorrarGlosario.Visible = True

  m_Siglas.MostrarSiglasEnTableViewGlosarioEnCurso(gvSiglasEnCurso.Row)

End

Public Sub btnGuardarCambiosBib_Click()

  m_Bibtex.GuardarCambiosBibtex()

End

Public Sub btnGuardarCambiosGlosario_Click()

  m_Siglas.GuardarCambiosSigla()
  m_Siglas.RefrescarTableViewGlosarioEnCurso()
  m_Siglas.LimpiarTextboxSigla()
  gvSiglasEnCurso.Refresh

End

Public Sub btbBuscarTapita_Click()

  Dim rutaOrigen As String
  Dim rutaDestino As String
  Dim nombreArchivo As String

  Dialog.Title = "Seleccionar archivo"
  Dialog.Filter = ["*.png", ("Archivos png"), ("Todos los archivos")]
  Dialog.AutoExt = True
  Dialog.Path = RutaProyecto & "/media/"

  If Dialog.OpenFile() Then
    Return
  Else
    nombreArchivo = File.Name(Dialog.Path)
    txtImagenTapita.Text = nombreArchivo

    rutaOrigen = Dialog.Path
    rutaDestino = RutaProyecto & "/docs/" & nombreArchivo

    ' VERIFICAR SI ORIGEN Y DESTINO SON EL MISMO ARCHIVO
    If rutaOrigen = rutaDestino Then
      m_Sonido.sonar("Info")
      Message.Info("El archivo ya está en la ubicación correcta")
      Return
    Endif

    ' VERIFICAR SI EL ARCHIVO YA EXISTE EN DESTINO
    If Exist(rutaDestino) Then
      m_Sonido.sonar("Info")
      Message.Info("El archivo ya existe y será sobreescrito")
      Kill rutaDestino
    Endif

    ' COPIAR EL ARCHIVO
    Copy rutaOrigen To rutaDestino
  Endif
Catch
  m_Sonido.sonar("Error")
  Message.Error("Error al procesar el archivo: " & Error.Text)

End

Public Sub ButtonFiltrosLuaPropios_Click()

  Dim rutaFiltros As String = User.Home & "/.gbpublisher/luapropios"
  Dim archivos As String[]

  archivos = Dir(rutaFiltros, "*.lua")

  If archivos.Count = 0 Then
    m_Sonido.sonar("Info")
    Message.Info("No hay filtros propios disponibles en\n<b>" & rutaFiltros & "</b>")
    Return
  Else
    f_FiltrosLua.ShowModal()
  Endif

End

Public Sub txtlibrocaporden_KeyPress()

  Dim texto As String = txtlibrocaporden.Text
  Dim posicion As Integer = txtlibrocaporden.Pos
  Dim tecla As String = Key.Text

  ' PERMITIR TECLAS DE CONTROL
  If Key.Code = Key.BackSpace Or Key.Code = Key.Delete Or Key.Code = Key.Left Or Key.Code = Key.Right Then Return
  If Key.Code >= Key.F1 Then Return  ' Teclas de función

  ' LIMITAR LONGITUD MÁXIMA (3 DÍGITOS)
  If Len(texto) >= 3 And Key.Code <> Key.BackSpace Then
    Stop Event
    Return
  Endif

  ' SOLO PERMITIR NÚMEROS (0-9)
  If tecla < "0" Or tecla > "9" Then
    Stop Event
    Return
  Endif

  ' VERIFICAR QUE EL NÚMERO RESULTANTE NO EXCEDA 999
  Dim textoNuevo As String
  Dim valorNumerico As Integer

  ' CONSTRUIR EL TEXTO QUE RESULTARÍA DESPUÉS DE LA PULSACIÓN
  textoNuevo = Left(texto, posicion) & tecla & Mid(texto, posicion + 1)
  valorNumerico = CInt(textoNuevo)
  If valorNumerico > 999 Then
    Stop Event
    Return
  Endif

Finally
  If Error Then
    Stop Event
    Return
  Endif

End

Public Sub txtlibrocaporden_LostFocus()

  Dim texto As String = txtlibrocaporden.Text
  Dim valorNumerico As Integer

  ' SI ESTÁ VACÍO, NO HACER NADA
  If Len(texto) = 0 Then Return

  ' CONVERTIR A NÚMERO Y VERIFICAR QUE NO SEA 0
  valorNumerico = CInt(texto)

  If valorNumerico = 0 Then
    ' SI ES 000 O CUALQUIER VARIANTE DE CERO, LIMPIAR EL CAMPO
    txtlibrocaporden.Text = ""
    m_Sonido.sonar("Warning")
    Message.Warning("El valor 000 no está permitido")
    txtlibrocaporden.SetFocus()
    Return
  Endif

  ' COMPLETAR CON CEROS A LA IZQUIERDA PARA QUE TENGA 3 DÍGITOS
  txtlibrocaporden.Text = Format(valorNumerico, "000")

Finally
  If Error Then
    txtlibrocaporden.Text = ""
    m_Sonido.sonar("Warning")
    Message.Warning("Valor inválido")
  Endif

End

Public Sub ButtonGoogleAnalytics_Click()

  ' ABRIR LA BÚSQUEDA EN EL NAVEGADOR PREDETERMINADO
  Desktop.Open("https://analytics.google.com/")

End

Public Sub btnVerificarDOIdatos_Click()

  m_FuncionesGenericas.VerificarDOI(txtDatosDoi)

End

Public Sub btnVerificarDoiLibro_Click()

  m_FuncionesGenericas.VerificarDOI(txtRevistaDoi)

End

Public Sub ComboBoxJournal_Click()

  If ComboBoxJournal.Index >= 0 Then
    txtJournalTitle.Text = ComboBoxJournal.Text
    ComboBoxJournal.Visible = False
  Endif

End

Public Sub txtJournalTitle_LostFocus()

  TimerJournal.Delay = 100
  TimerJournal.Start()

  Dim titulo As String = Trim(txtJournalTitle.Text)

  If titulo = "" Then
    txtJournalTitle.Background = Color.White
    Return
  Endif

  Dim posicion As Integer
  Dim encontradoAmpersand As Boolean = False

  For posicion = 1 To Len(titulo)
    If Mid(titulo, posicion, 1) = "&" Then
      If posicion = 1 Then
        encontradoAmpersand = True
        Exit
      Else
        If Mid(titulo, posicion - 1, 1) <> "\\" Then
          encontradoAmpersand = True
          Exit
        Endif
      Endif
    Endif
  Next

  If encontradoAmpersand Then
    txtJournalTitle.Background = Color.Pink
    m_Sonido.sonar("Warning")
    Message.Warning("El carácter '&' debe escaparse como '\\&' para biblatex, de lo contrario la compilación fallará")
    txtJournalTitle.SetFocus()
  Else
    txtJournalTitle.Background = Color.White
  Endif

End

Public Sub menuGlosarioImportarJson_Click()

  m_Json.ImportarJSONsiglas()

  Wait 1

  m_Siglas.RefrescarTableViewGlosarioEnCurso()

End

Public Sub menuGlosarioExportarJson_Click()

  m_Json.ExportarJSONsiglas()

End

Public Sub importarJSONbibtex_Click()

  Dim resultado As Boolean
  Dim respuesta As Integer

  ' Confirmar acción
  respuesta = Message.Question("¿Desea importar entradas desde un archivo JSON?\n\nEsto agregará nuevas entradas al proyecto actual.", "Sí", "No")

  If respuesta = 1 Then  ' Usuario eligió "Sí"
    resultado = m_Json.ImportarJSON()

    If resultado Then
      m_Sonido.sonar("Info")
      Message.Info("Entradas importadas exitosamente")
      m_Bibtex.RefrescarTableViewBibtexEnCurso()
    Else
      m_Sonido.sonar("Error")
      Message.Error("No se pudieron importar las entradas")
    Endif
  Endif

End

Public Sub ButtonRepositorioGithub_Click()

  Dim sTITULO As String = Trim(txtRepositorioGithub.Text)

  If sTITULO = "" Then
    m_Sonido.sonar("Error")
    Message.Error("Por favor, introduce la url de un repositorio.")
    Return
  Endif

  Desktop.Open(sTITULO)

End

Public Sub btnAbrirTerminal_Click()

  Dim sText As String = RutaProyecto

  If Trim(sText) = "" Then
    m_Sonido.sonar("Warning")
    Message.Warning("No tiene ningún proyecto abierto.")
    Return
  Endif

  If Exist(sText) Then
    ' ABRIR TERMINAL POR DEFECTO EN LA RUTA DEL PROYECTO
    Shell "kitty --working-directory " "" & sText & "" " &"
  Endif

End

Public Sub AllTextBoxes_LostFocus()

  Dim sText As String = Trim(Last.Text)

  Select Case Last.Name
    Case "txtAuthor", "txtBookAuthor", "txtEditor"
      If sText = "" Then
        Last.Background = Color.White
        Return
      Endif

      ' ERRORES GRAVES QUE BLOQUEAN
      If InStr(sText, ";") > 0 Or InStr(sText, ":") > 0 Or InStr(sText, "&") > 0 Then
        Last.Background = Color.Pink
        m_Sonido.sonar("Warning")
        Message.Warning("Se han encontrado caracteres prohibidos en este campo.")
        Last.SetFocus()
        Return
      Endif

      ' ADVERTENCIAS QUE SE PUEDEN NORMALIZAR (NO BLOQUEAN)
      If InStr(sText, " y ") > 0 Then
        Last.Background = Color.Pink
      Else
        Last.Background = Color.White
      Endif

    Case Else
  End Select

End

Public Sub btnImportarCSV_Click()

  With Dialog
    .Title = "Seleccionar archivo de CSV"
    .Filter = ["*.csv", "Archivos CSV", "*", "Todos los archivos"]
    .Path = User.Home
  End With
  If Dialog.OpenFile() Then Return
  Return

End

Public Sub ButtonLogin_Click()

  m_Sonido.sonar("OpenApp")
  f_Login.ShowModal()

End

Public Sub ButtonVisitarProyecto_Click()

  Dim sURL As String

  sURL = "https://albertomoyano.github.io/gbpublisher/"

  Desktop.Open(sURL)

End

Public Sub ButtonBlockNotas_Click()

  m_Sonido.sonar("OpenApp")
  f_BlockNotas.ShowModal()

End

Public Sub ButtonNotitas_Click()

  ' VOLVER A HABILITAR EL TIMER SI ESTÁ DETENIDO
  If Not TimerNotitas.Enabled Then
    TimerNotitas.Start()
  Endif

  m_Sonido.sonar("OpenApp")
  f_NotaNueva.Show

End

Public Sub ButtonInfoPC_Click()

  m_Sonido.sonar("Info")
  Dim sInfo As String
  Dim iResult As Integer

  ' INFORMACIÓN BÁSICA DE LA APLICACIÓN
  sInfo = "gbpublisher versión " & Application.Version & "\n"
  sInfo &= "Desarrollada por Alberto Moyano\n"
  sInfo &= "Linkedin: https://www.linkedin.com/in/edicion-cientifica/\n"
  sInfo &= String(50, "-") & "\n\n"

  ' INFORMACIÓN DEL SISTEMA OPERATIVO
  sInfo &= "<b>Información del sistema</b>\n"
  sInfo &= "Motor gráfico: " & Desktop.Platform & "\n"
  sInfo &= "Entorno gráfico: " & Desktop.Type & "\n"
  sInfo &= "Nombre del equipo: " & System.Host & "\n\n"

  ' INFORMACIÓN DE GAMBAS
  sInfo &= "<b>Información de Gambas</b>\n"
  sInfo &= "Versión de Gambas: " & System.FullVersion & "\n"
  sInfo &= "Codificación del sistema: " & System.Charset & "\n\n"

  ' INFORMACIÓN DE HARDWARE
  sInfo &= "<b>Información del hardware</b>\n"
  sInfo &= "Arquitectura: " & System.Architecture & "\n"

  ' INFORMACIÓN DE CPU (SI ESTÁ DISPONIBLE)
  If Exist("/proc/cpuinfo") Then
    sInfo &= "Procesador: " & GetCPUInfo() & "\n"
  Endif

  ' INFORMACIÓN DE MEMORIA
  sInfo &= "Memoria total: " & GetMemoryInfo() & "\n"

  ' INFORMACIÓN DE PANTALLA
  sInfo &= "Resolución de pantalla: " & Desktop.W & "x" & Desktop.H & "\n"

  ' MOSTRAR LA INFORMACIÓN Y CAPTURAR LA RESPUESTA
  iResult = Message.Question(sInfo, "Visitar perfil", "Copiar info", "Cerrar")

  Select Case iResult
    Case 1  ' VISITAR PERFIL
      Desktop.Open("https://www.linkedin.com/in/edicion-cientifica/")
    Case 2  ' COPIAR INFO
      Clipboard.Copy(sInfo)
      Message.Info("Información copiada al portapapeles")
  End Select

End

Public Sub ButtonRutaApp_Click()

  Dim sText As String = rutaApp

  If Exist(sText) Then
    m_Sonido.sonar("OpenApp")
    Desktop.Open(sText)
  Endif

End

Public Sub ButtonConsultasSQL_Click()

  m_Sonido.sonar("OpenApp")
  f_Estadisticas.ShowModal()

End

Public Sub ButtonAdminRepositorios_Click()

  m_Sonido.sonar("OpenApp")
  f_AdminGit.ShowModal()

End

Public Sub ButtonCorregirBib_Click()

  m_Sonido.sonar("OpenApp")
  f_CorregirBBDD.ShowModal()

End

Public Sub ButtonSuperUsuario_Click()

  m_Sonido.sonar("OpenApp")
  f_SysAdmin.ShowModal()

End

' FUNCIÓN AUXILIAR PARA OBTENER LA RUTA RELATIVA
Private Function ObtenerRutaRelativa(RutaCompleta As String, RutaBase As String) As String

  ' NORMALIZAR LAS RUTAS (ELIMINAR BARRAS FINALES)
  If RutaBase Ends "/" Then RutaBase = Left(RutaBase, -1)
  If RutaCompleta Ends "/" Then RutaCompleta = Left(RutaCompleta, -1)

  ' SI LA RUTA COMPLETA EMPIEZA CON LA RUTA BASE, DEVOLVER SOLO LA PARTE RELATIVA
  If RutaCompleta Begins RutaBase Then
    Return Mid(RutaCompleta, Len(RutaBase) + 2) ' +2 para saltar la barra "/"
  Else
    ' SI NO ESTÁ DENTRO DEL PROYECTO, DEVOLVER LA RUTA COMPLETA
    Return RutaCompleta
  Endif

End

Public Sub MenuProyectoParcial_Click()

  AbrirFormularioModoMedia()

End

Public Sub ButtonCerrarApp_Click()

  m_ConexionBD.CerrarTodo()

End

Public Sub ButtonInfoApp_Click()

  m_Sonido.sonar("OpenApp")
  Dim frmLegales As New F_AvisoLegal

  frmLegales.EsFormularioInicial = False
  frmLegales.ShowModal()

End

Public Sub ButtonAutores_Click()

  Dim frmAutores As New F_Autores

  m_Sonido.sonar("OpenApp")
  frmAutores.EsFormularioOriginal = False
  frmAutores.ShowModal()

End

Public Sub btnRefrescarFileView_Click()

  refrecarViewFile()

End

Public Sub TimerNotitas_Timer()

  Dim sUsuario As String = m_InicioCierre.UsuarioEnCurso
  Dim hRs As Result
  Dim sSQL As String

  ' Verificar conexión
  If Not m_ConexionBD.mConn Then Return
  If Not m_ConexionBD.mConn.Opened Then Return

  ' BUSCAR NOTAS DESTINADAS AL USUARIO ACTUAL
  sSQL = "SELECT * FROM postits WHERE para = '" & sUsuario & "' AND oculto = 0 AND leido = 0 ORDER BY fecha DESC"
  hRs = m_ConexionBD.mConn.Exec(sSQL)

  ' SI HAY NOTAS DISPONIBLES, MOSTRAR FORMULARIO DE NOTAS
  If hRs.Available Then
    f_NotasRecibidas.CargarNota(hRs)
    m_Sonido.sonar("Info")
    f_NotasRecibidas.Show
  Endif

End

Public Sub AbrirFormularioModoMedia()

  Dim rs As Result
  Dim sSQL As String

  ' VERIFICAR SI HAY PROYECTOS ACTIVOS ANTES DE ABRIR EL FORMULARIO
  sSQL = "SELECT COUNT(*) as total FROM proyectos WHERE activo = 1"
  rs = m_ConexionBD.mConn.Exec(sSQL)

  If rs["total"] = 0 Then
    Message.Info("No hay proyectos activos")
    Return
  End If

  f_ProyectosParcial.ShowModal()

Catch
  Message.Error("Error al verificar proyectos: " & Error.Text)

End

Public Sub ButtonVerFunding_Click()

  f_TXTextendido.OriginalTextBox = txtFundingStatement
  f_TXTextendido.ShowModal

End

Public Sub btnGuardarCambiosOT_Click()

  m_OrdenTaller.GuardarOrdenTaller()

End

Public Sub btnBuscarCabecera_Click()

  Dialog.Title = "Seleccionar archivo"
  Dialog.Filter = ["*.png", ("Archivos png"), ("Todos los archivos")]
  Dialog.AutoExt = True
  Dialog.Path = RutaProyecto & "/media/"
  If Dialog.OpenFile() Then
    Return
  Else
    txtCabeceraOT.Text = File.Name(Dialog.Path)
  Endif
Catch
  m_Sonido.sonar("Error")
  Message.Error("No se pudo abrir el archivo")

End

Public Sub btnLimpiarCabecera_Click()

  txtCabeceraOT.Clear

End

Public Sub btnGenerarSalidaPDF_Click()

  m_OrdenTaller.GenerarSalidaPDF()

End

Public Sub btnAgregarAutorListado_Click()

  f_Autores.ShowModal()

End

Public Sub menuODT2TEX_Click()

  f_ConversorODT2TeX.ShowModal()

End

Public Sub menuDOCX2MD_Click()

  f_ConversorDOCX2MD.ShowModal()

End

Public Sub menuDOCX2TEX_Click()

  f_ConversorDOCX2TeX.ShowModal()

End

Public Sub menuODT2MD_Click()

  f_ConversorODT2MD.ShowModal()

End

Public Sub txtHyphenation_GotFocus()

  If Trim(txtPublisher.Text) <> "" Then
    m_FuncionesGenericas.BuscarHyphenationDesdeTexto()
  Endif

End

Public Sub txtHyphenation_KeyPress()

  If Key.Code = Key.Enter Or Key.Code = Key.Return Then
    m_FuncionesGenericas.BuscarHyphenationDesdeTexto()
  Endif

End

Public Sub txtHyphenation_LostFocus()

  TimerHyphenation.Delay = 100
  TimerHyphenation.Start()

End

Public Sub cmbHyphenation_Click()

  If cmbHyphenation.Index >= 0 Then
    txtHyphenation.Text = cmbHyphenation.Text
    cmbHyphenation.Visible = False
  Endif

End

Public Sub cmbPubState_Click()

  Dim sCodigo As String
  Dim mapa As Collection

  sCodigo = Trim(FMain.cmbPubState.Text)
  mapa = m_Bibtex.mMapaPubState

  If sCodigo = "" Then
    FMain.TextBoxPubState.Text = ""
    Return
  End If

  If mapa And If mapa.Exist(sCodigo) Then
    FMain.TextBoxPubState.Text = mapa[sCodigo]
  Else
    FMain.TextBoxPubState.Text = ""
  End If

End

Public Sub cmbPagination_Click()

  Dim sCodigo As String
  Dim mapa As Collection

  sCodigo = Trim(FMain.cmbPagination.Text)
  mapa = m_Bibtex.mPagination

  If sCodigo = "" Then
    FMain.TextBoxPagination.Text = ""
    Return
  End If

  If mapa And If mapa.Exist(sCodigo) Then
    FMain.TextBoxPagination.Text = mapa[sCodigo]
  Else
    FMain.TextBoxPagination.Text = ""
  End If

End

Public Sub cmbBookPagination_Click()

  Dim sCodigo As String
  Dim mapa As Collection

  sCodigo = Trim(FMain.cmbBookPagination.Text)
  mapa = m_Bibtex.mPagination

  If sCodigo = "" Then
    FMain.TextBoxBookPagination.Text = ""
    Return
  End If

  If mapa And If mapa.Exist(sCodigo) Then
    FMain.TextBoxBookPagination.Text = mapa[sCodigo]
  Else
    FMain.TextBoxBookPagination.Text = ""
  End If

End

Public Sub cmbEprintType_Click()

  Dim sCodigo As String
  Dim mapa As Collection

  sCodigo = Trim(FMain.cmbEprintType.Text)
  mapa = m_Bibtex.mEprint

  If sCodigo = "" Then
    FMain.txtEprintType.Text = ""
    Return
  End If

  If mapa And If mapa.Exist(sCodigo) Then
    FMain.txtEprintType.Text = mapa[sCodigo]
  Else
    FMain.txtEprintType.Text = ""
  End If

End

Public Sub cmbRelatedType_Click()

  Dim sCodigo As String
  Dim mapa As Collection

  sCodigo = Trim(FMain.cmbRelatedType.Text)
  mapa = m_Bibtex.mRelatedType

  If sCodigo = "" Then
    FMain.txtRelatedType.Text = ""
    Return
  End If

  If mapa And If mapa.Exist(sCodigo) Then
    FMain.txtRelatedType.Text = mapa[sCodigo]
  Else
    FMain.txtRelatedType.Text = ""
  End If

End

Public Sub cmbTipoDeTesis_Click()

  Dim sCodigo As String
  Dim mapa As Collection

  sCodigo = Trim(FMain.cmbTipoDeTesis.Text)
  mapa = m_Bibtex.mTipoDeTesis

  If sCodigo = "" Then
    FMain.txtTipoDeTesis.Text = ""
    Return
  End If

  If mapa And If mapa.Exist(sCodigo) Then
    FMain.txtTipoDeTesis.Text = mapa[sCodigo]
  Else
    FMain.txtTipoDeTesis.Text = ""
  End If

End

Public Sub cmbTipoDeEntrada_Click()

  Dim sCodigo As String
  Dim mapa As Collection

  sCodigo = Trim(FMain.cmbTipoDeEntrada.Text)
  mapa = m_Bibtex.mTipoDeEntrada

  If sCodigo = "" Then
    FMain.txtTipoDeEntrada.Text = ""
    Return
  End If

  If mapa And If mapa.Exist(sCodigo) Then
    FMain.txtTipoDeEntrada.Text = mapa[sCodigo]
  Else
    FMain.txtTipoDeEntrada.Text = ""
    FMain.txtTipoDeTesis.Text = ""
    FMain.HBoxTipoDeTesis.Visible = False
  End If

  If FMain.cmbTipoDeEntrada.Text = "thesis" Then
    FMain.HBoxTipoDeTesis.Visible = True
  Else
    FMain.HBoxTipoDeTesis.Visible = False
    FMain.cmbTipoDeTesis.Text = ""
    FMain.txtTipoDeTesis.Text = ""
  Endif

End

' DETECTA CTRL + F1
Public Sub Form_KeyRelease()

  If Key.Control And Key.Code = Key.F1 Then
    MostrarAyuda()
  Endif

End

Private Sub MostrarAyuda()

  Dim ctrl As Object
  Dim nombreControl As String
  Dim r As Result
  Dim textoAyuda As String
  Dim claveAyuda As String
  Dim fAyuda As New F_Ayudas

  ctrl = Application.ActiveControl
  If ctrl = Null Then Return

  nombreControl = ctrl.Name

  ' BUSCAR AYUDA Y CLAVE DEL CONTROL ACTUAL
  r = m_ConexionBD.mConn.Exec("SELECT clave, ayuda FROM manual_ayudas WHERE componente = &1 LIMIT 1;", nombreControl)

  If r.Available Then
    textoAyuda = r!ayuda
    claveAyuda = r!clave

    ' Verificar si el campo ayuda tiene contenido
    If Not textoAyuda Or Trim(textoAyuda) = "" Then
      m_Sonido.sonar("Info")
      Message.Info("No hay ayuda disponible para este campo.")
      Return
    Endif
  Else
    m_Sonido.sonar("Info")
    Message.Info("No hay ayuda disponible para este campo.")
    Return
  Endif

  ' MOSTRAR LA AYUDA EN UN FORMULARIO MODAL CON LA CLAVE
  fAyuda.MostrarTexto(nombreControl, textoAyuda, claveAyuda)
  fAyuda.ShowModal()

End

Private Sub RespaldarBaseDatos()

  Dim sHost As String
  Dim sUsuario As String
  Dim sPassword As String
  Dim sBaseDatos As String = "gbpublisher"
  Dim sFecha As String
  Dim sRutaRespaldo As String
  Dim sArchivoRespaldo As String
  Dim sComando As String
  Dim hConexion As Connection

  ' CONFIGURAR PARÁMETROS DE CONEXIÓN
  sHost = "localhost"
  sUsuario = "app_user"
  sPassword = "AppUser2024!"

  ' SOLO HACER RESPALDO SI LA BD ES LOCAL (LOCALHOST O 127.0.0.1)
  If sHost <> "localhost" And sHost <> "127.0.0.1" Then
    Return
  Endif

  ' VERIFICAR SI LA BASE DE DATOS EXISTE
  hConexion = New Connection
  hConexion.Type = "mysql"
  hConexion.Host = sHost
  hConexion.Login = sUsuario
  hConexion.Password = sPassword
  hConexion.Name = sBaseDatos

  Try hConexion.Open()

  If Error Then
    Return
  Endif

  ' SI LLEGAMOS AQUÍ, LA BASE EXISTE
  hConexion.Close()

  ' OBTENER FECHA EN FORMATO YYMMDD
  sFecha = Format(Now, "yymmdd")

  ' CONSTRUIR RUTA DEL ARCHIVO DE RESPALDO
  sRutaRespaldo = User.Home &/ ".gbpublisher/respaldo"
  sArchivoRespaldo = sRutaRespaldo &/ "gbpublisher_" & sFecha & ".sql"

  ' CONSTRUIR COMANDO MYSQLDUMP
  sComando = "mysqldump -h " & sHost & " -u " & sUsuario

  If sPassword <> "" Then
    sComando &= " -p" & sPassword
  Endif

  ' OPCIONES PARA EVITAR ERRORES DE PERMISOS
  sComando &= " --no-tablespaces --skip-lock-tables"
  sComando &= " " & sBaseDatos & " > " & Shell(sArchivoRespaldo)

  ' EJECUTAR RESPALDO
  Try Shell sComando Wait

End

Public Sub exportarBibTeX_Click()

  Dim resultado As Boolean

  ' NOS REASEGURAMOS DE LA RUTA EN LA TERMINAL
  TerminalViewProyecto.Input("cd " & RutaProyecto & "\n")
  TerminalViewProyecto.Input("clear" & "\n")

  resultado = m_Bibtex.ExportarBibTeX()

  If resultado Then
    m_Sonido.sonar("Info")
    Message.Info("Archivo BibTeX exportado exitosamente")
  Else
    m_Sonido.sonar("Error")
    Message.Error("Error al exportar el archivo BibTeX")
  Endif

End

Public Sub exportarBibTeX2JSON_Click()

  Dim resultado As Boolean

  resultado = m_Json.ExportarBibTeX2JSON()

  If resultado Then
    m_Sonido.sonar("Info")
    Message.Info("Archivo JSON exportado exitosamente")
  Else
    m_Sonido.sonar("Error")
    Message.Error("Error al exportar el archivo JSON")
  Endif

End

Public Sub menuXMLbase_Click()

  Message.Info("XML-JATS maestro de las restantes salidas.")

End

Public Sub btnLimpiarImagenTapa_Click()

  txtImagenTapaOT.Clear

End

Public Sub btnBuscarImagenTapa_Click()

  Dialog.Title = "Seleccionar archivo"
  Dialog.Filter = ["*.png", ("Archivos png"), ("Todos los archivos")]
  Dialog.AutoExt = True
  Dialog.Path = RutaProyecto & "/media/"
  If Dialog.OpenFile() Then
    Return
  Else
    txtImagenTapaOT.Text = File.Name(Dialog.Path)
  Endif
Catch
  m_Sonido.sonar("Error")
  Message.Error("No se pudo abrir el archivo")

End

Public Sub btnBuscarBookTitleEnGoogle_Click()

  Dim sTITULO As String = Trim(txtBookTitle.Text)

  ' VERIFICAR QUE NO ESTÉ VACÍO
  If sTITULO = "" Then
    m_Sonido.sonar("Error")
    Message.Error("Por favor, introduce un título a buscar.")
    Return
  Endif

  ' ABRIR LA BÚSQUEDA EN EL NAVEGADOR PREDETERMINADO
  Desktop.Open("https://www.google.com/search?q=" & sTITULO)

End

Public Sub exportarCSLJSON_Click()

  Dim resultado As Boolean

  resultado = m_Json.ExportarCSLJSON()

  If resultado Then
    m_Sonido.sonar("Info")
    Message.Info("Archivo CSL-JSON exportado exitosamente")
  Else
    m_Sonido.sonar("Error")
    Message.Error("Error al exportar el archivo CSL-JSON")
  Endif

End

Public Sub menuEstadisticas_Click()

  m_InicioCierre.AnalizarMD()

End

Public Sub menuVerificarFootnote_Click()

  m_FuncionesGenericas.VerificarFootnotes()

End

Public Sub mINFOcomentario_Click()

  m_FuncionesGenericas.InsertarFechaHoraComentario("INFO")

End

Public Sub mFIXMEcomentario_Click()

  m_FuncionesGenericas.InsertarFechaHoraComentario("FIXME")

End

Public Sub mERRORcomentario_Click()

  m_FuncionesGenericas.InsertarFechaHoraComentario("ERROR")

End

Public Sub btnBold_Click()

  Dim sTextoSeleccionado As String
  Dim iInicio As Integer
  Dim iLongitud As Integer
  Dim sNuevoTexto As String

  ' VALIDACIÓN 1: Verificar que hay texto seleccionado
  If Not txtEditorProyecto.SelectedText
    m_Sonido.sonar("Warning")
    Message.Warning("Debe seleccionar texto para aplicar formato Bold.")
    Return
  Endif

  ' Obtener el texto seleccionado
  sTextoSeleccionado = txtEditorProyecto.SelectedText

  ' VALIDACIÓN 2: Verificar que no hay líneas vacías en la selección
  If InStr(sTextoSeleccionado, "\n\n") > 0 Or
      InStr(sTextoSeleccionado, "\r\n\r\n") > 0 Then
    m_Sonido.sonar("Warning")
    Message.Warning("La selección contiene líneas vacías." & gb.NewLine &
      "No se puede aplicar formato Bold a través de párrafos separados.")
    Return
  Endif

  ' GUARDAR POSICIÓN INICIAL DE LA SELECCIÓN
  iInicio = txtEditorProyecto.Column
  iLongitud = Len(sTextoSeleccionado)

  ' APLICAR FORMATO BOLD
  sNuevoTexto = "**" & sTextoSeleccionado & "**"

  ' INSERTAR EL TEXTO FORMATEADO
  txtEditorProyecto.Insert(sNuevoTexto)

End

Public Sub btnItalic_Click()

  Dim sTextoSeleccionado As String
  Dim iInicio As Integer
  Dim iLongitud As Integer
  Dim sNuevoTexto As String

  ' VALIDACIÓN 1: VERIFICAR QUE HAY TEXTO SELECCIONADO
  If Not txtEditorProyecto.SelectedText
    m_Sonido.sonar("Warning")
    Message.Warning("Debe seleccionar texto para aplicar formato Italic.")
    Return
  Endif

  ' OBTENER EL TEXTO SELECCIONADO
  sTextoSeleccionado = txtEditorProyecto.SelectedText

  ' VALIDACIÓN 2: VERIFICAR QUE NO HAY LÍNEAS VACÍAS EN LA SELECCIÓN
  If InStr(sTextoSeleccionado, "\n\n") > 0 Or
      InStr(sTextoSeleccionado, "\r\n\r\n") > 0 Then
    m_Sonido.sonar("Warning")
    Message.Warning("La selección contiene líneas vacías." & gb.NewLine &
      "No se puede aplicar formato Italic a través de párrafos separados.")
    Return
  Endif

  ' GUARDAR POSICIÓN INICIAL DE LA SELECCIÓN
  iInicio = txtEditorProyecto.Column
  iLongitud = Len(sTextoSeleccionado)

  ' APLICAR FORMATO ITALIC
  sNuevoTexto = "*" & sTextoSeleccionado & "*"

  ' INSERTAR EL TEXTO FORMATEADO
  txtEditorProyecto.Insert(sNuevoTexto)

End

Public Sub btnFootnote_Click()

  Dim iNumeroFootnote As Integer
  Dim sMarcaFootnote As String
  Dim sDefinicionFootnote As String
  Dim iLineaOriginal As Integer
  Dim iColumnaOriginal As Integer
  Dim iUltimaLinea As Integer
  Dim sUltimaLinea As String

  ' GUARDAR POSICIÓN ORIGINAL DEL CURSOR
  iLineaOriginal = txtEditorProyecto.Line
  iColumnaOriginal = txtEditorProyecto.Column

  ' Obtener el texto completo del editor
  Dim sTextoCompleto As String = txtEditorProyecto.Text

  ' CALCULAR EL SIGUIENTE NÚMERO DE FOOTNOTE
  iNumeroFootnote = ContarFootnotes(sTextoCompleto) + 1

  ' CREAR LA MARCA Y LA DEFINICIÓN
  sMarcaFootnote = "[^" & CStr(iNumeroFootnote) & "]"
  sDefinicionFootnote = "[^" & CStr(iNumeroFootnote) & "]: Texto del footnote aquí"

  ' INSERTAR LA MARCA EN LA POSICIÓN ACTUAL DEL CURSOR
  txtEditorProyecto.Insert(sMarcaFootnote)

  ' AHORA IR AL FINAL ABSOLUTO DEL ARCHIVO
  txtEditorProyecto.Text = txtEditorProyecto.Text & "\n\n" & sDefinicionFootnote

End

' FUNCIÓN AUXILIAR PARA CONTAR FOOTNOTES EXISTENTES
Private Function ContarFootnotes(sTexto As String) As Integer

  Dim iContador As Integer
  Dim iPos As Integer

  iContador = 0
  iPos = 1

  ' BUSCAR TODAS LAS OCURRENCIAS DE [^N]: (SOLO LAS DEFINICIONES)
  While True
    iPos = InStr(sTexto, "[^", iPos)
    If iPos = 0 Then Break

    ' BUSCAR EL CIERRE ] Y VERIFICAR SI TIENE :
    Dim iPosCierre As Integer = InStr(sTexto, "]", iPos)
    If iPosCierre > 0 Then
      ' VERIFICAR SI DESPUÉS DEL ] HAY UN :
      If iPosCierre < Len(sTexto) Then
        If Mid(sTexto, iPosCierre + 1, 1) = ":" Then
          iContador += 1
        Endif
      Endif
    Endif

    iPos += 2
  Wend

  Return iContador

End

Public Sub btnCrearCuadro_Click()

  Dim f_Tabla As New FTabla
  Dim sTabla As String

  sTabla = f_Tabla.MarkDownTable

  If sTabla <> "" Then
    txtEditorProyecto.Insert(sTabla)
  Endif

End

Public Sub btnMostrarTodas_Click()

  ' Primero verificar si hay resultados suficientes
  If vConteo.Value > 1 Then
    ' Si hay resultados, el estado del botón controla la visibilidad
    TabPanelIzquierdaInferior[6].Visible = btnMostrarTodas.Value

    If btnMostrarTodas.Value Then
      ' Si se activa el botón, mostrar la solapa
      TabPanelIzquierdaInferior.Index = 6
    Endif

  Else
    ' Si no hay suficientes resultados
    ' Solo mostrar mensaje si el botón estaba activado (intentando activarse)
    If btnMostrarTodas.Value = True Then
      m_Sonido.sonar("Info")
      Message.Info("El resultado de la búsqueda debe ser mayor a 2.")
    Endif

    btnMostrarTodas.Value = False  ' Desactivar el botón
    TabPanelIzquierdaInferior[6].Visible = False
  Endif

End

Public Sub btnMostrarBuscar_Click()

  If btnMostrarBuscar.Value Then
    HBoxBuscar.Visible = True
  Else
    HBoxBuscar.Visible = False
  Endif

End

Public Sub btnShortCodes_Click()

  FShortcodes.Show()
  FShortcodes.TopOnly = True

End

Public Sub menuTraducir_Click()

  m_Traduccion.MostrarFormulario(txtEditorProyecto.SelectedText)

End

Public Sub btnDiccionario_Click()

  FDiccionario.ShowModal()

End

Public Sub menuDiccionario_Click()

  Dim sPalabraSeleccionada As String

  ' Obtener el texto seleccionado del TextEditor
  sPalabraSeleccionada = Trim(txtEditorProyecto.SelectedText)

  ' Si no hay selección, mostrar error y salir
  If sPalabraSeleccionada = "" Then
    Message.Info("Debe seleccionar una palabra primero.")
    Return
  Endif

  ' Pasar la palabra al formulario y mostrarlo
  FDiccionario.txtPalabra.Text = sPalabraSeleccionada
  FDiccionario.ShowModal()

End

Public Sub btnOrtografia_Click()

  FOrtografia.ShowModal()

End

' MOSTRAMOS EL PANEL INFERIOR IZQUIERDO
Public Sub tbMostrarPanelInferior_Click()

  If tbMostrarPanelInferior.Value Then
    TabPanelIzquierdaInferior.Visible = True
  Else
    TabPanelIzquierdaInferior.Visible = False
  Endif

End

Public Sub menuEstructuraMD_Click()

  m_FuncionesGenericas.VerificarEstructuraMD()

End

Public Sub menuContarCoincidencias_Click()

  m_FuncionesGenericas.ContarCaracteresPares()

End

'------------------------------------------------------------------------------
' btnUpperCase_Click
'------------------------------------------------------------------------------
' Convierte el texto seleccionado en el editor a MAYÚSCULAS.
'
' Validaciones:
'   - Verifica que exista texto seleccionado antes de proceder.
'
' Comportamiento:
'   - Obtiene el texto seleccionado del editor.
'   - Lo convierte a mayúsculas usando String.UCase() para soporte UTF-8.
'   - Reemplaza la selección con el texto transformado.
'
' Notas técnicas:
'   - Se usa String.UCase() en lugar de UCase() porque la función nativa
'     solo maneja ASCII (7 bits). String.UCase() es el método de la clase
'     estática String que soporta caracteres UTF-8 como acentos (á, é, í, ó, ú),
'     eñes (ñ) y otros caracteres multibyte.
'   - Referencia: https://gambaswiki.org/wiki/comp/gb/string/ucase
'
' Dependencias:
'   - txtEditorProyecto: TextArea del editor principal.
'   - m_Sonido: Módulo para reproducir sonidos de feedback.
'------------------------------------------------------------------------------
Public Sub btnUpperCase_Click()

  Dim sTextoSeleccionado As String
  Dim sNuevoTexto As String

  ' VALIDACIÓN: VERIFICAR QUE HAY TEXTO SELECCIONADO
  If Not txtEditorProyecto.SelectedText Then
    m_Sonido.sonar("Warning")
    Message.Warning("Debe seleccionar texto para convertir a mayúsculas.")
    Return
  Endif

  ' OBTENER EL TEXTO SELECCIONADO
  sTextoSeleccionado = txtEditorProyecto.SelectedText

  ' CONVERTIR A MAYÚSCULAS (UTF-8)
  ' String.UCase() maneja correctamente caracteres acentuados y ñ
  sNuevoTexto = String.UCase(sTextoSeleccionado)

  ' INSERTAR EL TEXTO TRANSFORMADO
  txtEditorProyecto.Insert(sNuevoTexto)

End

Public Sub btnConvertirMinusculas_Click()

  Dim sTextoSeleccionado As String
  Dim sNuevoTexto As String

  ' VALIDACIÓN: VERIFICAR QUE HAY TEXTO SELECCIONADO
  If Not txtEditorProyecto.SelectedText Then
    m_Sonido.sonar("Warning")
    Message.Warning("Debe seleccionar texto para convertir a minúsculas.")
    Return
  Endif

  ' OBTENER EL TEXTO SELECCIONADO
  sTextoSeleccionado = txtEditorProyecto.SelectedText

  ' CONVERTIR A MAYÚSCULAS (UTF-8)
  ' String.LCase() maneja correctamente caracteres acentuados y ñ
  sNuevoTexto = String.LCase(sTextoSeleccionado)

  ' INSERTAR EL TEXTO TRANSFORMADO
  txtEditorProyecto.Insert(sNuevoTexto)

End

Public Sub menuEscanear_Click()

  m_EscanerTipografico.EscanearCodificacionUTF8()

End

Public Sub menuCercania_Click()

  ' LLAMAR A LA FUNCIÓN DEL MÓDULO m_EscanerTipografico
  m_EscanerTipografico.MostrarControlCercania()

End

' ============================================================================
' EVENTO CLICK DEL BOTÓN AGREGAR IMAGEN
' ============================================================================
' INSERTA CÓDIGO MARKDOWN PARA UNA IMAGEN EN EL EDITOR DE TEXTO.
' LA IMAGEN SE SELECCIONA MEDIANTE UN DIÁLOGO Y SE COPIA A LA CARPETA
' /MEDIA DEL PROYECTO SI NO ESTÁ UBICADA AHÍ.
' EL CÓDIGO SE INSERTA ASEGURANDO QUE QUEDE EN UNA LÍNEA NUEVA Y VACÍA,
' CON UNA LÍNEA VACÍA ANTES Y DESPUÉS.
' ============================================================================

Public Sub btnAgregarImagen_Click()

  Dim sRutaImagen As String
  Dim sNombreArchivo As String
  Dim sRutaMedia As String
  Dim sRutaDestino As String
  Dim sCodigoMarkdown As String

  ' VALIDAR QUE EL CURSOR ESTÉ AL INICIO DE UNA LÍNEA VACÍA
  If Not ValidarPosicionCursorImagen() Then
    m_Sonido.sonar("Error")
    Message.Title = "Error de posición."
    Message.Error("No se puede insertar una imagen dentro de una línea o párrafo.\nEl cursor debe estar al inicio de una línea vacía.")
    Return
  Endif

  ' VERIFICAR QUE HAY UN PROYECTO ABIERTO Y OBTENER RUTA DE MEDIA
  ' NOTA: AJUSTAR SEGÚN CÓMO SE ALMACENA LA RUTA DEL PROYECTO EN TU APLICACIÓN
  If Not RutaProyecto Then
    m_Sonido.sonar("Warning")
    Message.Warning("No hay ningún proyecto abierto.")
    Return
  Endif

  sRutaMedia = RutaProyecto &/ "media/"

  ' CREAR CARPETA MEDIA SI NO EXISTE
  If Not Exist(sRutaMedia) Then
    Try Mkdir sRutaMedia
    If Error Then
      m_Sonido.sonar("Error")
      Message.Error("No se pudo crear la carpeta media: " & Error.Text)
      Return
    Endif
  Endif

  ' CONFIGURAR Y MOSTRAR DIÁLOGO DE SELECCIÓN DE IMAGEN
  Dialog.Title = "Seleccionar imagen"
  Dialog.Filter = ["*.png;*.jpg;*.jpeg", "Archivos de imagen (PNG, JPG)", "*", "Todos los archivos"]
  Dialog.Path = sRutaMedia

  ' SI EL USUARIO CANCELA, SALIR
  If Dialog.OpenFile() Then Return

  sRutaImagen = Dialog.Path
  sNombreArchivo = File.Name(sRutaImagen)
  sRutaDestino = sRutaMedia &/ sNombreArchivo

  ' COPIAR IMAGEN A CARPETA MEDIA SOLO SI FUE SELECCIONADA DESDE OTRA UBICACIÓN
  ' SI LA IMAGEN YA ESTÁ EN LA CARPETA MEDIA, NO HACER NADA
  If File.Dir(sRutaImagen) & "/" <> sRutaMedia Then
    ' LA IMAGEN VIENE DE OTRA UBICACIÓN, VERIFICAR SI YA EXISTE EN MEDIA
    If Exist(sRutaDestino) Then
      ' PREGUNTAR SI DESEA SOBRESCRIBIR
      m_Sonido.sonar("Question")
      If Message.Question("Ya existe una imagen con el nombre '" & sNombreArchivo & "' en la carpeta media.\n¿Desea sobrescribirla?", "Sí", "No") = 2 Then
        Return
      Endif
    Endif

    ' COPIAR EL ARCHIVO
    Try Copy sRutaImagen To sRutaDestino
    If Error Then
      m_Sonido.sonar("Error")
      Message.Error("No se pudo copiar la imagen: " & Error.Text)
      Return
    Endif
  Endif

  ' CONSTRUIR EL CÓDIGO MARKDOWN Y SHORTCODE
  ' RUTA RELATIVA DESDE EL DOCUMENTO: media/nombre_archivo
  sCodigoMarkdown = "![descripción de la imagen](media/" & sNombreArchivo & ")" & gb.NewLine
  sCodigoMarkdown &= "[Aquí va el shortcode]"

  ' INSERTAR EL CÓDIGO EN EL EDITOR CON POSICIONAMIENTO CORRECTO
  InsertarCodigoImagen(sCodigoMarkdown)

  ' DEVOLVER FOCO AL EDITOR
  txtEditorProyecto.SetFocus

End

' ============================================================================
' FUNCIÓN AUXILIAR PARA VALIDAR POSICIÓN DEL CURSOR
' ============================================================================
' VERIFICA QUE EL CURSOR ESTÉ EN FMAIN.TXTEDITORPROYECTO EN POSICIÓN VÁLIDA
' RETORNA TRUE SI EL CURSOR ESTÁ AL INICIO DE UNA LÍNEA VACÍA
' ============================================================================

Private Function ValidarPosicionCursorImagen() As Boolean

  Dim iLinea As Integer
  Dim iColumna As Integer
  Dim sLineaActual As String
  Dim oEditor As TextEditor

  ' OBTENER REFERENCIA AL EDITOR
  oEditor = FMain.txtEditorProyecto

  ' OBTENER LA POSICIÓN ACTUAL DEL CURSOR
  iLinea = oEditor.Line
  iColumna = oEditor.Column

  ' SI EL EDITOR ESTÁ VACÍO, ES VÁLIDO
  If oEditor.Count = 0 Then Return True

  ' VERIFICAR QUE LA COLUMNA SEA 0 (INICIO DE LÍNEA)
  If iColumna <> 0 Then Return False

  ' OBTENER EL CONTENIDO DE LA LÍNEA ACTUAL
  sLineaActual = oEditor[iLinea].Text

  ' VERIFICAR QUE LA LÍNEA ESTÉ VACÍA (SIN CONTENIDO O SOLO ESPACIOS)
  If Trim(sLineaActual) <> "" Then Return False

  Return True

Catch
  ' SI HAY ERROR AL ACCEDER AL EDITOR, RETORNAR FALSE
  m_Sonido.sonar("Error")
  Message.Error("Error al validar posición del cursor: " & Error.Text)
  Return False

End

' ============================================================================
' FUNCIÓN AUXILIAR PARA INSERTAR EL CÓDIGO DE IMAGEN
' ============================================================================
' INSERTA EL CÓDIGO MARKDOWN EN LA POSICIÓN ACTUAL DEL CURSOR
' ASEGURANDO LÍNEAS VACÍAS ANTES Y DESPUÉS SI ES NECESARIO
' ============================================================================

Private Sub InsertarCodigoImagen(sCodigo As String)

  Dim iLinea As Integer
  Dim sTextoLineaAnterior As String
  Dim sTextoLineaSiguiente As String
  Dim sInsercion As String
  Dim oEditor As TextEditor

  oEditor = FMain.txtEditorProyecto
  iLinea = oEditor.Line

  sInsercion = ""

  ' VERIFICAR LÍNEA ANTERIOR (SI EXISTE Y NO ESTÁ VACÍA)
  If iLinea > 0 Then
    sTextoLineaAnterior = Trim(oEditor[iLinea - 1].Text)
    If sTextoLineaAnterior <> "" Then
      ' AGREGAR LÍNEA VACÍA ANTES
      sInsercion = gb.NewLine
    Endif
  Endif

  ' AGREGAR EL CÓDIGO
  sInsercion &= sCodigo

  ' VERIFICAR LÍNEA SIGUIENTE (SI EXISTE Y NO ESTÁ VACÍA)
  If iLinea < oEditor.Count - 1 Then
    sTextoLineaSiguiente = Trim(oEditor[iLinea + 1].Text)
    If sTextoLineaSiguiente <> "" Then
      ' AGREGAR LÍNEA VACÍA DESPUÉS
      sInsercion &= gb.NewLine & gb.NewLine
    Else
      sInsercion &= gb.NewLine
    Endif
  Else
    ' ESTAMOS EN LA ÚLTIMA LÍNEA
    sInsercion &= gb.NewLine
  Endif

  oEditor.Insert(sInsercion)

End

' ==============================================================================
' FUNCIÓN: vLinea_KeyPress
' DESCRIPCIÓN: POSICIONA EL EDITOR EN LA LÍNEA ESPECIFICADA POR EL USUARIO.
'              SE EJECUTA AL PRESIONAR ENTER EN EL VALUEBOX.
'              LA LÍNEA SOLICITADA QUEDA CENTRADA EN EL ÁREA VISIBLE.
' PARÁMETROS: NINGUNO (USA EL VALOR DE vLinea.Value)
' NOTAS:
'   - EL USUARIO INGRESA LÍNEAS EN BASE 1 (NATURAL PARA EDICIÓN)
'   - INTERNAMENTE TEXTEDITOR USA BASE 0
'   - GotoCenter(X, Y): PRIMER PARÁMETRO ES COLUMNA, SEGUNDO ES LÍNEA
' ==============================================================================
Public Sub vLinea_KeyPress()

  Dim iLinea As Integer

  ' SOLO PROCESAR SI SE PRESIONA ENTER O RETURN
  If Key.Code <> Key.Enter And Key.Code <> Key.Return Then Return

  ' OBTENER LA LÍNEA SOLICITADA (EL USUARIO INGRESA EN BASE 1)
  iLinea = CInt(vLinea.Value)

  ' VERIFICAR QUE LA LÍNEA EXISTA EN EL DOCUMENTO
  If iLinea < 1 Or iLinea > txtEditorProyecto.Count Then
    Message.Info("Línea no disponible")
    Return
  Endif

  ' MOVER EL CURSOR Y CENTRAR LA VISTA EN LA LÍNEA
  ' GotoCenter(Columna, Línea) - COLUMNA 0, LÍNEA EN BASE 0
  txtEditorProyecto.GotoCenter(0, iLinea - 1)

End

' ==============================================================================
' FUNCIÓN: ActualizarContadorLineas
' DESCRIPCIÓN: MUESTRA EL NÚMERO TOTAL DE LÍNEAS DEL DOCUMENTO EN txtLineas.
' ==============================================================================
Public Sub ActualizarContadorLineas()

  txtLineas.Text = txtEditorProyecto.Count & " líneas"

End

Public Sub tbGoTo_Click()

  If tbGoTo.Value Then
    hbGoTo.Visible = True
  Else
    hbGoTo.Visible = False
    vLinea.Text = ""
  Endif

End

Public Sub btnBuscarReemplazar_Click()

  FBuscarReemplazar.ShowModal()

End

Public Sub menuBR_Click()

  FBuscarReemplazar.ShowModal()

End

Public Sub btnComentarios_Click()

  Dim sTextoSeleccionado As String
  Dim iInicio As Integer
  Dim iLongitud As Integer
  Dim sNuevoTexto As String

  ' VALIDACIÓN 1: Verificar que hay texto seleccionado
  If Not txtEditorProyecto.SelectedText
    m_Sonido.sonar("Warning")
    Message.Warning("Debe seleccionar texto para aplicar código de resaltado.")
    Return
  Endif

  ' Obtener el texto seleccionado
  sTextoSeleccionado = txtEditorProyecto.SelectedText

  ' VALIDACIÓN 2: Verificar que no hay líneas vacías en la selección
  If InStr(sTextoSeleccionado, "\n\n") > 0 Or
      InStr(sTextoSeleccionado, "\r\n\r\n") > 0 Then
    m_Sonido.sonar("Warning")
    Message.Warning("La selección contiene líneas vacías." & gb.NewLine &
      "No se puede aplicar código de resaltado a través de párrafos separados.")
    Return
  Endif

  ' GUARDAR POSICIÓN INICIAL DE LA SELECCIÓN
  iInicio = txtEditorProyecto.Column
  iLongitud = Len(sTextoSeleccionado)

  ' APLICAR FORMATO BOLD
  sNuevoTexto = "\\highlight{" & sTextoSeleccionado & "}"

  ' INSERTAR EL TEXTO FORMATEADO
  txtEditorProyecto.Insert(sNuevoTexto)

End

' FUNCIÓN PARA CARGAR LA CONFIGURACIÓN DE TIPOGRAFÍA
Private Sub LoadFontSettings()

  Dim sConfigPath As String
  Dim hSettings As Settings
  Dim sFontName As String
  Dim iFontSize As Integer

  ' CONSTRUIR RUTA DEL ARCHIVO DE CONFIGURACIÓN
  sConfigPath = User.Home &/ ".gbpublisher" &/ "gbpublisher.conf"

  ' VERIFICAR SI EXISTE EL ARCHIVO
  If Exist(sConfigPath) Then

    ' CREAR OBJETO SETTINGS CON LA RUTA DEL ARCHIVO
    hSettings = New Settings(sConfigPath)

    ' LEER VALORES CON VALORES POR DEFECTO
    sFontName = hSettings["Editor/FontName", "Monospace"]
    iFontSize = hSettings["Editor/FontSize", 13]

    ' APLICAR LA FUENTE AL EDITOR
    txtEditorProyecto.Font.Name = sFontName
    txtEditorProyecto.Font.Size = iFontSize

  Else

    ' SI NO EXISTE EL ARCHIVO, APLICAR VALORES POR DEFECTO
    txtEditorProyecto.Font.Name = "Monospace"
    txtEditorProyecto.Font.Size = 13

  Endif

End

' FUNCIÓN PARA GUARDAR LA CONFIGURACIÓN DE TIPOGRAFÍA
Private Sub SaveFontSettings()

  Dim sConfigPath As String
  Dim hSettings As Settings

  ' CONSTRUIR RUTA DEL ARCHIVO DE CONFIGURACIÓN
  sConfigPath = User.Home &/ ".gbpublisher" &/ "gbpublisher.conf"

  ' CREAR OBJETO SETTINGS
  hSettings = New Settings(sConfigPath)

  ' GUARDAR VALORES ACTUALES
  hSettings["Editor/FontName"] = txtEditorProyecto.Font.Name
  hSettings["Editor/FontSize"] = txtEditorProyecto.Font.Size

  ' LOS CAMBIOS SE GUARDAN AUTOMÁTICAMENTE AL ASIGNAR VALORES

End

Public Sub menuTypo_Click()

  ' CONFIGURAR LA FUENTE ACTUAL
  Dialog.Font = FMain.txtEditorProyecto.Font

  ' MOSTRAR EL DIÁLOGO
  Dialog.SelectFont()

  ' APLICAR DIRECTAMENTE LA FUENTE DEL DIÁLOGO
  FMain.txtEditorProyecto.Font = Dialog.Font

  ' GUARDAR LA CONFIGURACIÓN AUTOMÁTICAMENTE
  SaveFontSettings()

  ' DEVOLVER EL FOCO AL EDITOR
  FMain.txtEditorProyecto.SetFocus()

End

Public Sub btnRayaDelMedio_Click()

  Dim sTextoSeleccionado As String
  Dim iInicio As Integer
  Dim iLongitud As Integer
  Dim sNuevoTexto As String

  ' VALIDACIÓN 1: VERIFICAR QUE HAY TEXTO SELECCIONADO
  If Not txtEditorProyecto.SelectedText
    m_Sonido.sonar("Warning")
    Message.Warning("Debe seleccionar texto para aplicar formato rayas del medio.")
    Return
  Endif

  ' OBTENER EL TEXTO SELECCIONADO
  sTextoSeleccionado = txtEditorProyecto.SelectedText

  ' VALIDACIÓN 2: VERIFICAR QUE NO HAY LÍNEAS VACÍAS EN LA SELECCIÓN
  If InStr(sTextoSeleccionado, "\n\n") > 0 Or
      InStr(sTextoSeleccionado, "\r\n\r\n") > 0 Then
    m_Sonido.sonar("Warning")
    Message.Warning("La selección contiene líneas vacías." & gb.NewLine &
      "No se puede aplicar formato de raya del medio  a través de párrafos separados.")
    Return
  Endif

  ' GUARDAR POSICIÓN INICIAL DE LA SELECCIÓN
  iInicio = txtEditorProyecto.Column
  iLongitud = Len(sTextoSeleccionado)

  ' APLICAR FORMATO ITALIC
  sNuevoTexto = "\\rdm{" & sTextoSeleccionado & "}"

  ' INSERTAR EL TEXTO FORMATEADO
  txtEditorProyecto.Insert(sNuevoTexto)

End

Public Sub btnComillas_Click()

  Dim sTextoSeleccionado As String
  Dim iInicio As Integer
  Dim iLongitud As Integer
  Dim sNuevoTexto As String

  ' VALIDACIÓN 1: VERIFICAR QUE HAY TEXTO SELECCIONADO
  If Not txtEditorProyecto.SelectedText
    m_Sonido.sonar("Warning")
    Message.Warning("Debe seleccionar texto para aplicar comillas.")
    Return
  Endif

  ' OBTENER EL TEXTO SELECCIONADO
  sTextoSeleccionado = txtEditorProyecto.SelectedText

  ' ' VALIDACIÓN 2: VERIFICAR QUE NO HAY LÍNEAS VACÍAS EN LA SELECCIÓN
  ' If InStr(sTextoSeleccionado, "\n\n") > 0 Or
  '     InStr(sTextoSeleccionado, "\r\n\r\n") > 0 Then
  '   m_Sonido.sonar("Warning")
  '   Message.Warning("La selección contiene líneas vacías." & gb.NewLine &
  '     "No se puede aplicar comillas a través de párrafos separados.")
  '   Return
  ' Endif

  ' GUARDAR POSICIÓN INICIAL DE LA SELECCIÓN
  iInicio = txtEditorProyecto.Column
  iLongitud = Len(sTextoSeleccionado)

  ' APLICAR FORMATO ITALIC
  sNuevoTexto = "\\enquote{" & sTextoSeleccionado & "}"

  ' INSERTAR EL TEXTO FORMATEADO
  txtEditorProyecto.Insert(sNuevoTexto)

End

Public Sub menuEnDash_Click()

  m_FuncionesGenericas.ReemplazarRdm()

End

Public Sub menuEliminarFootnote_Click()

  m_FuncionesGenericas.EliminarFootnote()

End

Public Sub menuAsistencia_Click()

  FAI.ShowModal()

End

Public Sub menuConversionDirectaTEX_Click()

  Dim rutaOrig As String
  Dim rutaDest As String
  Dim mediaPath As String
  Dim comando As String
  Dim baseName As String

  ' 1. PEDIR ARCHIVO
  Dialog.Title = "Seleccione el documento ODT a convertir"
  Dialog.Filter = ["*.odt", ("Archivos LibreOffice"), ("Todos los archivos")]
  Dialog.AutoExt = True

  If Dialog.OpenFile() Then Return

  rutaOrig = Dialog.Path
  If rutaOrig = "" Then Return

  ' 2. NOMBRE BASE (SIN EXTENSIÓN)
  baseName = File.BaseName(rutaOrig)

  ' 3. ARCHIVO DE SALIDA (MISMO DIRECTORIO)
  rutaDest = File.Dir(rutaOrig) &/ baseName & ".tex"

  ' 4. DIRECTORIO PARA IMÁGENES (AISLADO)
  mediaPath = File.Dir(rutaOrig) &/ baseName & "-media"

  ' 5. CONSTRUIR Y EJECUTAR PANDOC
  comando = "pandoc " & Quote$(rutaOrig) &
    " -o " & Quote$(rutaDest) &
    " --from=odt --to=latex" &
    " --wrap=none" &
    " --extract-media=" & Quote$(mediaPath)

  Shell comando Wait

  If Error Then
    m_Sonido.sonar("Error")
    Message.Error("Error al ejecutar pandoc:" & gb.NewLine & Error.Text)
    Return
  Endif

  ' 6. VERIFICAR RESULTADO
  If Exist(rutaDest) Then
    m_Sonido.sonar("Info")
    Message.Info("Conversión finalizada." & gb.NewLine &
      "Archivo: " & rutaDest & gb.NewLine &
      "Imágenes: " & mediaPath)
  Else
    m_Sonido.sonar("Error")
    Message.Error("No se generó el archivo .tex")
  Endif

End

Public Sub menuConversionDirectaMD_Click()

  Dim rutaOrig As String
  Dim rutaDest As String
  Dim mediaPath As String
  Dim comando As String
  Dim baseName As String

  ' 1. PEDIR ARCHIVO
  Dialog.Title = "Seleccione el documento ODT a convertir a Markdown"
  Dialog.Filter = ["*.odt", ("Archivos LibreOffice"), ("Todos los archivos")]
  Dialog.AutoExt = True

  If Dialog.OpenFile() Then Return

  rutaOrig = Dialog.Path
  If rutaOrig = "" Then Return

  ' 2. NOMBRE BASE (SIN EXTENSIÓN)
  baseName = File.BaseName(rutaOrig)

  ' 3. ARCHIVO DE SALIDA (MISMO DIRECTORIO)
  rutaDest = File.Dir(rutaOrig) &/ baseName & ".md"

  ' 4. DIRECTORIO PARA IMÁGENES (AISLADO)
  mediaPath = File.Dir(rutaOrig) &/ baseName & "-media"

  ' 5. CONSTRUIR Y EJECUTAR PANDOC
  comando = "pandoc " & Quote$(rutaOrig) &
    " -o " & Quote$(rutaDest) &
    " --from=odt --to=markdown" &
    " --standalone" &
    " --wrap=none" &
    " --extract-media=" & Quote$(mediaPath)

  Shell comando Wait

  If Error Then
    m_Sonido.sonar("Error")
    Message.Error("Error al ejecutar pandoc:" & gb.NewLine & Error.Text)
    Return
  Endif

  ' 6. VERIFICAR RESULTADO
  If Exist(rutaDest) Then
    m_Sonido.sonar("Info")
    Message.Info("Conversión finalizada." & gb.NewLine &
      "Archivo: " & rutaDest & gb.NewLine &
      "Imágenes: " & mediaPath)
  Else
    m_Sonido.sonar("Error")
    Message.Error("No se generó el archivo .md")
  Endif

End
