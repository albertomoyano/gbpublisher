' Gambas class file

' Formulario que pide el nombre del archivo nuevo
Public RutaDestino As String ' Ruta donde se creará el archivo
Public ArchivoCreado As String ' Nombre final del archivo creado

Public Sub Form_Open()

  txtNombreArchivo.SetFocus()

End

' Validar que solo se puedan escribir letras a-z, números, punto y permitir teclas de control
Public Sub txtNombreArchivo_KeyPress()
  ' Permitir teclas de control (Backspace, Delete, flechas, etc.)

  If Key.Code = Key.BackSpace Or Key.Code = Key.Delete Or Key.Code = Key.Left Or Key.Code = Key.Right Or Key.Code = Key.Home Or Key.Code = Key.End Then
    Return ' Permite estas teclas
  Endif

  ' Validar solo caracteres permitidos para el nombre del archivo
  If Not InStr("0123456789abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ.", Key.Text) Then
    Stop Event ' Bloquea la tecla
  Endif

End

Public Sub btnCrearArchivo_Click()

  Dim rutaCompleta As String
  Dim partes As String[]
  Dim extension As String

  If txtNombreArchivo.Text = "" Then Return

  ' Validar que el nombre tenga una extensión válida
  partes = Split(txtNombreArchivo.Text, ".")
  If partes.Count < 2 Then
    Message.Warning("El archivo debe tener una extensión válida (ej: .js .tex, .html).")
    Return
  Endif

  ' Obtener la extensión (última parte después del punto)
  extension = partes[partes.Count - 1]

  ' Validar que la extensión tenga entre 2 y 4 caracteres
  If Len(extension) < 2 Or Len(extension) > 4 Then
    Message.Warning("La extensión debe tener entre 2 y 4 caracteres (ej: .js .tex, .html).")
    Return
  Endif

  rutaCompleta = RutaDestino &/ txtNombreArchivo.Text
  If Exist(rutaCompleta) Then
    Message.Warning("Ya existe un archivo con ese nombre.")
    Return
  Endif

  ' Crear archivo vacío
  File.Save(rutaCompleta, "")
  ArchivoCreado = rutaCompleta
  Me.Close()

End
