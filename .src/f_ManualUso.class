' Gambas class file

Public Sub Form_Open()

  SplitterManualUso.Layout = [2, 7]
  CargarListaManual()

End

Public Sub btnCerrar_Click()

  Me.Close

End

Public Sub SplitterManualUso_Resize()

  SplitterManualUso.Layout = [2, 7]

End

Public Sub ListBoxManualUso_Click()

  Dim sArchivoMD As String
  Dim sRutaCompleta As String
  Dim sContenidoMD As String
  Dim sHTML As String
  Dim hFile As File

  If ListBoxManualUso.Index = -1 Then
    Return
  Endif

  sArchivoMD = ListBoxManualUso[ListBoxManualUso.Index].Text
  sRutaCompleta = User.Home &/ ".gbpublisher/manual/" &/ sArchivoMD

  If Not Exist(sRutaCompleta) Then
    Message.Error("El archivo no existe: " & sRutaCompleta)
    Return
  Endif

  hFile = Open sRutaCompleta For Read

  sContenidoMD = Read #hFile, Lof(hFile)
  Close #hFile

  ' Convertir a HTML
  sHTML = Markdown.ToHTML(sContenidoMD)

  ' Usar la función que genera el HTML completo con CSS
  WebViewManualUso.SetHTML(GenerarHTMLCompleto(sHTML))

End

Public Sub ButtonBuscarManual_Click()

  Dim sDir As String
  Dim aArchivos As String[]
  Dim sArchivo As String
  Dim sRutaCompleta As String
  Dim sContenido As String
  Dim hFile As File
  Dim sTerminoBusqueda As String
  Dim aResultados As New String[]

  sTerminoBusqueda = Trim(TextBoxBuscar.Text)

  If sTerminoBusqueda = "" Then
    Message.Warning("Por favor ingrese un término de búsqueda")
    Return
  Endif

  sDir = User.Home &/ ".gbpublisher/manual/"

  If Not Exist(sDir) Then
    Message.Warning("No se encontró el directorio del manual")
    Return
  Endif

  aArchivos = Dir(sDir, "*.md")

  For Each sArchivo In aArchivos
    sRutaCompleta = sDir &/ sArchivo

    Try hFile = Open sRutaCompleta For Read
    If Not Error Then
      sContenido = Read #hFile, Lof(hFile)
      Close #hFile

      If InStr(Lower(sContenido), Lower(sTerminoBusqueda)) > 0 Then
        aResultados.Add(sArchivo)
      Endif
    Endif
  Next

  ListBoxManualUso.Clear()

  If aResultados.Count = 0 Then
    Message.Info("No se encontraron resultados para: " & sTerminoBusqueda)
  Else
    For Each sArchivo In aResultados
      ListBoxManualUso.Add(sArchivo)
    Next
    Message.Info("Se encontraron " & aResultados.Count & " archivo(s)")
  Endif

End

Public Sub ButtonLimpiarBusqueda_Click()

  TextBoxBuscar.Text = ""
  CargarListaManual()

End

Public Sub CargarListaManual()

  Dim sDir As String
  Dim aArchivos As String[]
  Dim sArchivo As String

  sDir = User.Home &/ ".gbpublisher/manual/"

  ' Verificar que el directorio existe
  If Not Exist(sDir) Then
    Message.Warning("No se encontró el directorio del manual")
    Return
  Endif

  ' Limpiar el ListBox
  ListBoxManualUso.Clear()

  ' Buscar todos los archivos .md
  aArchivos = Dir(sDir, "*.md")

  ' Agregar cada archivo al ListBox
  For Each sArchivo In aArchivos
    ListBoxManualUso.Add(sArchivo)
  Next

End

Public Function GenerarHTMLCompleto(sContenido As String) As String

  Dim sCSS As String

  sCSS = "body { font-family: Arial, sans-serif; Line-height: 1.5; margin: 4px; }" &
    "h1 { font-size: 2em; color: #6b3f2a; }" &
    "h2 { font-size: 1.5em; color: #6b3f2a; }" &
    "h3 { font-size: 1.3em; color: #6b3f2a; }" &
    "p { Line-height: 1.5em; font-size: 1.2em; }"

  Return "<html><head>" &
    "<meta charset='UTF-8'>" &
    "<style>" & sCSS & "</style>" &
    "</head><body>" & sContenido & "</body></html>"

End
